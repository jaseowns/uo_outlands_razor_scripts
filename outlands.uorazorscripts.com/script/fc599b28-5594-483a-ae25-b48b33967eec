# This is an automated backup - check out https://outlands.uorazorscripts.com/script/fc599b28-5594-483a-ae25-b48b33967eec for latest
# Automation by Jaseowns
## Script: Bapeth's One-Tile Item Dumper
## Created by: barryroser#0
#############################################

# Bapeths One Tile Item Dump & Restock
#
# November 10th 2025 - Added wand select
#
# This script will quickly dump and sort all items you acquire while adventuring in Outlands
#
# "READ THIS:"
#
# When you run the script for the first time you will be asked to select two containers "Main Loot Container" and "Second Loot Container" - These containers are for storing loot
# You will also be asked to select a "Wand Container" - This is for the optional ID wand loadout restocker
#
# If you wish to use the ID Wand restocker "open the Atlas" then play the script to set or remove wand types - You must also keep your "Wand Container" stocked with ID Wands and/or Container ID wands
# Type "[Atlas" in game to spawn a free weightless "Atlas" in your bag
#
# If you ever need to reset storage items, tomes, containers etc.. delete the variables in "Script Options"
# The "Script Options" tab can be found in the top left of razor while the "Scripts" tab is focused
#
# "Auto Loot Control"
# This script uses razor "Auto-Queue Object Delay" setting
# Make sure this setting is turned ON in the razor Options tab > Targeting & Queues sub-tab
# Set "object delay to 503"
#
# "For Houses:"
# You must have the "Warp Tile unlocked" and marked within 2 tiles of all storage items
# Your house rune must be named "House" in your runebook/tome
# "Peace Mode" Will recall to your dump & restock spot and proceed to dump & restock.
# "War Mode" Recall is blocked, the script assumes you are already in range of your containers and proceeds to dump & restock
#
# "For Inn Rooms:"
# If you use an Inn room make sure you do not have a rune named "House" anywhere in your backpack
# Stand within 2 tiles of all storage items and play the script
#
# "Note on Magic Item Vault"
# The script will block dumping items into the vault if you have any bard skills or tactics - This is to prevent absorbing your magic instruments/weapons
#
# "Note on Harpooners"
# If you have more than 80 fishing and 80 tactics the script will add 3x "Rage Runes" to your loadout - used for melee damage on ships
#
# "***REQUIRED***"
#
# Items this script uses are as follows:
# 
# Do not put any of these items inside containers, although some of them may work while in secure containers
# All of these items should be locked down/secured in your inn room or house
# Arrange these items so that your character can reach them from your house rune spot/inn room warp point within 2 tiles
#   
# "Two containers of any kind" - locked down and secured in your house/inn room
# "A rare cloth tome"
# "A dyes tome"
# "An arcane rune tome"
# "A treasure map tome"
# "A ship upgrade tome"
# "A skill mastery tome"
# "A collectable card tome"
# "An ore map tome"
# "A skinning map tome"
# "A lumber map tome"
# "A fishing map tome"
# "A storage shelf"
# "A bank deposit box"
# "A garden shelf"
# "A resource stockpile"
# "A magic item recycler"
# "A magic item vault"
# "A repair bench"
# "A flooring rack"
# "A rune tome/book" - with a rune marked to your dump & restock spot
#
# Script starts here...

@clearignore 
if not varexist lootchestdump
    overhead "Set Main Loot Container" 88
    hotkey "Cancel Current Target"
    setlasttarget 
    wft 1000
    while targetexists      
        //donothing
    endwhile
    @setvar "lootchestdump" lasttarget 
    overhead "Loot Container Set" 77
endif
if not varexist lootchestdump2
    overhead "Set Second Loot Container" 88
    hotkey "Cancel Current Target"
    setlasttarget 
    wft 1000
    while targetexists      
        //donothing
    endwhile
    @setvar "lootchestdump2" lasttarget 
    overhead "Loot Container Set" 77
endif
if not varexist wandcontainer
    overhead "Set Wand Container" 88
    hotkey "Cancel Current Target"
    setlasttarget 
    wft 1000
    while targetexists      
        //donothing
    endwhile
    @setvar "wandcontainer" lasttarget 
    overhead "Wand Container Set" 77
endif
if not varexist rareclothtome
    if findtype "book" ground 1495 -1 2 as book
        @setvar "rareclothtome" book
    endif
endif
if not varexist flooringrack
    if findtype "carpet rack" ground -1 -1 2 as book
        @setvar "flooringrack" book
    endif
endif
if not varexist dyestome
    if findtype "book" ground 1494 -1 2 as book
        @setvar "dyestome" book
    endif
endif
if not varexist arcanerunetome
    if findtype "book" ground 2085 -1 2 as book
        @setvar "arcanerunetome" book
    endif
endif
if not varexist treasuremaptome
    if findtype "book" ground 2990 -1 2 as book
        @setvar "treasuremaptome" book
    endif
endif
if not varexist shipupgradetome
    if findtype "book" ground 2877 -1 2 as book
        @setvar "shipupgradetome" book
    endif
endif
if not varexist skillmasterytome
    if findtype "book" ground 2963 -1 2 as book
        @setvar "skillmasterytome" book
    endif
endif
if not varexist collectablecardtome
    if findtype "book" ground 2141 -1 2 as book
        @setvar "collectablecardtome" book
    endif
endif
if not varexist oremaptome
    if findtype "book" ground 2796 -1 2 as book
        @setvar "oremaptome" book
    endif
endif
if not varexist skinningmaptome
    if findtype "book" ground 2651 -1 2 as book
        @setvar "skinningmaptome" book
    endif
endif
if not varexist lumbermaptome
    if findtype "book" ground 2799 -1 2 as book
        @setvar "lumbermaptome" book
    endif
endif
if not varexist fishingmaptome
    if findtype "book" ground 2722 -1 2 as book
        @setvar "fishingmaptome" book
    endif
endif
if not varexist repbench
    if findtype "repair bench" ground -1 -1 2 as rbench
        @setvar "repbench" rbench
    endif
endif
if not varexist deposafe
    if findtype "bank deposit safe" ground -1 -1 2 as bsafe
        @setvar "deposafe" bsafe
    endif
endif
if not varexist myshelf
    if findtype "storage shelf" ground -1 -1 2 as shelf
        @setvar "myshelf" shelf
    endif
endif
if not varexist gardenshelf
    if findtype "barrel planter shelf|garden shelf" ground -1 -1 2 as gshelf
    @setvar "gardenshelf" gshelf
    endif
endif
if not varexist respile
    if findtype "resource container" ground -1 -1 2 as pile
    @setvar "respile" pile
    endif
endif
if not varexist magicrecycler
    if findtype "magic item recycler|ornate elven chest" ground -1 -1 2 as recycle
        @setvar "magicrecycler" recycle
    endif
endif
if not varexist magicchest
    if findtype "sm magic item vault|magic item chest" ground -1 -1 2 as mchest
        @setvar "magicchest" mchest
    endif
endif

if gumpexists 341416395
    gumpclose 341416395
    if varexist regwand
        @unsetvar regwand
    endif
    if varexist conwand
        @unsetvar conwand
    endif
    overhead "Select wand type for loadout" 88
    overhead "Select backpack for no wand"78
    pause 250
    hotkey "Set Last Target"
    wft 1000
    while targetexists 
        //donothing
    endwhile
    pause 250
    getlabel lasttarget desc
    if "backpack" in desc
        //donothing
    elseif "container" in desc
        @setvar conwand lasttarget 
    elseif "item" in desc
        @setvar regwand lasttarget 
    endif
endif

if not warmode
    //Recalling to dump loot
    if skill "Magery" <= 60
        say "[recallcharge House"
        pause 3000
    endif
    if skill "Magery" >= 60
        say "[recall House"
        pause 3000
    endif
endif

warmode off

if skill "Magery" >= 30 and mana >= 4 and not findbuff "Reactive Armor"
    cast 'Reactive Armor'
endif

if varexist repbench
    dclick repbench
    pause 2000
    getlabel backpack ping
endif

while findtype "iron ore" backpack as ore
    drop backpack
    lift ore 60000
    drop magicrecycler -1 -1 0
    while queued
        //donothing
    endwhile
endwhile

while findtype "arcane essence" backpack as bessence
    drop backpack
    lift bessence 60000
    drop magicrecycler -1 -1 0
    while queued
        //donothing
    endwhile
endwhile

while findtype 5901|5899|5903|5905 backpack as footwear
    getlabel footwear desc
    if "[blessed]" in desc
        @ignore footwear
    elseif findtype "scissors" backpack as footcutter
        while queued
            //donothing
        endwhile
        dclick footcutter
        wft 1000
        target footwear
        pause 500
        getlabel backpack ping
    else
        drop backpack
        lift footwear 60000
        drop lootchestdump2 -1 -1 0
    endif
endwhile

//Dumping Gear
hotkey "Cancel Current Target"
menu myshelf 0
wft 1000
target self
pause 500
getlabel backpack ping
gumpclose 3232825965
hotkey "Cancel Current Target"

//Dumping all items
menu respile 0
wft 1000
target 'self'
hotkey "Cancel Current Target"
menu gardenshelf 0
wft 1000
target 'self'
hotkey "Cancel Current Target"
menu flooringrack 0
wft 1000
target 'self'
hotkey "Cancel Current Target"
menu rareclothtome 0
menu dyestome 0
menu arcanerunetome 0
menu treasuremaptome 0
menu shipupgradetome 0
menu skillmasterytome 0
menu collectablecardtome 0
menu oremaptome 0
menu skinningmaptome 0
menu lumbermaptome 0
menu fishingmaptome 0
menu magicrecycler 1
menu deposafe 0

if skill "Discordance" <= 50 or skill "Peacemaking" <= 50 or skill "Provocation" <= 50 or "Tactics" <= 50
    menu magicchest 0
    wft 1000
    target self
    hotkey "Cancel Current Target"
endif

while findtype "void orb|research materials|spell hue deed|black market goods|redline bottle" backpack as chest1dump
    drop backpack
    lift chest1dump 60000
    drop lootchestdump -1 -1 0
    while queued
        //donothing
    endwhile
endwhile

while findtype "15178|card|soul jar|candy corn|colonial marks|27612|eggplant seeds|bag of gems|17619|17617|2508|7710|3712|3648|3650|3708|3648|2475|3649|29833|29832|2463" backpack as chest2dump
    drop backpack
    lift chest2dump 60000
    drop lootchestdump2 -1 -1 0
    while queued
        //donothing
    endwhile
endwhile

while findtype "3530" backpack as bspecialnets
    getlabel bspecialnets desc
    if "mastercrafted" in desc
        @ignore bspecialnets
    else
        drop backpack
        lift bspecialnets 60000
        drop lootchestdump2 -1 -1 0
        while queued
            //donothing
        endwhile
    endif
endwhile

//Resupply
hotkey "Cancel Current Target"
menu myshelf 1


if skill "Magery" >= 80 and mana >= 14 and not findbuff "Magic Reflection"
    cast 'Magic Reflection'
endif

if counttype "20496" >= 2 backpack
    //do nothing
elseif varexist "regwand"
    dclick "wandcontainer"
    pause 500
    getlabel backpack ping
    while counttype "20496" < 2
        if findtype "20496" "wandcontainer" as newwand
            drop backpack
            lift newwand
            if findtype "bag" backpack as wandbag
                drop wandbag
            else
                drop backpack
            endif
            pause 500
            getlabel backpack ping
        endif
    endwhile
endif
if counttype "20495" >= 2 backpack
    //do nothing
elseif varexist "conwand"
    while counttype "20495" < 2
        if findtype "20495" "wandcontainer" as newwand
            drop backpack
            lift newwand
            if findtype "bag" backpack as wandbag
                drop wandbag
            else
                drop backpack
            endif
            pause 500
            getlabel backpack ping
        endif
    endwhile
endif

if skill "Fishing" >= 80 and skill "Tactics" >= 80
    dclick arcanerunetome
    waitforgump 3561255175 500
    gumpresponse 18
    waitforgump 3561255175 500
    gumpresponse 18
    waitforgump 3561255175 500
    gumpresponse 18
    waitforgump 3561255175 500
    gumpclose 3561255175
endif

clearall
@clearignore 
overhead '********************' 181
overhead '**DUMP COMPLETE**' 181
overhead '********************' 181