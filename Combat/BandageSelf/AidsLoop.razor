if not timerexists "aids"
    createtimer "aids"
    settimer "aids" 11000
endif

if not timerexists "healpot"
    createtimer "healpot"
    settimer "healpot" 8000
endif

while not dead
    //if paralyzed // faster but only works on the beta client
    if findbuff "Paralyze"
        overhead 'Drinking cures!'
        say "[pouch"
        wait 200
    endif

    if findtype "clean bandage%s%" backpack
        if hp < maxhp

            if hp < 45
                if findtype "Yellow Potion" backpack

                    if timer "healpot" >= 8000
                        overhead 'Drinking heal!'
                        potion "heal"
                        wait 200
                        settimer "healpot" 0
                    endif
                else
                    overhead 'Out of heals!' 34
                endif
            endif

            if poisoned and hp < 60
                if findtype "Orange Potion" backpack
                    overhead 'Drinking cures!'
                    potion "cure"
                    wait 200
                else
                    overhead 'Out of cures!' 34
                endif
            endif

            if timer "aids" >= 11000
                dclicktype "clean bandage%s%" backpack
                waitfortarget
                hotkey "Target Self"
                wait 200
                if insysmsg "begin applying"
                    settimer "aids" 0
                endif
            endif
        endif
    else
        overhead "No aids!" 34
    endif
    wait 1000
endwhile
