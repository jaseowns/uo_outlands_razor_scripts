# This is an automated backup - check out https://outlands.uorazorscripts.com/script/ac637102-a701-4680-a780-faf3efb5c274 for latest
# Automation by Jaseowns
## Script: Bapeth's Nox Trapper Background Script
## Created by: barryroser#0
#############################################

# Bapeths Nox Trapper Background Script
# 
# Keep this script playing at all times
# If you use another script, such as "BOOM BOOM by Bapeth"
# Loop this background script at the end of your other script(s)
#
# This script auto handles buffs and debuffs
#
# Script starts here...

if skill "Spirit Speak" >= 80 and findtype "an earth elemental" ground -1 -1 1 as Summon
    if not find "bsummon1" ground -1 -1
        while findtype "an earth elemental" ground -1 -1 1 as bsummon1
            if noto bsummon1 = "friend"
                @setvar "bsummon1" bsummon1
                # Name your summon if you like (no spaces must be unique)
                rename bsummon1 "LootGolem"
            endif
            break
        endwhile
        getlabel backpack ping
    elseif not find "bsummon2" ground -1 -1
        while findtype "an earth elemental" ground -1 -1 1 as bsummon2
            if noto bsummon2 = "friend"
                @setvar "bsummon2" bsummon2
                # Name your summon if you like (no spaces must be unique)
                rename bsummon2 "LootGolem"
            endif
            break
        endwhile
    endif
endif

if not timerexists rptimer
    createtimer rptimer
    settimer rptimer 1200
endif

if not find "placedtrap" ground and findtype 51163 ground -1 -1 8 as smokingtrap
    @setvar "placedtrap" smokingtrap
endif

if not timerexists armedtimer
    createtimer armedtimer
    settimer armedtimer 3000
endif
if find "placedtrap" ground as smokingtrap and timer armedtimer >= 3000
    overhead "*Armed*" 38 smokingtrap
    settimer armedtimer 0
endif

if skill "Alchemy" >= 10 and not hidden
    if not findbuff "Strength" and findtype "White Potion" backpack as wpot
        dclick wpot
        pause 500
        getlabel backpack ping
    endif
    if not findbuff "Agility" and findtype "Blue Potion" backpack as bpot
        dclick bpot
        pause 500
        getlabel backpack ping
    endif
    if not findbuff "Magic Resist Potion" and findtype "Black Potion" backpack as mpot
        dclick mpot
        pause 500
        getlabel backpack ping
    endif
endif

if skill "Magery" >= 80
    if timerexists reflect and not findbuff "Magic Reflection"
        if timer reflect > 30000
            overhead "Magic Reflect is ready..." 201
            settimer reflect 12000
        endif
    endif
    if findbuff "Magic Reflection" and timerexists reflect
        removetimer reflect
    endif
    if not findbuff "Magic Reflection" and not timerexists reflect
        createtimer reflect
    endif
endif

if skill "Magery" >= 20
    if timerexists reactive and not findbuff "Reactive Armor"
        if timer reactive > 30000
            overhead "Reactive Armor is ready..." 139
            settimer reactive 13500
        endif
    endif
    if findbuff "Reactive Armor" and timerexists reactive
        removetimer reactive
    endif
    if not findbuff "Reactive Armor" and not timerexists reactive
        createtimer reactive
    endif
endif

if not hidden and not timerexists brew and findbuff "Bleed" and findtype 50675 backpack as cbrew
    pause 1200
    dclick cbrew
    createtimer brew
elseif not hidden and not timerexists brew and findbuff "Diseased" and findtype 50675 backpack as cbrew
    while queued
        //donothing
    endwhile
    pause 700
    dclick cbrew
    createtimer brew
endif
if timerexists brew
    if timer brew >= 120000
        removetimer brew
    endif
endif

if not hidden and findbuff "Weaken" and not targetexists and findtype "White Potion" backpack as stpot
    while queued
        //donothing
    endwhile
    dclick stpot
endif

if not hidden and insysmsg "You are carrying too much weight" and not findbuff "Strength" and findtype "White Potion" backpack as wpot
    while queued
        //donothing
    endwhile
    dclick wpot
endif

if not hidden and stam <= 1 and timer rptimer > 1200 and findbuff "Strength" and findtype "Red Potion" backpack as redpot
    if counttype "Red Potion" >= 1
        while queued
            //donothing
        endwhile
        dclick redpot
        settimer rptimer 0
    endif
endif

if not hidden and paralyzed and not targetexists and findtype "pouch" backpack 38
    say "[Pouch"
endif

if not hidden and poisoned and not targetexists and findtype "Orange Potion" backpack as curepot
    while queued
        //donothing
    endwhile
    dclick curepot
endif
loop