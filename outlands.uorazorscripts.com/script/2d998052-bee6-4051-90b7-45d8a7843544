# This is an automated backup - check out https://outlands.uorazorscripts.com/script/2d998052-bee6-4051-90b7-45d8a7843544 for latest
# Automation by Jaseowns
## Script: Silent Pet Commands using Menu (Earth/Mummy only atm)
## Created by: Jaseowns#6694
#############################################

# Silent Pet Commands using Menu by Jaseowns
# UO Outlands
# WIP: ONLY EARTH ELEMENTAL OR MUMMY ATM
# Live stream: https://youtube.com/live/xdcZhPSpuiM?feature=share

// Copy/Paste as many times as you need
// Set Hotkey Type Based on Your Preference
// Hotkey Type
// -- 1 = Kill
// -- 2 = Patrol
// -- 3 = Guard
// -- 4 = Follow
// -- 5 = Come
// -- 6 = Move
// -- 7 = Stay
@setvar! jaseowns_HotKeyType 3

if not varexist jaseowns_PetOne
    @setvar! jaseowns_PetOne 0
endif


@clearignore 
while jaseowns_PetOne = 0 and findtype 158|14 ground -1 -1 2 as jaseowns_PotentialPet
    if noto jaseowns_PotentialPet = "friend"
        overhead "Potential Pet" 66 jaseowns_PotentialPet
        rename jaseowns_PotentialPet "Jaseowns"
        getlabel jaseowns_PotentialPet jaseowns_PetDescription 
        if "Jaseowns" in jaseowns_PetDescription or insysmsg "That name is unacceptable"
            @setvar! jaseowns_PetOne jaseowns_PotentialPet    
        else
            @ignore jaseowns_PotentialPet
        endif
    else
        @ignore jaseowns_PotentialPet
    endif
endwhile
if find jaseowns_PetOne ground -1 -1 18
    menu jaseowns_PetOne jaseowns_HotKeyType
    if jaseowns_HotKeyType = 1
        overhead "ý Attacking ý" 88 jaseowns_PetOne    
    elseif jaseowns_HotKeyType = 2
        overhead "ý Patrolling ý" 88 jaseowns_PetOne    
    elseif jaseowns_HotKeyType = 3
        overhead "ý Guarding ý" 88 jaseowns_PetOne
    elseif jaseowns_HotKeyType = 4
        overhead "ý Following ý" 88 jaseowns_PetOne
    elseif jaseowns_HotKeyType = 5
        overhead "ý Coming ý" 88 jaseowns_PetOne
    elseif jaseowns_HotKeyType = 6
        overhead "ý Moving ý" 88 jaseowns_PetOne
    elseif jaseowns_HotKeyType = 7
        overhead "ý Staying ý" 88 jaseowns_PetOne
    endif
    if timerexists jaseowns_NoFoundPetTimer
        removetimer jaseowns_NoFoundPetTimer
    endif
else
    sysmsg "No pet found..." 38
    if not timerexists jaseowns_NoFoundPetTimer
        settimer jaseowns_NoFoundPetTimer 0
    endif
    
    if timer jaseowns_NoFoundPetTimer >= 5000
        unsetvar jaseowns_PetOne
    endif
    getlabel backpack jaseowns_PingCheck
    replay
endif
@clearignore 

# we want to use the context menu 
# of our pets to attack
#  -- all of them at once?
#  -- individual keys?
#  -- if pet dies, auto retarget
####
# How to handle pet off screen but still around?
#  - a timer... if pet off screen for certain time then retry?
#  - followers changing.. did it die?
#  --- if timer, followers a certain for a certain time, then it chagnes? dead?
