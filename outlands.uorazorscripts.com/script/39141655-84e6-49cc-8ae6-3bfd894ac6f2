# This is an automated backup - check out https://outlands.uorazorscripts.com/script/39141655-84e6-49cc-8ae6-3bfd894ac6f2 for latest
# Automation by Jaseowns
## Script: Ultimate Thief Trainer
## Created by: Jaseowns#6694
#############################################

# Ultimate Thief Trainer by Jaseowns
# UO Outlands

@setvar! trainHiding 1
@setvar! trainTracking 1
@setvar! trainCamping 1
@setvar! trainStealth 1
@setvar! trainStealingAndSnoopingOnDip 1

if not timerexists jaseTrainingTimer 
    settimer jaseTrainingTimer 11000
endif

if not timerexists jaseTrainingDipperTimer 
    settimer jaseTrainingDipperTimer 11000
endif

while not dead
    if trainTracking = 1
        if not findbuff "tracking"
            clearsysmsg
            while not gumpexists 4267467659
                useskill 'tracking'
                wait 200
            endwhile
            if gumpexists 4267467659
                while not insysmsg "You will now hunt all hostile players."
                    gumpresponse 8 4267467659
                    waitforgump 4267467659 5000
                endwhile
                gumpresponse 6 4267467659
                wait 200
            endif
            gumpclose 4267467659
        endif
    endif

    if trainCamping = 1
        if not findtype "campfire" ground -1 -1 2
            if findtype "kindling" backpack as jindling
                dclick jindling
            else
                overhead "We have no kindling" 34
            endif
        endif
    endif

    if trainStealingAndSnoopingOnDip = 1
        if findtype "pickpocket dip" ground -1 -1 1 as dipper
            if timer jaseTrainingDipperTimer >= 10500
                dclick dipper
                settimer jaseTrainingDipperTimer 0
            endif
        endif
    endif

    if trainHiding = 1
        if timer jaseTrainingTimer >= 10500
            if skill "hiding" < 80 or not hidden
                useskill 'Hiding'
                settimer jaseTrainingTimer 0
            endif
        endif

        if timer jaseTrainingTimer >= 10500
            if hidden and trainStealth = 1 and skill 'stealth' < 100
                useskill 'Stealth'
                settimer jaseTrainingTimer 0
            else
                useskill 'Hiding'
                settimer jaseTrainingTimer 0
            endif
        endif
    endif

    wait 200
endwhile