# This is an automated backup - check out https://outlands.uorazorscripts.com/script/df86f94a-832a-496d-b65c-34bc2f7f97ab for latest
# Automation by Jaseowns
## Script: Ultimate Recall (Recall, Runetome, Runebook, Chivalry)
## Created by: special_sy#0
#############################################

# Made by Chumber
# Based on https://outlands.uorazorscripts.com/script/fb2d9f3c-0f8a-4de6-b8f3-f5c07b790d8c
# Expanded with the runetome and swapped the order (runebook before chiv)
# Added all follow me if you got followers

if followers > 0
    setvar! allFollowMe 1
else
    setvar! allFollowMe 0
endif

if findtype "blue moongate" ground 0 1 2 as mGate
    while not gumpexists 3899019871
        dclick mGate
    endwhile
    if gumpexists 3899019871
        if allFollowMe = 1
            say "all follow me"
        endif
        gumpresponse 2 3899019871
        stop
    endif
elseif findtype "blue moongate" ground -1 1 2 as mGate
    while not gumpexists 3773199800
        dclick mGate
    endwhile
    if gumpexists 3773199800
        if allFollowMe = 1
            say "all follow me"
        endif
        wait 200
        gumpresponse 2 3773199800
        stop
    endif
elseif skill "Magery" > 70 and mana > 11 and not findtype "blue moongate" ground 0 1 8
    while not targetexists
        cast "Recall"
        wait 200
    endwhile
    if findtype 8901 backpack as portTarget
        if allFollowMe = 1
            say "all follow me"
        endif
        wait 200
        target portTarget
        stop
    elseif findtype 7956 backpack as portTarget
        target portTarget
        stop
    endif
elseif findtype 29103 backpack as runetome and not findtype "blue moongate" ground 0 1 8
    dclick runetome
    waitforgump 167090027 1000
    if gumpexists 167090027
        if allFollowMe = 1
            say "all follow me"
        endif
        gumpresponse 100
        stop
    endif
elseif findtype 8901 backpack as runebook and not findtype "blue moongate" ground 0 1 8
    //change gumpresponse to number correlating to rune in book
    //Page 1: 2,6,14,20,26,32,38,44  Page 2: 50,56,62,68,74,80,86,92
    dclick runebook
    waitforgump 1551740969 1000
    if gumpexists 1551740969
        if allFollowMe = 1
            say "all follow me"
        endif
        gumpresponse 2
        stop
    endif

elseif skill "Chivalry" > 95 and not findtype "blue moongate" ground 0 1 8
    overhead 'Chivescape!'
    if not gumpexists 1482653814
        say '[SacredJourney'
    elseif gumpexists 1482653814
        gumpresponse 1
    endif
    if findtype 30528 ground 2091 1 2 as mgate
        dclick mgate
    endif
    if gumpexists 3773199800
        if allFollowMe = 1
            say "all follow me"
        endif
        wait 200
        gumpresponse 2
        stop
    endif
endif
wait 250
loop