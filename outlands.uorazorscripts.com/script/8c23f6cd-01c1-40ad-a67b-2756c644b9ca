# This is an automated backup - check out https://outlands.uorazorscripts.com/script/8c23f6cd-01c1-40ad-a67b-2756c644b9ca for latest
# Automation by Jaseowns
## Script: Training Poisoning with no shelf
## Created by: Jaseowns#6694
#############################################

# Training Poisoning with no shelf by Jaseowns
# UO Outlands - Updated 12/09/2021 for timeout issue
#   Updated 5/12/2025 - system message update for kegs
#   Updated 1/17/2022 for making sure keg containers are set
# Make sure to update the max skill on line 59 if you want to go over 100 
#################
# Requirements:  separate bags containing the different types of poisons, 
# heals and cures optional but at least someone healing you on a loop
# one weapon (ex: kryss selected by default)
##################
#   One Empty bottle in your backpack
#   Each type of Posion keg in different containers
#   Restock Agent 1 as keg
#   Script variable called "restock_container_regular_kegs"
#   Script variable called "restock_container_greater_kegs"
#   Script variable called "restock_container_deadly_kegs"
#   Script variable called "restock_container_lethal_kegs"
#   Organizer Agent 1 as keg - Set to a different container then your restock containers
##################
# How to heal through failed posion?
##################
#   Training poison has the ability to kill you if you do not heal or cure yourself.  
#   This script attempts to use heal and cure pots to survive.  You should also
#   have another character constantly bandaging you since this script does not use 
#   and has no extra restock agent related to heal/cure pots

if not findtype "kryss" backpack and not listexists 'select_train_weapon'
    overhead 'Please select your target weapon to train poison on' 88
    setvar 'select_train_weapon'
    createlist 'select_train_weapon'
endif

if not find restock_container_regular_kegs ground
    overhead "Select REGULAR poison keg container"
    setvar restock_container_regular_kegs
endif

if not find restock_container_greater_kegs ground
    overhead "Select GREATER poison keg Container"
    setvar restock_container_greater_kegs
endif

if not find restock_container_deadly_kegs ground
    overhead "Select DEADLY poison keg Container"
    setvar restock_container_deadly_kegs
endif

if not find restock_container_lethal_kegs ground
    overhead "Select LETHAL poison keg Container"
    setvar restock_container_lethal_kegs
endif

while not dead

    removelist 'skill_check'
    removelist 'keg_container'
    createlist 'skill_check'
    createlist 'keg_container'

    if skill 'poisoning' >= 100
        overhead 'Maxed out' 88
        wait 2500
        replay
    elseif skill 'poisoning' < 50
        overhead 'Go train at vendor to 50' 34
        wait 2500
        replay
    elseif skill 'poisoning' < 55
        pushlist "skill_check" 55
        pushlist "keg_container" "restock_container_regular_kegs"
    elseif skill 'poisoning' < 80
        pushlist "skill_check" 80
        pushlist "keg_container" "restock_container_greater_kegs"
    elseif skill 'poisoning' < 100
        pushlist "skill_check" 100
        pushlist "keg_container" "restock_container_deadly_kegs"
    elseif skill 'poisoning' < 120
        pushlist "skill_check" 120
        pushlist "keg_container" "restock_container_lethal_kegs"
    else
        replay
    endif
        
    if not findtype "empty bottle%s%" backpack
        overhead 'Need at least one empty bottle!' 34
        wait 1000
        replay
    endif
    
    if findtype "keg" backpack
        overhead 'Clear bag of kegs'
        organizer 1
        wait 500
    endif
    
    foreach skillchecker in skill_check
        while skill 'poisoning' < skillchecker
            overhead skillchecker
            if not findtype "keg" backpack
                restock 1
                wft 500 
                foreach kegcontainer in keg_container
                    overhead kegcontainer
                    target kegcontainer
                endfor
                wait 2500
            endif
            
            if not findtype "keg" backpack
                overhead "Missing keg!" 34
                overhead "Make sure the following variable points to correct keg location" 34
                foreach kegcontainer in keg_container
                    overhead kegcontainer 88
                endfor 
                wait 2500
                replay
            endif
            
            dclicktype "keg" backpack
            wait 500
            
            // Move keg it found a cleared one
            if findtype "keg" backpack 0
                organizer 1
                wait 500
            endif
            
            if findtype "Green Potion" backpack and hp > 50
                skill 'Poisoning'
                wft 500 
                targettype "Green Potion" backpack
                wft 500 
                if findtype "kryss" backpack
                    targettype "kryss" backpack
                else
                    target 'select_train_weapon'
                endif
                wait 4000
            endif
            
            if findtype "Yellow Potion" backpack and hp < 50
                hotkey 'Drink Heal'
                wait 500
            endif

            if poisoned
                overhead 'Posioned'
                while poisoned
                    if findtype "Orange Potion" backpack
                        hotkey 'Drink Cure'
                        wait 500
                    else
                        overhead "Out of cures... you gonna die" 34
                        wait 1000
                    endif
                    
                    if findtype "Yellow Potion" backpack and hp < 40
                        hotkey 'Drink Heal'
                        wait 500
                    endif
                endwhile
                wait 4000
            else
                wait 4000
            endif
        endwhile
    endfor
    
endwhile