# This is an automated backup - check out https://outlands.uorazorscripts.com/script/5fdda29b-3f1f-4393-b760-b03409265a30 for latest
# Automation by Jaseowns
## Script: Bandage Self Combat Loop
## Created by: Jaseowns#6694
#############################################

# Bandage Self Combat Loop by Jaseowns
# UO Outlands
# Disclaimer:  This may target monsters during a heal attempt and turn you gray.  Use with caution!
if not timerexists "healpot"
    createtimer "healpot"
    settimer "healpot" 8000
endif

@setvar! chromrik_HitPointDrinkHealPotion 45
@setvar! chromrik_HitPointDrinkCurePotion 60

while not dead
    if findtype "clean bandage%s%" backpack
        if hp < maxhp
            if hp <= chromrik_HitPointDrinkHealPotion
                if findtype "Yellow Potion" backpack
                    if timer "healpot" >= 8000
                        overhead 'Drinking heal!'
                        potion "heal"
                        wait 200
                        settimer "healpot" 0
                    endif
                else 
                    overhead 'Out of heals!' 34
                endif
            endif
            if poisoned and hp <= chromrik_HitPointDrinkCurePotion
                if findtype "Orange Potion" backpack
                    overhead 'Drinking cures!'
                    potion "cure"
                    wait 200
                else 
                    overhead 'Out of cures!' 34
                endif
            endif

            if not bandaging and findtype "clean bandage%s%" backpack
                hotkey "Bandage Self"
                wait 200
            endif
        endif
    else
        overhead "No aids!" 34
    endif
    wait 500
endwhile 