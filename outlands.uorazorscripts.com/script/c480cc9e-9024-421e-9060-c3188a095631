# This is an automated backup - check out https://outlands.uorazorscripts.com/script/c480cc9e-9024-421e-9060-c3188a095631 for latest
# Automation by Jaseowns
## Script: Shelter Dungeon Animal Taming 
## Created by: Jaseowns#6694
#############################################

# Shelter Dungeon Animal Taming by Jaseowns
# UO Outlands
# Breakdown of mobs:
# min 30 skill
    # a mongbat
    # a giant rat
# min 40 skill
    # a guar
    # a wolf
# min 50 skill
    # a familiar
    # a giant frog
    # a primordial whelp
    # a colossal frog
    # a giant bat
    # a monitor hatchling
# min 55 skill
    # a corpse eater
# min 60 skill 
    # a primordial
    # a cave bear
# min 75 skill 
    # a drake whelp
    # a chameleon

@setvar! doYouWantATankPetToTryAndStayAlive 1
if not varexist jankPet
    @setvar! jankPet 0
endif

if doYouWantATankPetToTryAndStayAlive = 1
    if followers > 0 and jankPet = 0
    
        if tameThisThing != 0 and find tameThisThing ground -1 -1 2
            getlabel tameThisThing tamedDesc
            if "tame" in tamedDesc
                overhead "Automatically setting jankPet"
                @setvar! jankPet tameThisThing
            endif
        endif
        
        if jankPet = 0
            overhead "Select your tank pet to keep alive" 88
            sysmsg "Select your tank pet to keep alive" 88
            @setvar! jankPet
        endif
    elseif jankPet != 0 and followers = 0
        @setvar! jankPet 0 
   endif
endif
    
if skill "Animal Taming" < 50
    overhead "Go train to 50 skill" 88
    stop
elseif skill "Animal Taming" >= 80
    overhead "Go use another script uorazorscripts.com" 88
    stop
endif

@setvar! tamingTimerCd 11500
if not timerexists tamingTimer
    settimer tamingTimer tamingTimerCd
endif

if not timerexists sayTimer
    settimer sayTimer 0
endif

@setvar! healPetCheckTimerCd 10000
if not timerexists healPetCheckTimer
    settimer healPetCheckTimer healPetCheckTimerCd
endif

@setvar! overheadHelperTimerCd 2000
if not timerexists overheadHelperTimer
    settimer overheadHelperTimer overheadHelperTimerCd
endif

@setvar! skillTimerCd 11000
if not timerexists skillTimer
    settimer skillTimer skillTimerCd
endif

if dead 
    overhead "You dead bro" 34
    wait 1000
    replay
endif

if doYouWantATankPetToTryAndStayAlive = 1 and skill "veterinary" > 0
    if find jankPet ground -1 -1 2 and timer healPetCheckTimer >= healPetCheckTimerCd
        if not bandaging and findtype "clean bandage%s%" backpack as item
            dclick item
            wft 200
            target jankPet
            settimer healPetCheckTimer 0
            if targetexists 
                hotkey "Cancel current target"
            endif
        endif        
    endif
endif

if followers = 0
    @setvar! myFollowers 0  
    settimer sayTimer 0
elseif followers = 1
    @setvar! myFollowers 1
elseif followers = 2
    @setvar! myFollowers 2
elseif followers = 3
    @setvar! myFollowers 3
elseif followers = 4
    @setvar! myFollowers 4
elseif followers = 5
    @setvar! myFollowers 5
    overhead "Too many followers - release some!" 88
    if tameThisThing != 0
            while not gumpexists 2426193729
                menu tameThisThing 9
                wait 50
            endwhile
        while gumpexists 2426193729
            gumpresponse 1 2426193729
            wait 50
        endwhile
    else
        wait 2000
    endif
    replay
endif

@clearignore 

@setvar! tameThisThing 0

if skill "Animal Taming" < 60
    while findtype "a mongbat|a giant rat|a guar|a wolf|a familiar|a giant frog|a primordial whelp|a colossal frog|a giant bat|a monitor hatchling" ground -1 -1 8 as janimal
        getlabel janimal desc
        if "released" in desc
            // ignore them
            @ignore janimal
        elseif "tame" in desc or "bonded" in desc
            // ignore them
            @ignore janimal
        else
            @setvar! tameThisThing janimal
            break
        endif
    endwhile
elseif skill "Animal Taming" < 75
    while findtype "a primordial|a cave bear|a corpse eater|a familiar|a guar|a wolf|a giant frog|a primordial whelp|a colossal frog|a giant bat|a monitor hatchling" ground -1 -1 8 as janimal
        getlabel janimal desc
        if "released" in desc
            // ignore them
            @ignore janimal
        elseif "tame" in desc or "bonded" in desc
            // ignore them
            @ignore janimal
        else
            @setvar! tameThisThing janimal
            break
        endif
    endwhile
elseif skill "Animal Taming" >= 75
    while findtype "a corpse eater|a primordial|a cave bear|a drake whelp|a chameleon" ground -1 -1 8 as janimal
        getlabel janimal desc
        if "released" in desc
            // ignore them
            @ignore janimal
        elseif "tame" in desc or "bonded" in desc
            // ignore them
            @ignore janimal
        else
            @setvar! tameThisThing janimal
            break
        endif
    endwhile
endif

if tameThisThing != 0
    
    if timer overheadHelperTimer >= overheadHelperTimerCd
        overhead "!! attempting to tame !!" 88 tameThisThing 
        settimer overheadHelperTimer 0
    endif

    clearsysmsg 

    settimer tamingTimer 0
    settimer sayTimer 0
    
    if followers > 0
        say "all kill"
        wft 500
        target tameThisThing
        settimer sayTimer 0
        wait 200
        while timer sayTimer < 2550
        endwhile
        say "all follow me"
        wait 1000
        warmode off
    endif
    
    @setvar! isTamed 0
    @setvar! isTaming 1
    
    while isTaming = 1
        
        if dead 
            replay
        endif
    
        if doYouWantATankPetToTryAndStayAlive = 1 and skill "veterinary" > 0
            if find jankPet ground -1 -1 2 and timer healPetCheckTimer >= healPetCheckTimerCd
                if not bandaging and findtype "clean bandage%s%" backpack as item
                    dclick item
                    wft 200
                    target jankPet
                    settimer healPetCheckTimer 0
                    if targetexists 
                        hotkey "Cancel current target"
                    endif
                endif        
            endif
        endif
    
        while not targetexists and not dead
            dclicktype "dagger" backpack
            wft 500
        endwhile
        if targetexists 
            target tameThisThing
        endif
        wait 200
        if insysmsg "That is too far away."
            overhead "Too far away bro.. move closer!" 34
            overhead "THIS IS MY TARGET" 88 tameThisThing
            wait 500
            if timer tamingTimer > 15000
                @setvar! isTaming 0
            endif
        elseif insysmsg "use a bladed item on that"
            clearsysmsg 
            if timer skillTimer >= skillTimerCd
                
                getlabel tameThisThing isItTamedYetDesc
                
                if "tame" in isItTamedYetDesc
                    @setvar! isTamed 1    
                    @setvar! isTaming 0
                    break
                endif
            
                while not targetexists and not dead
                    useskill "taming"
                    wft 500
                endwhile
                target tameThisThing
                settimer skillTimer 0
            endif
            wait 200
            if insysmsg "That is too far away."
                overhead "Too far away bro" 34
                @setvar! isTaming 0
            elseif insysmsg "Target cannot be seen."
                overhead "Cannot be seen bro" 29
                @setvar! isTaming 0
            elseif targetexists 
                @setvar! isTaming 0
                hotkey "cancel current target"
            endif
        endif
    endwhile 
    
    if followers > 3 and isTamed = 1 and tameThisThing != 0
            while not gumpexists 2426193729
                menu tameThisThing 9
                wait 50
            endwhile
        while gumpexists 2426193729
            gumpresponse 1 2426193729
            wait 50
        endwhile
    endif
    
elseif timer overheadHelperTimer >= overheadHelperTimerCd
    overhead "Nothing to tame found!" 34
    settimer overheadHelperTimer 0
endif


wait 100
replay



    