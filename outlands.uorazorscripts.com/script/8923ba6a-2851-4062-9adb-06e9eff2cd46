# This is an automated backup - check out https://outlands.uorazorscripts.com/script/8923ba6a-2851-4062-9adb-06e9eff2cd46 for latest
# Automation by Jaseowns
## Script: Bapeths Stabber Background
## Created by: barryroser#0
#############################################

# Bapeths Stabber Background 
#
# Jan 28th 2026 - Added auto skin before auto hide feature, Added logic to weapon ability use, Added tab buffer of 200ms (it now takes 200ms of being in warmode to auto attack, this allows players to tab in/out when needed without auto attacking)
#
# What it does:
#
# "Warmode Activation Features"
# Insta-Hide/Auto Smoke Bomb after attacking
# Auto skins corpses when using Insta-Hide (toggle feature below)
# Auto Swaps Ranged/Melee weapons when using Insta-Hide (open Thieves codex to set weapons)
# Always Smoke Bomb after attacking (toggle feature below)
# Use Cleansing Brew when Bleeding or Diseased (toggle Utility feature below)
#   
# "Other Features" 
# Auto Stealth Skill (Activates stealth when ready)
# Indicates range above closet targets head & searches for closest target to attack (Use "Manual Override" script to force a target)
# Stealth Wizard hat trick to keep stamina down and prevent pushing though mobs/players (Magical Wizard Hat Required to activate this feature)
# Auto Equip after disarm (toggle feature below)
# Auto Utility - Paralyze pouch, Cure Potion, Str Pot when weak, Resist pot when decreased, Eats food, Use Cleansing Brew (toggle feature below)
# Gold dropper when over weight (toggle feature below)
# Turns Hunting Mode on if user has tracking skill 
# Aspect checker (Shows your current armor aspect overhead periodically)
# Auto Use Weapon Ability ---> Ability turns on when visible and off when hidden (toggle feature below)
# ID Items + Recycle using a "Container ID Wand/120 Item ID Skill" and any recycle tool (Open the "Party Menu" while the script is playing to activate)
#
#
# "REQUIRED": <---------- "Important information!"
# User must create three cooldowns named:
#
# "Hiding"
# Type: Regular
# Trigger: Overhead Message
# Trigger Text: You have hidden
# -------------- Cooldown : 10.1
#    
# "Stealth"
# Type: Regular
# Trigger: Sys Message
# Trigger Text: You begin to move
# -------------- Cooldown : 10.3
#
# "Smoke Bomb"
# Type: Regular
# Trigger: N/A
# Trigger Text: N/A
# -------------- N/A
#
# "Manual Override" script found here -----> https://outlands.uorazorscripts.com/script/80d24cbc-c121-484d-af2f-8fe45c765a2b
#
# SET YOUR WEAPON(S) <---------- "Important information!"
# Type [Atlas to create an atlas in your bag
# Open the "Thieves Codex" while the script is playing to trigger the "Weapon Setter" (If only using one wep, set both settings to the same wep)
# Open The "Atlas" to switch between Melee or Ranged weapon as your default carry
#
##############################################################################################################################################################################################################
#
# "SCRIPT SETTINGS"
#
##############################################################################################################################################################################################################
#
# "Utility Features"
# Utility Features: Cure Potion, Cleansing Brew (use warmode), Trapped Pouch, Strength Potion (when weakened by another player), Refresh Potion when low stam, Eat delectable food
# Revealing actions will not be used while hidden
# (0 = off 1 = on) 
#
# UTILITY FEATURES
@setvar! "Utility" 1
#
##############################################################################################################################################################################################################
#
# "Auto Drink Resist upon debuff"
# This is a seperate utility feature that will counter drink a resist potion when a resist decrease buff is detected
# (0 = off 1 = on) 
# Disable this if you have a "Time Gem" debuff
#
@setvar! "EnableResistPot" 0
#
##############################################################################################################################################################################################################
#
# "Equip After Disarm Feature"
# This feature will keep your weapon equipped
# (0 = off 1 = on) 
#
# AUTO EQUIP AFTER DISARM
@setvar! "DisarmEquip" 1
#
##############################################################################################################################################################################################################
#
# "Smoke Bomb cooldown"
# 0,1,2,3 to match thief codex "Without a Trace" perk
#
# SMOKE BOMB TIMER
@setvar! "WithoutTracePerk" 3
#
##############################################################################################################################################################################################################
#
# "Gold Dropper feature" (set to increase amount of gold dropped if over weight)
# This feature drops piles of gold if you become overweight
# (0 = off 1 = on) 
#
# GOLD DROPPER
@setvar! "DisableGoldDropper" 0
@setvar! "Drop444" 1
@setvar! "Drop2222" 0
@setvar! "Drop3444" 0
#
##############################################################################################################################################################################################################
#
# "Always Smoke Bomb"
# This feature works when using the "warmode" Insta-Hide/Auto Smoke Bomb feature
# (0 = off 1 = on) 
#
# ALWAYS SMOKEBOMB
@setvar! "AlwaysSmokebomb" 0
#
##############################################################################################################################################################################################################
#
# "Auto use weapon ability feature"
# This feature will use a weapon ability post-backstab if you are not hidden and wish to continue striking your target
# It will enable your weapon ability when not hidden and disable it when hidden
# Should this feature ever become inverted (On when hidden and Off when not hidden) user can manually toggle their weapon ability a single time to fix it
# (0 = off 1 = on) 
#
# AUTO USE ABILITY
@setvar! "AutoWeaponAbility" 1
#
# Set your "Weapon Ability" to auto use it while in combat
# 1 = selected ablity, leave other abilities at 0
#
# MELEE WEAPON ABILITY
@setvar! "MeleeBapWeaponAbility-1" 1
@setvar! "MeleeBapWeaponAbility-2" 0
@setvar! "MeleeBapWeaponAbility-3" 0
#
# RANGED WEAPON ABILITY
@setvar! "RangedBapWeaponAbility-1" 0
@setvar! "RangedBapWeaponAbility-2" 0
@setvar! "RangedBapWeaponAbility-3" 1
#
##############################################################################################################################################################################################################
#
# "Auto Skinning Feature"
#
# Auto skins a moment before the Insta-Hide/Auto Smoke Bomb feature triggers
# This only works when using the (warmode) auto hide after attack feature
# Warning! ---> Be careful using this feature in the wilderness as there is a CAPTCHA when using a skinning knife
# (0 = off 1 = on) 
#
# AUTO SKIN BEFORE AUTO HIDE
@setvar! "SkinBeforeHide" 0
#
##############################################################################################################################################################################################################
#
if casting 
    while casting 
        //donothing
    endwhile
    pause 330
endif

//Auto stealth activator
if not timerexists StabberOTS and cooldown "Hiding"
    createtimer StabberOTS
endif
if timerexists StabberOTS and cooldown "Stealth"
    removetimer StabberOTS
endif
if timerexists StabberOTS and hidden and not cooldown "Hiding" and not cooldown "Stealth"
    skill "Stealth"
    cooldown "Stealth" 10000
    removetimer StabberOTS
    getlabel backpack ping
endif
//End of Auto stealth activator

// Set Base Weapon Scheme for Multi Weapon Builds (Open Thieves Codex to set weapons, Open Atlas to switch the default)
if gumpexists 2955404556
    gumpclose 2955404556
    overhead "Setting Weapons..." 45
    overhead "Set both as the same if only one weapon" 45
    pause 400
    overhead "Set Melee Wep..." 38
    hotkey "Set Last Target"
    wft 500
    while targetexists 
        //donothing
    endwhile
    overhead "Melee Wep Set!" 38
    @setvar "Melee Stabber Wep" lasttarget 
    pause 400
    overhead "Set Ranged Wep..." 88
    hotkey "Set Last Target"
    wft 500
    while targetexists 
        //donothing
    endwhile
    @setvar "Ranged Stabber Wep" lasttarget 
    overhead "Ranged Wep Set!" 88
endif
if not timerexists BapBaseStabberWep
    if gumpexists 341416395
        gumpclose 341416395
        createtimer BapBaseStabberWep
        overhead "*Ranged Set*" 2085
    endif
endif
if timerexists BapBaseStabberWep
    if gumpexists 341416395
        gumpclose 341416395
        removetimer BapBaseStabberWep
        overhead "*Melee Set*" 1779
    endif
endif
if timerexists BapBaseStabberWep and find "Ranged Stabber Wep" backpack
    while queued
        //donothing
    endwhile
    dclick "Ranged Stabber Wep"
    getlabel backpack ping
    say '[abilityhotbar'
endif
if not timerexists BapBaseStabberWep and find "Melee Stabber Wep" backpack
    while queued
        //donothing
    endwhile
    dclick "Melee Stabber Wep"
    getlabel backpack ping
    say '[abilityhotbar'
endif
//End of Base Weapon Scheme for Multi Weapon Builds

//Hide after attack routine
if warmode and not timerexists baptabtimer
    createtimer baptabtimer
endif
if not warmode and timerexists baptabtimer
    removetimer  baptabtimer
endif
if timerexists baptabtimer
    if timer baptabtimer >= 200 and warmode and find "Manual Override" ground and hidden and not cooldown "Stealing" and not cooldown "Stealth" and not cooldown "Hiding" and not timerexists StabberOTS
        clearsysmsg 
        if timerexists BapCancelAttack
            removetimer BapCancelAttack
        endif
        if skill "Archery" >= 50 or skill "Fishing" >= 50
            if find "Manual Override" ground -1 -1 1
                if find "Melee Stabber Wep" backpack
                    while queued
                        //donothing
                    endwhile
                    dclick "Melee Stabber Wep"
                    getlabel backpack ping
                endif
            elseif find "Manual Override" ground -1 -1 2
                createtimer BapCancelAttack
            elseif find "Manual Override" ground -1 -1 3
                createtimer BapCancelAttack
            elseif find "Manual Override" ground -1 -1 4
                createtimer BapCancelAttack
            elseif find "Ranged Stabber Wep" backpack
                while queued
                    //donothing
                endwhile
                dclick "Ranged Stabber Wep"
                getlabel backpack ping
            endif
        endif
        if timerexists BapCancelAttack
            //donothing
        else
            attack "Manual Override"
            getlabel backpack ping
        endif
        if warmode
            warmode off
        endif
        if timerexists BapCancelAttack
            removetimer BapCancelAttack
        endif
        if insysmsg "You strike your target from the shadows!"
            if "SkinBeforeHide" = 1
                if findtype "corpse" ground -1 -1 2 and findtype 11552 backpack as sknife
                    hotkey "Cancel Current Target"
                    dclick sknife
                    wft 500
                    target self
                    hotkey "Cancel Current Target"
                    getlabel backpack ping
                endif
            endif
            if "AlwaysSmokebomb" = 1
                if not hidden and not cooldown "Smoke Bomb"
                    if findtype "bombs" backpack as sbomb
                        clearsysmsg 
                        while queued
                            //donothing
                        endwhile
                        dclick sbomb
                        getlabel backpack ping
                        if insysmsg "You begin to move"
                            createtimer StabberOTS
                            if "WithoutTracePerk" = 0
                                cooldown "Smoke bomb" 120000
                            endif
                            if "WithoutTracePerk" = 1
                                cooldown "Smoke bomb" 100000
                            endif
                            if "WithoutTracePerk" = 2
                                cooldown "Smoke bomb" 80000
                            endif
                            if "WithoutTracePerk" = 3
                                cooldown "Smoke bomb" 60000
                            endif
                        endif
                    else
                        overhead "Out of Smoke Bombs..." 38
                    endif
                endif
            endif  
            if not hidden
                skill "Hiding"
                getlabel backpack ping
            endif
            if "AlwaysSmokebomb" = 0 and not hidden and not cooldown "Smoke Bomb"
                if findtype "bombs" backpack as sbomb
                    clearsysmsg 
                    while queued
                        //donothing
                    endwhile
                    dclick sbomb
                    getlabel backpack ping
                    if insysmsg "You begin to move"
                        createtimer StabberOTS
                        if "WithoutTracePerk" = 0
                            cooldown "Smoke bomb" 120000
                        endif
                        if "WithoutTracePerk" = 1
                            cooldown "Smoke bomb" 100000
                        endif
                        if "WithoutTracePerk" = 2
                            cooldown "Smoke bomb" 80000
                        endif
                        if "WithoutTracePerk" = 3
                            cooldown "Smoke bomb" 60000
                        endif
                    endif
                else
                    overhead "Out of Smoke Bombs..." 38
                endif
            endif
        else 
            overhead "No shot..." 38
        endif
    elseif timer baptabtimer >= 200 and warmode and find "BapStabTarget" ground and hidden and not cooldown "Stealing" and not cooldown "Stealth" and not cooldown "Hiding" and not timerexists StabberOTS
        clearsysmsg 
        if skill "Archery" >= 50 or skill "Fishing" >= 50
            if find "BapStabTarget" ground -1 -1 1
                if find "Melee Stabber Wep" backpack
                    while queued
                        //donothing
                    endwhile
                    dclick "Melee Stabber Wep"
                    getlabel backpack ping
                endif
            elseif find "BapStabTarget" ground -1 -1 2
                createtimer BapCancelAttack
            elseif find "BapStabTarget" ground -1 -1 3
                createtimer BapCancelAttack
            elseif find "BapStabTarget" ground -1 -1 4
                createtimer BapCancelAttack
            elseif find "Ranged Stabber Wep" backpack
                while queued
                    //donothing
                endwhile
                dclick "Ranged Stabber Wep"
                getlabel backpack ping
            endif
        endif
        if timerexists BapCancelAttack
            //donothing
        else
            attack "BapStabTarget"
            getlabel backpack ping
        endif
        if warmode
            warmode off
        endif
        if timerexists BapCancelAttack
            removetimer BapCancelAttack
        endif
        if insysmsg "You strike your target from the shadows!"
            if "SkinBeforeHide" = 1
                if findtype "corpse" ground -1 -1 2 and findtype 11552 backpack as sknife
                    hotkey "Cancel Current Target"
                    dclick sknife
                    wft 500
                    target self
                    hotkey "Cancel Current Target"
                    getlabel backpack ping
                endif
            endif
            if "AlwaysSmokebomb" = 1
                if not hidden and not cooldown "Smoke Bomb"
                    if findtype "bombs" backpack as sbomb
                        clearsysmsg 
                        while queued
                            //donothing
                        endwhile
                        dclick sbomb
                        getlabel backpack ping
                        if insysmsg "You begin to move"
                            createtimer StabberOTS
                            if "WithoutTracePerk" = 0
                                cooldown "Smoke bomb" 120000
                            endif
                            if "WithoutTracePerk" = 1
                                cooldown "Smoke bomb" 100000
                            endif
                            if "WithoutTracePerk" = 2
                                cooldown "Smoke bomb" 80000
                            endif
                            if "WithoutTracePerk" = 3
                                cooldown "Smoke bomb" 60000
                            endif
                        endif
                    else
                        overhead "Out of Smoke Bombs..." 38
                    endif
                endif
            endif  
            if not hidden
                skill "Hiding"
                getlabel backpack ping
            endif
            if "AlwaysSmokebomb" = 0 and not hidden and not cooldown "Smoke Bomb"
                if findtype "bombs" backpack as sbomb
                    clearsysmsg 
                    while queued
                        //donothing
                    endwhile
                    dclick sbomb
                    getlabel backpack ping
                    if insysmsg "You begin to move"
                        createtimer StabberOTS
                        if "WithoutTracePerk" = 0
                            cooldown "Smoke bomb" 120000
                        endif
                        if "WithoutTracePerk" = 1
                            cooldown "Smoke bomb" 100000
                        endif
                        if "WithoutTracePerk" = 2
                            cooldown "Smoke bomb" 80000
                        endif
                        if "WithoutTracePerk" = 3
                            cooldown "Smoke bomb" 60000
                        endif
                    endif
                else
                    overhead "Out of Smoke Bombs..." 38
                endif
            endif
        else 
            overhead "No shot..." 38
        endif
    endif
endif
// End of Hide after attack routine

// Search for target routune
if not timerexists newstabtarget
    createtimer newstabtarget
    settimer newstabtarget 600
endif
if not targetexists and not find "Manual Override" and timer newstabtarget >= 600
    settimer newstabtarget 0
    clearsysmsg 
    hotkey 'Target Closest Grey Monster'
    if insysmsg "No one matching"
        //donnothing
    else 
        @setvar! "BapStabTarget" lasttarget 
    endif
    hotkey "Cancel Current Target"
endif
// End of Search for target routune

// Target Range Overhead Routine
if not timerexists baprangecheck
    createtimer baprangecheck
    settimer baprangecheck 1200
elseif timer baprangecheck >= 1200
    settimer baprangecheck 0
    if find "Manual Override" ground
        if find "Manual Override" ground -1 -1 1
            overhead "1 Tile" 33 "Manual Override"
        elseif find "Manual Override" ground -1 -1 2
            overhead "2 Tile" 38 "Manual Override"
        elseif find "Manual Override" ground -1 -1 3
            overhead "3 Tile" 43 "Manual Override"
        elseif find "Manual Override" ground -1 -1 4
            overhead "4 Tile" 48 "Manual Override"
        elseif find "Manual Override" ground -1 -1 5
            overhead "5 Tile" 53 "Manual Override"
        elseif find "Manual Override" ground -1 -1 6
            overhead "6 Tile" 58 "Manual Override"
        elseif find "Manual Override" ground -1 -1 7
            overhead "7 Tile" 63 "Manual Override"
        elseif find "Manual Override" ground -1 -1 8
            overhead "8 Tile" 68 "Manual Override"
        elseif find "Manual Override" ground -1 -1 9
            overhead "9 Tile" 73 "Manual Override"
        elseif find "Manual Override" ground -1 -1 10
            overhead "10 Tile" 78 "Manual Override"
        elseif find "Manual Override" ground -1 -1 11
            overhead "11 Tile" 83 "Manual Override"
        elseif find "Manual Override" ground -1 -1 12
            overhead "12 Tile" 88 "Manual Override"
        elseif find "Manual Override" ground -1 -1 13
            overhead "13 Tile" 93 "Manual Override"
        elseif find "Manual Override" ground -1 -1 14
            overhead "14 Tile" 93 "Manual Override"
        elseif find "Manual Override" ground -1 -1 15
            overhead "15 Tile" 93 "Manual Override"
        elseif find "Manual Override" ground -1 -1 16
            overhead "16 Tile" 93 "Manual Override"
        elseif find "Manual Override" ground -1 -1 17
            overhead "17 Tile" 93 "Manual Override"
        elseif find "Manual Override" ground -1 -1 18
            overhead "18 Tile" 93 "Manual Override"
        endif
    else 
        if find "BapStabTarget" ground -1 -1 1
            overhead "1 Tile" 33 "BapStabTarget"
        elseif find "BapStabTarget" ground -1 -1 2
            overhead "2 Tile" 38 "BapStabTarget"
        elseif find "BapStabTarget" ground -1 -1 3
            overhead "3 Tile" 43 "BapStabTarget"
        elseif find "BapStabTarget" ground -1 -1 4
            overhead "4 Tile" 48 "BapStabTarget"
        elseif find "BapStabTarget" ground -1 -1 5
            overhead "5 Tile" 53 "BapStabTarget"
        elseif find "BapStabTarget" ground -1 -1 6
            overhead "6 Tile" 58 "BapStabTarget"
        elseif find "BapStabTarget" ground -1 -1 7
            overhead "7 Tile" 63 "BapStabTarget"
        elseif find "BapStabTarget" ground -1 -1 8
            overhead "8 Tile" 68 "BapStabTarget"
        elseif find "BapStabTarget" ground -1 -1 9
            overhead "9 Tile" 73 "BapStabTarget"
        elseif find "BapStabTarget" ground -1 -1 10
            overhead "10 Tile" 78 "BapStabTarget"
        elseif find "BapStabTarget" ground -1 -1 11
            overhead "11 Tile" 83 "BapStabTarget"
        elseif find "BapStabTarget" ground -1 -1 12
            overhead "12 Tile" 88 "BapStabTarget"
        elseif find "BapStabTarget" ground -1 -1 13
            overhead "13 Tile" 93 "BapStabTarget"
        elseif find "BapStabTarget" ground -1 -1 14
            overhead "14 Tile" 93 "BapStabTarget"
        elseif find "BapStabTarget" ground -1 -1 15
            overhead "15 Tile" 93 "BapStabTarget"
        elseif find "BapStabTarget" ground -1 -1 16
            overhead "16 Tile" 93 "BapStabTarget"
        elseif find "BapStabTarget" ground -1 -1 17
            overhead "17 Tile" 93 "BapStabTarget"
        elseif find "BapStabTarget" ground -1 -1 18
            overhead "18 Tile" 93 "BapStabTarget"
        endif
    endif
endif
// End of Target Range Overhead Routine
// "You cannot perform that ability yet"
//Auto Use Weapon Ability
if "AutoWeaponAbility" = 1
    if not timerexists abilitynotready
        createtimer abilitynotready
        settimer abilitynotready 4000
    endif
    if insysmsg "Weapon ability ready" and not hidden and timerexists BapWepAbilityOn
        removetimer BapWepAbilityOn
    endif
    if timer abilitynotready >= 4000 and hidden and not timerexists BapWepAbilityOff
        createtimer BapWepAbilityOff
        removetimer BapWepAbilityOn
        if not timerexists BapBaseStabberWep
            if "MeleeBapWeaponAbility-1" = 1
                say "[WeaponAbility1"
            elseif "MeleeBapWeaponAbility-2" = 1
                say "[WeaponAbility2"
            elseif "MeleeBapWeaponAbility-3" = 1
                say "[WeaponAbility3"
            endif
        endif
        if timerexists BapBaseStabberWep
            if "RangedBapWeaponAbility-1" = 1
                say "[WeaponAbility1"
            elseif "RangedBapWeaponAbility-2" = 1
                say "[WeaponAbility2"
            elseif "RangedBapWeaponAbility-3" = 1
                say "[WeaponAbility3"
            endif
        endif
    endif
    if timer abilitynotready >= 4000 and not hidden and not timerexists BapWepAbilityOn
        clearsysmsg 
        removetimer BapWepAbilityOff
        createtimer BapWepAbilityOn
        if not timerexists BapBaseStabberWep
            if "MeleeBapWeaponAbility-1" = 1
                say "[WeaponAbility1"
            elseif "MeleeBapWeaponAbility-2" = 1
                say "[WeaponAbility2"
            elseif "MeleeBapWeaponAbility-3" = 1
                say "[WeaponAbility3"
            endif
        endif
        if timerexists BapBaseStabberWep
            if "RangedBapWeaponAbility-1" = 1
                say "[WeaponAbility1"
            elseif "RangedBapWeaponAbility-2" = 1
                say "[WeaponAbility2"
            elseif "RangedBapWeaponAbility-3" = 1
                say "[WeaponAbility3"
            endif
        endif
        getlabel backpack ping
        if insysmsg "You cannot perform that ability yet"
            removetimer BapWepAbilityOn
            createtimer BapWepAbilityOff
            settimer abilitynotready 0
        endif
    endif
endif
//End of Auto Use Weapon Ability

// Stealth Wiz Hat Routine
if findtype 5912 self and not varexist "bwiz" and findlayer self head as regularhat
    getlabel regularhat desc
    if "magical" in desc
        dclick regularhat
        pause 500
        getlabel backpack ping
        replay
    endif
endif
if not find "bwiz" self and findtype 5912 backpack as wizhat
    getlabel wizhat desc
    if "restyled" in desc
        @ignore wizhat
        replay
    elseif "armored" in desc
        @setvar! "bwiz" wizhat
    elseif "magical" in desc
        @setvar! "bwiz" wizhat
        @setvar! "nonaspectedwiz" wizhat
    endif
endif
if varexist "bwiz" and find "bwiz" backpack
    if not find "bmain" self and findlayer self head as hat
        @setvar! "bmain" hat
    endif
    if not varexist "bmain"
        overhead "I need a primary hat or helm to wear.." 88
        pause 2000
        overhead "Anything will do, just not a magical wizard hat..." 88
        stop
    endif
endif
if varexist "bwiz" and find "bwiz" backpack and find "bmain" self and skill "Stealth" >= 80
    if not targetexists and not casting and hidden and stam = maxstam
        while queued
            //donothing
        endwhile
        dclick "bwiz"
        while queued
            //donothing
        endwhile
        pause 30
        dclick "bmain"
        pause 500
        getlabel backpack ping
    endif
    if not targetexists and not casting and not findlayer self head and find "bmain" self
        while queued
            //donothing
        endwhile
        dclick "bmain"
        pause 500
        getlabel backpack ping
    endif
endif
if varexist "bwiz" and find "bmain" self and skill "Stealth" >= 80
    if not targetexists and not casting and not find "bwiz" backpack
        while queued
            //donothing
        endwhile
        dclick "bmain"
        pause 500
        getlabel backpack ping
    endif
endif
// End of Stealth Wiz Hat Routine

// Tracking hunting mode enabler routine
if skill "Tracking" >= 10 and not findbuff "Tracking Hunting"
    skill 'tracking'
    waitforgump 4267467659 500
    gumpresponse 6
    pause 250
    gumpclose 4267467659
endif
// End of Tracking hunting mode enabler routine

// Fast container ID wand/skill recycle (use Party Menu) routine
if gumpexists 3527489586 and findtype "3997|4148|4032|7864" backpack as breakdowntool
    gumpclose 3527489586
    hotkey "Cancel Current Target"
    if not find "ContainerIDWand" and findtype 20495 backpack as contIDwand
        @setvar! "ContainerIDWand" contIDwand
    endif
    if skill "Item ID" = 120 or find "ContainerIDWand" backpack
        clearsysmsg 
        while queued
            //donothing
        endwhile
        if skill "Item ID" = 120
            skill "Item ID"
        elseif find "ContainerIDWand" backpack
            dclick "ContainerIDWand"
        endif
        wft 500
        overhead "Select Container to ID..." 88
        while targetexists 
            //donothing
        endwhile
        getlabel backpack ping
        overhead "Attempting recycle..." 88
        if insysmsg "No unidentified items found."
            overhead "Nothing fresh to ID..." 45
        elseif insysmsg "That is not a container." 
            overhead "Invalid container, try again.." 45
        elseif insysmsg "You must wait a few moments to use another skill" 
            overhead "Skill Cooldown, Try Again.." 38
        endif
        while findtype 3834 backpack 0 as bbook
            @ignore bbook
        endwhile
        while findtype 5056|5059|5060|5061|5063|5070|5074|5075|5076|5078|5085|5089|5090|5101|5103|5105|5106|5129|5131|5132|5135|5138|5139|5142|5143|5144|5146|5201|5203|5204|5205|5207|7169|7170|7173|7175|7177|7179|7181|7610|7947|31003|31004|31005|31006|31007|31008|31009|31010|31011|31012|31015|31191|31017|31019|31021|31023|31025|31027|31029|31031|31033|31035|31037|31038|31041|31043|31045|31047|31049|31051|31053|31055|3834|3740|3742|3762|3763|10245|20006|20008|20010|20012|20014|20016|7026|7027|7029|7031|7033|7034|7035|7107|7109|31002|31130|3920|5042|5117|30990|30993|30994|30995|31184|31186|3719|3938|5121|5123|5125|30989|30992|30996|30997|30998|31176|31188|3568|3713|3721|3932|5040|5044|5112|5127|5177|5179|5181|30991|30999|31000|31001|31014|31178|31180|31182|3909|3911|3913|3915|3917|3934|3937|5046|5049|5115|5119|5182|5185|5187|30988|31128|31190|22187|31141|31142|31169|31172 backpack as IDitem
            getlabel IDitem desc
            if "aspect" in desc or "training" in desc or "vanquishing" in desc or "supremely" in desc or "slaying" in desc or "enticement" in desc or "invulnerability" in desc or "power" in desc or "exceedingly accurate force" in desc or "exceedingly potent" in desc or "valorite" in desc or "valehide" in desc or "valewood" in desc or "avarite" in desc or "avarhide" in desc or "avarwood" in desc or "verite" in desc or "verehide" in desc or "verewood" in desc or "agapite" in desc or "rosehide" in desc or "rosewood" in desc or "fortification" in desc
                @ignore IDitem
                overhead "*strong magic*" 2091 IDitem
            elseif "durable" in desc or "substantial" in desc or "massive" in desc or "fortified" in desc or "indestructible" in desc or "exceptional" in desc or "accurate" in desc or "surpassingly" in desc or "eminently" in desc or "exceedingly" in desc or "ruin" in desc or "might" in desc or "force" in desc or "power" in desc or "mastercrafted" in desc or "defense" in desc or "guarding" in desc or "hardening" in desc or "potent" in desc or "melodious" in desc
                overhead "*trash*" 1779 IDitem
                while not gumpexists 949095101
                    while queued
                        //donothing
                    endwhile
                    dclick breakdowntool
                    waitforgump 949095101 1000
                endwhile
                while not ingump "Recycle Single Item" 949095101
                    gumpresponse 2
                    waitforgump 949095101 1000
                endwhile                                
                gumpresponse 3
                waitforgump 949095101 1000
                target IDitem
                getlabel backpack ping
                pause 250
                gumpclose 949095101
            else
                @ignore IDitem
            endif
            if skill "Item ID" = 120
                //donothing
            elseif not find "ContainerIDWand" backpack
                break
            endif
        endwhile
        if gumpexists 949095101
            gumpclose 949095101
        endif
    else
        overhead "No Container ID Wand..." 38
    endif
    if targetexists 
        hotkey "Cancel Current Target"
    endif
    overhead "Recycle Complete." 88
    @clearignore 
endif
// End of Fast container ID wand recycle routine

// Aspect checker routine
if not timerexists checkaspect
    createtimer checkaspect
    settimer checkaspect 60000
elseif timer checkaspect >= 60000 
    if findlayer self innertorso as aspectarm
        getlabel aspectarm desc
        if "aspect" in desc
            if "air" in desc
                overhead "<Air Active>" 2263
            elseif "arcane aspect" in desc
                overhead "<Arcane Active>" 1560
            elseif "artisan aspect" in desc
                overhead "<Artisan Active>" 2096
            elseif "blood aspect" in desc
                overhead "<Blood Active>" 2087
            elseif "command aspect" in desc
                overhead "<Command Active>" 1782
            elseif "death aspect" in desc
                overhead "<Death Active>" 2155
            elseif "discipline aspect" in desc
                overhead "<Discipline Active>" 2490
            elseif "earth aspect" in desc
                overhead "<Earth Active>" 2767
            elseif "eldritch aspect" in desc
                overhead "<Eldritch Active>" 2615
            elseif "fire aspect" in desc
                overhead "<Fire Active>" 2635
            elseif "fortune aspect" in desc
                overhead "<Fortune Active>" 2843
            elseif "frost aspect" in desc
                overhead "<Frost Active>" 2234
            elseif "gadget aspect" in desc
                overhead "<Gadget Active>" 2669
            elseif "harvest aspect" in desc
                overhead "<Harvest Active>" 2086
            elseif "holy aspect" in desc
                overhead "<Holy Active>" 2153
            elseif "lightning aspect" in desc
                overhead "<Lightning Active>" 2650
            elseif "lyric aspect" in desc
                overhead "<Lyric Active>" 2241
            elseif "madness aspect" in desc
                overhead "<Madness Active>" 1060
            elseif "poison aspect" in desc
                overhead "<Poison Active>" 2897
            elseif "shadow aspect" in desc
                overhead "<Shadow Active>" 1790
            elseif "void aspect" in desc
                overhead "<Void Active>" 2599
            elseif "water aspect" in desc
                overhead "<Water Active>" 1788
            elseif "war aspect" in desc
                overhead "<War Active>" 2503
            endif
        else 
            overhead "No aspect enabled" 45
        endif
    endif
    settimer checkaspect 0
endif
// End of aspect checker routine

// Utility Routine
if "Utility" = 1
    if not targetexists and not casting and not findbuff "Food Satisfaction"
        if findtype 2429|28885|29774|29773|28880|28888|28881|28883|28886|28879 backpack as food
            while queued
                //donothing
            endwhile
            dclick food
            pause 500
            getlabel backpack ping
        endif
    endif
    if not targetexists and not hidden and not casting and stam <= 4 and findtype "Red Potion" backpack as redpot
        while queued
            //donothing
        endwhile
        dclick redpot
        pause 500
        getlabel backpack ping
    endif
    while not hidden and paralyzed and not targetexists and findtype "pouch" backpack 38
        while queued
            //donothing
        endwhile
        say "[Pouch"
    endwhile
    while not hidden and not casting and poisoned and not targetexists and findtype "Orange Potion" backpack as curepot
        while queued
            //donothing
        endwhile
        dclick curepot
        pause 500
        getlabel backpack ping
    endwhile
    if warmode and not hidden and not timerexists brew and not targetexists and findbuff "Bleed" and findtype 50675 backpack as cbrew
        clearsysmsg 
        while queued
            //donothing
        endwhile
        dclick cbrew
        getlabel backpack ping
        if insysmsg "You drink a cleansing brew"
            createtimer brew
        endif
    elseif warmode and not hidden and not timerexists brew and not targetexists and findbuff "Diseased" and findtype 50675 backpack as cbrew
        while queued
            //donothing
        endwhile
        dclick cbrew
        getlabel backpack ping
        if insysmsg "You drink a cleansing brew"
            createtimer brew
        endif
    endif
    if timerexists brew
        if timer brew >= 120000
            removetimer brew
        endif
    endif
    if not hidden and not casting and findbuff "Weaken" and not targetexists and findtype "White Potion" backpack as stpot
        while queued
            //donothing
        endwhile
        dclick stpot
        pause 500
        getlabel backpack ping
    endif
endif
//End of Utility Routine

// End of Auto Drink Res Decreased Routune
if "EnableResistPot" = 1
    if not hidden and not casting and findbuff "Magic Resist Decrease" and not targetexists and findtype "Black Potion" backpack as mpot
        while queued
            //donothing
        endwhile
        dclick mpot
        pause 500
        getlabel backpack ping
    endif
endif
// End of Auto Drink Res Decreased Routune

// Auto Re-equip after disarm routine
if "DisarmEquip" = 1
    if insysmsg "disarms you!"
        createtimer disarmtimerright
        createtimer disarmtimerleft
        getlabel backpack ping
    endif
    if not findlayer self righthand and not findlayer self lefthand
        //donothing
    else
        if not timerexists checkequippeditems
            createtimer checkequippeditems
            settimer checkequippeditems 15000
        elseif timerexists checkequippeditems
            if timer checkequippeditems >= 15000
                settimer checkequippeditems 0
                if timerexists equippedrighthand and not timerexists disarmtimerright
                    removetimer equippedrighthand
                    @unsetvar "myrighthanditem"
                endif
                if timerexists equippedlefthand and not timerexists disarmtimerleft
                    removetimer equippedlefthand
                    @unsetvar "mylefthanditem"
                endif
            endif
        endif
    endif
    if timerexists disarmtimerright and findlayer self righthand
        removetimer disarmtimerright
    endif
    if timerexists disarmtimerleft and findlayer self lefthand
        removetimer disarmtimerleft
    endif
    if not timerexists disarmtimerright or not timerexists disarmtimerleft
        if not varexist myrighthanditem and timerexists equippedrighthand
            removetimer equippedrighthand
        endif
        if not varexist mylefthanditem and timerexists equippedlefthand
            removetimer equippedlefthand
        endif
        if not find "myrighthanditem" self and findlayer self righthand
            removetimer equippedrighthand
        endif
        if not find "mylefthanditem" self and findlayer self lefthand
            removetimer equippedlefthand
        endif
        if not timerexists equippedrighthand and findlayer self righthand as rheqitem
            getlabel rheqitem desc
            if "spells" in desc
                createtimer exceptionalspellbook
            elseif timerexists exceptionalspellbook
                removetimer exceptionalspellbook
            endif
            @setvar! "myrighthanditem" rheqitem
            createtimer equippedrighthand
        endif
        if not timerexists equippedlefthand and findlayer self lefthand as lheqitem
            @setvar! "mylefthanditem" lheqitem
            createtimer equippedlefthand
        endif
    endif
    if timerexists disarmtimerright
        if timer disarmtimerright >= 5000
            while not targetexists and not casting and varexist myrighthanditem and not timerexists exceptionalspellbook and not findlayer self righthand
                dclick "myrighthanditem"
                while queued
                    //dn
                endwhile
            endwhile
            while not targetexists and not casting and varexist myrighthanditem and timerexists exceptionalspellbook and not findlayer self righthand
                while queued
                    //dn
                endwhile
                drop backpack
                lift "myrighthanditem"
                drop self righthand
                while queued
                    //dn
                endwhile
            endwhile
            while targetexists and not casting and varexist myrighthanditem and not findlayer self righthand
                while queued
                    //dn
                endwhile
                drop backpack
                lift "myrighthanditem"
                drop self righthand
                while queued
                    //dn
                endwhile
            endwhile
        endif
    endif
    if timerexists disarmtimerleft  
        if timer disarmtimerleft >= 5000
            while not targetexists and not casting and varexist mylefthanditem and not findlayer self lefthand
                dclick "mylefthanditem"
                while queued
                    //dn
                endwhile
            endwhile
            while targetexists and not casting and varexist mylefthanditem and not findlayer self lefthand
                while queued
                    //dn
                endwhile
                drop backpack
                lift "mylefthanditem"
                drop self lefthand
                while queued
                    //dn
                endwhile
            endwhile
        endif
    endif
    if find "mylefthanditem" backpack and not casting and not targetexists and skill "Tactics" >= 80 and not findlayer self lefthand
        dclick "mylefthanditem"
        pause 500
        getlabel backpack ping
    endif
    if find "myrighthanditem" backpack and not casting and not targetexists and skill "Tactics" >= 80 and not findlayer self righthand
        dclick "myrighthanditem"
        pause 500
        getlabel backpack ping
    endif
endif
// End of Auto Re-equip after disarm routine

// Gold Dropper Routine
if "DisableGoldDropper" = 0
    while diffweight <= 0 and findtype "gold coin" backpack as dropgold
        if not hidden and stam <= 4 and findtype "Red Potion" backpack as redpot
            while queued
                //donothing
            endwhile
            dclick redpot
            pause 500
            getlabel backpack ping
        endif
        overhead "My cup runneth over..." 1194
        drop backpack
        if "Drop444" = 1
            lift dropgold 444
        elseif "Drop2222" = 1
            lift dropgold 2222
        elseif "Drop3444" = 1
            lift dropgold 3444
        endif
        drop 0
        while queued
            //donothing
        endwhile
    endwhile
endif
// End of Gold Dropper Routine

if dead 
    overhead "I need a new body..." 88
    stop
endif
loop