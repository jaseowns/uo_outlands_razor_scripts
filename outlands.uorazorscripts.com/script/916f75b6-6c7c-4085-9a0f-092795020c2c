# This is an automated backup - check out https://outlands.uorazorscripts.com/script/916f75b6-6c7c-4085-9a0f-092795020c2c for latest
# Automation by Jaseowns
## Script: Train Alchemy using Shelf
## Created by: Jaseowns#6694
#############################################

# Alchemy Training with Shelf by Jaseowns
# UO Outlands - https://youtu.be/k1PqAGIB0Ls
# Updated on march 1st 2023, again on march 21st 2023 (gump responses changed)
# Requirements: a restock shelf (regs/bottles/mortar), a restock container (kegs)
#   restock agent 1:  setup as keg (and any other things you might not have on shelf, mortar pestle, etc)
# Additional Info: 
#   This script was updated 9/8/2021 to make sure your restock variables are setup
#   Edit line 25 to decrease skill check (default is 120)
if not varexist "restock_container"
    overhead "Target your restock container (kegs)"
    setvar "restock_container"
endif


if not findtype "mortar and pestle" backpack or counttype 3976 backpack < 10 or counttype 3981 backpack < 5 or not findtype "empty bottle%s%" backpack or not findtype "keg" backpack
    if findtype "storage shelf" ground any any 2 as the_shelf
        overhead "Resupply" 88
        menu the_shelf 1
        wait 500
    endif
    restock 1
    waitfortarget 
    target "restock_container"
    wait 1000
endif

if skill 'Alchemy' >= 120
    overhead 'Hit Max!' 88
    wait 5000
    replay
endif

if not findtype "mortar and pestle" backpack 
    if findtype "resource container" ground -1 -1 2 as item
        // do nothing
    else
        overhead 'No mortar and pestle!' 34
        wait 2000
        replay
    endif
endif


if counttype 3976 backpack < 10
    overhead 'No Nightshade!' 34
    wait 2000
    replay
endif

if counttype 3981 backpack < 5
    overhead "No Spiders Silk" 34
    wait 2000
    replay
endif

if not findtype "keg" backpack
    overhead 'No keg!' 34
    wait 2000
    replay
endif

if not findtype "empty bottle%s%" backpack
    overhead 'No Bottles!' 34
    wait 2000
    replay
endif

clearsysmsg 
gumpclose 949095101
gumpclose 1859005118

if findtype "mortar and pestle" backpack as item
    dclick item
    waitforgump 949095101 5000 
elseif findtype "resource container" ground -1 -1 2 as item
    dclick item
    waitforgump 1859005118
    gumpresponse 7 1859005118
    waitforgump 1859005118 5000
    gumpresponse 100 1859005118
    waitforgump 949095101 5000
endif

if skill 'Alchemy' < 55
    overhead '50 - 55: Lesser Poison'
    gumpresponse 106 949095101
    waitforgump 949095101 5000
    gumpresponse 200 949095101
    waitforgump 949095101 5000
elseif skill 'Alchemy' < 75
    overhead '55 - 75: Lesser Magic Resist'
    gumpresponse 105 949095101
    waitforgump 949095101 5000
    gumpresponse 200 949095101
    waitforgump 949095101 5000
elseif skill 'Alchemy' < 95
    overhead '75 - 95: Greater Poison'
    gumpresponse 106 949095101
    waitforgump 949095101 5000
    gumpresponse 202 949095101
    waitforgump 949095101 5000
elseif skill 'Alchemy' < 115
    overhead '95 - 115: Deadly Poison'
    gumpresponse 106 949095101
    waitforgump 949095101 5000
    gumpresponse 204 949095101
    waitforgump 949095101 5000
elseif skill 'Alchemy' < 120
    overhead '115 - 120: Lethal Poison'
    gumpresponse 106 949095101
    waitforgump 949095101 5000
    gumpresponse 206 949095101
    waitforgump 949095101 5000
else
    overhead 'All done!'
endif
wait 3000

if findtype "Green Potion" backpack
    hotkey 'Set Last Target'
    waitfortarget 
    targettype "keg" backpack
    lifttype "Green Potion" 1
    drop lasttarget 0 0 0
    wait 500
endif
if findtype "Black Potion" backpack
    dclicktype "Black Potion" backpack
    wait 500
endif

if insysmsg "not hold any more" or insysmsg "bad idea to mix"
    overhead 'Storage Shelf!'
    organizer 2
    wait 1000
    if insysmsg "cannot be stored in a storage shelf"
        overhead 'Move keg!'
        organizer 1
        wait 1000     
    endif 
endif

loop