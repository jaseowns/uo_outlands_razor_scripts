# Alchemy Training with Shelf by Jaseowns
# UO Outlands - https://youtu.be/k1PqAGIB0Ls
# Requirements: a restock shelf (regs/bottles/mortar), a restock container (kegs)
#   restock agent 1:  setup as keg (and any other things you might not have on shelf, mortar pestle, etc)
# Additional Info: 
#   This script was updated 9/8/2021 to make sure your restock variables are setup
#   Edit line 27 to decrease skill check (default is 120)
if not varexist "restock_container"
    overhead "Target your restock container (kegs)"
    setvar "restock_container"
endif

if not varexist "restock_shelf"
    overhead "Target your restock shelf"
    setvar "restock_shelf"
endif

if not findtype "mortar and pestle" backpack or count 'Nightshade' < 10 or count "Spider's Silk" < 5 or not findtype "empty bottle%s%" backpack or not findtype "keg" backpack
    menu "restock_shelf" 1
    wait 500
    restock 1
    waitfortarget 
    target "restock_container"
    wait 1000
endif

if skill 'Alchemy' >= 120
    overhead 'Hit Max!' 88
    wait 5000
    replay
endif

if not findtype "mortar and pestle" backpack
    overhead 'No mortar and pestle!' 34
    wait 2000
    replay
endif

if count 'Nightshade' < 10
    overhead 'No Nightshade!' 34
    wait 2000
    replay
endif

if count "Spider's Silk" < 5
    overhead "No Spider's Silk" 34
    wait 2000
    replay
endif

if not findtype "keg" backpack
    overhead 'No keg!' 34
    wait 2000
    replay
endif

if not findtype "empty bottle%s%" backpack
    overhead 'No Bottles!' 34
    wait 2000
    replay
endif

clearsysmsg 
gumpclose 

dclicktype "mortar and pestle" backpack
waitforgump 949095101

if skill 'Alchemy' < 55
    overhead '50 - 55: Lesser Poison'
    gumpresponse 106
    waitforgump 949095101
    gumpresponse 200
    waitforgump 949095101
elseif skill 'Alchemy' < 75
    overhead '55 - 75: Lesser Magic Resist'
    gumpresponse 105
    waitforgump 949095101
    gumpresponse 200
    waitforgump 949095101
elseif skill 'Alchemy' < 95
    overhead '75 - 95: Greater Poison'
    gumpresponse 106
    waitforgump 949095101
    gumpresponse 202
    waitforgump 949095101
elseif skill 'Alchemy' < 115
    overhead '95 - 115: Deadly Poison'
    gumpresponse 106
    waitforgump 949095101
    gumpresponse 203
    waitforgump 949095101
elseif skill 'Alchemy' < 120
    overhead '115 - 120: Lethal Poison'
    gumpresponse 106
    waitforgump 949095101
    gumpresponse 204
    waitforgump 949095101
else
    overhead 'All done!'
endif
wait 3000

if findtype "Green Potion" backpack
    hotkey 'Set Last Target'
    waitfortarget 
    targettype "keg" backpack
    lifttype "Green Potion" 1
    drop lasttarget 0 0 0
    wait 500
endif
if findtype "Black Potion" backpack
    dclicktype "Black Potion" backpack
    wait 500
endif

if insysmsg "not hold any more" or insysmsg "bad idea to mix"
    overhead 'Storage Shelf!'
    organizer 2
    wait 1000
    if insysmsg "cannot be stored in a storage shelf"
        overhead 'Move keg!'
        organizer 1
        wait 1000     
    endif 
endif

loop



    
    