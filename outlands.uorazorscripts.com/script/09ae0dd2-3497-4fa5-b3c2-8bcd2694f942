# This is an automated backup - check out https://outlands.uorazorscripts.com/script/09ae0dd2-3497-4fa5-b3c2-8bcd2694f942 for latest
# Automation by Jaseowns
## Script: Ultimate Quick Escape through gate (M)
## Created by: Jaseowns#6694
#############################################

# Quick Escape through Moongate by Jaseowns
# UO Outlands
# Different flavor of https://outlands.uorazorscripts.com/script/3b44493a-da5e-4259-9b6f-25855b2f0eb2
# Description:
#   Take a moongate, chiv gate or dungeon gate as fast as possible
#   If you add a recall home script on line 15, you will recall
#   After you take the gate.  Check out https://youtu.be/JtWgeyr85uQ
#  
#   01/17/2023 - Added Grey SSC Moongate
#
removelist recallScriptList
createlist recallScriptList
// Set a recall script name in this list if you want to recall after taking moongate 
// Check out the livestream for question - https://youtu.be/JtWgeyr85uQ
if name = "Jaseowns" or name = "Jase Owns" or name = "of Time"
    pushlist recallScriptList "___RECALL___"
endif

#############
# Player gates
#############
while not gumpexists 3899019871 and findtype "blue moongate" ground 0 1 2 as jGate
    dclick jGate
endwhile

if gumpexists 3899019871 and findtype "blue moongate" ground 0 1 2 as jGate
    while gumpexists 3899019871 and findtype "blue moongate" ground 0 1 2 as jGate
        gumpresponse 2 3899019871
        wait 200
    endwhile
    stop
endif

#############
# Chiv gate
#############
while not gumpexists 3899019871 and findtype 30528 ground 2091 1 1 as jGate
    dclick jGate
endwhile

if gumpexists 3899019871 and findtype 30528 ground 2091 1 1 as jGate
    while gumpexists 3899019871 and findtype 30528 ground 2091 1 1 as jGate
        gumpresponse 2 3899019871
        wait 200
    endwhile
    stop
endif


#############
# Town/Dungeon Moongates - yellow, red, death, blue
#############
while not gumpexists 3773199800 and findtype "blue moongate" ground -1 1 2 as jGate
    dclick jGate
endwhile

if gumpexists 3773199800 and findtype "blue moongate" ground -1 1 2 as jGate
    while gumpexists 3773199800 and findtype "blue moongate" ground -1 1 2 as jGate
        gumpresponse 2 3773199800
        wait 200
    endwhile
    if not dead
        foreach recallScript in recallScriptList
            script recallScript
        endfor
    endif
    stop
endif

#############
# Grey Moongates - SSC
#############
while not gumpexists 3899019871 and findtype "moongate" ground -1 -1 2 as jGate
    dclick jGate
endwhile

if gumpexists 3899019871 and findtype "moongate" ground -1 -1 2 as jGate
    while gumpexists 3899019871 and findtype "moongate" ground -1 -1 2 as jGate
        gumpresponse 2 3899019871
        wait 200
    endwhile
    stop
endif

if findtype "moongate" ground -1 1 12 or findtype "blue moongate" ground -1 1 12 as jGate
    overhead "Move closer!" 34
    wait 50
    replay 
else 
    overhead "No gate nearby!" 34
endif