# This is an automated backup - check out https://outlands.uorazorscripts.com/script/cd615541-4b48-4e3a-b585-91aa77a7e0de for latest
# Automation by Jaseowns
## Script: Auto-Taming Script (TameOutlands.com)
## Created by: [BB] Khal Draco#6607
#############################################

///////////////////////////////////////////////////////
// TamingDragons v1.2
// Written by: Zell
//
// IMPORTANT
// This requires you to have a dagger in your backpack
//
// You require a tank for this
// Create a script variable called tank
// Make sure that tank is on follow, not guard
//
// 50-72.5 use rock guar as tank
// 75-102.5 use scarab as tank
// 105-120 use sand roach as tank
//
// The important thing here is to not use
// a scarab as a tank when taming scarabs
//
///////////////////////////////////////////////////////
//
// This is enforces selection of tank on client launch
if not listexists 'tankcount'
  createlist 'tankcount'
  overhead 'Please select your tanking pet'
  setvar 'tank'
endif
// Bandage timer creation
if not timerexists 'bandageTimer'
  createtimer 'bandageTimer'
  settimer 'bandageTimer' 0
endif
// Bandage my tank every 10 seconds, change as required
if timer 'bandageTimer' > 10000
  @dclicktype 3617 backpack
  pause 500
  target 'tank'
  pause 1000
  if insysmsg 'not damaged'
    settimer 'bandageTimer' 4000
  else
    settimer 'bandageTimer' 0
  endif
endif
// In case for some reason bandage doesnt get used on tank
// cancels target so bandage doesnt target grey untamed
hotkey 'Cancel current target'
// Apply pets to tame at specific levels, feel free to add
// them, see user doc for how
if skill 'Taming' < 60
  removelist 'pets'
  createlist 'pets'
  // Frog
  pushlist 'pets' '80'
elseif skill 'Taming' < 70
  removelist 'pets'
  createlist 'pets'
  //Cave Bear
  pushlist 'pets' '213'
elseif skill 'Taming' < 75
  removelist 'pets'
  createlist 'pets'
  //Scarab
  pushlist 'pets' '169'
elseif skill 'Taming' < 80
  removelist 'pets'
  createlist 'pets'
  // Drake Whelp
  pushlist 'pets' '718'
  // or Aegis Slime
  pushlist 'pets' '51'
elseif skill 'Taming' < 85
  removelist 'pets'
  createlist 'pets'
  // Deep Crawler
  pushlist 'pets' '315'
elseif skill 'Taming' < 90
  removelist 'pets'
  createlist 'pets'
  // Drake red body
  pushlist 'pets' '61'
  // Drake grey body
  pushlist 'pets' '60'
elseif skill 'Taming' < 95
  removelist 'pets'
  createlist 'pets'
  // Husk Crab
  pushlist 'pets' '729'
elseif skill 'Taming' < 100
  removelist 'pets'
  createlist 'pets'
  // or Dragon grey body
  pushlist 'pets' '12'
  // or Dragon red body
  pushlist 'pets' '59'
  // Dragon whelp
  //pushlist 'pets' '718'
elseif skill 'Taming' < 105
  removelist 'pets'
  createlist 'pets'
  // Snow Drift
  pushlist 'pets' '51'
elseif skill 'Taming' < 110
  removelist 'pets'
  createlist 'pets'
  // Drake grey body
  pushlist 'pets' '60'
  // Drake red body
  pushlist 'pets' '61'
  // Acarid
  //pushlist 'pets' '385'
elseif skill 'Taming' < 115
  removelist 'pets'
  createlist 'pets'
  // Aegis Minion
  pushlist 'pets' '776'
  // or Sphinx
  pushlist 'pets' '293'
elseif skill 'Taming' < 120
  removelist 'pets'
  createlist 'pets'
  // Grey Dragon body value
  pushlist 'pets' '12'
  // Red Dragon body value
  pushlist 'pets' '59'
  // or Colossal Sandroach
  pushlist 'pets' '388'
else
  overhead 'STOP! You have finished your journey'
  pause 1000
  replay
endif
clearsysmsg
// Search for appropriate pet to tame
overhead 'Searching'
if skill 'taming' < 70
  foreach pet in pets
    if findtype pet any any any 2
      @dclicktype 3922 backpack
      pause 1000
      @targettype pet any any any 2
      pause 500
      if skill 'herding' > 0
        menu 'last' 11
      else
        menu 'last' 10
      endif
      pause 1000
      hotkey 'Cancel current target'
      if insysmsg 'person'
          say 'all kill'
          pause 2500
          @target 'last'
          break
      else
        if insysmsg 'bladed item'
          if not timerexists 'tamingTimer'
            createtimer 'tamingTimer'
            settimer 'tamingTimer' 13000
          endif
          if timer 'tamingTimer' > 13000
            useskill 'Taming'
            pause 750
            @target 'last'
            settimer 'tamingTimer' 0
          endif
        endif    
      endif
    else
      if findtype pet
        overhead 'Fetching tame sub 70'
        pause 550
        say 'all kill'
        wft 1000
        @targettype pet
        pause 2500
        say 'all follow me'
      endif
    endif
  endfor
else
  foreach pet in pets
    @dclicktype 3922 backpack
    pause 1000
    @targettype pet any any any 2
    pause 500
    if skill 'herding' > 0
      menu 'last' 11
    else
      menu 'last' 10
    endif
    pause 1000
    hotkey 'Cancel current target'
    if insysmsg 'person'
        say 'all kill'
        pause 1000
        @target 'last'
        break
    else
      if insysmsg 'bladed item'
        if not timerexists 'tamingTimer'
          createtimer 'tamingTimer'
          settimer 'tamingTimer' 13000
        endif
        if timer 'tamingTimer' > 13000
          useskill 'Taming'
          pause 750
          @target 'last'
          settimer 'tamingTimer' 0
        endif
      endif    
    endif
  endfor
endif
replay