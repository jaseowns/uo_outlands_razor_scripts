# This is an automated backup - check out https://outlands.uorazorscripts.com/script/fcdb0d04-f69a-4708-9eb6-66a0d45495ea for latest
# Automation by Jaseowns
## Script: Auto Equip Weapons, Armor, Satchel, and Aspect (Dress)
## Created by: mushnar#0
#############################################

#######################################
############ MATERIAL HUES ############
#Leather/Board Log/Iron 0
#Dullhide/Dullwood/Dull Copper 2419
#Shadowhide/Shadowwood/Shadow Iron 2406
#Copperhide/Copperwood/Copper 2413
#Bronzehide/Bronzewood/Bronze 2418
#Goldenhide/Goldenwood/Gold 2213
#Rosehide/Rosewood/Agapite 2425
#Verehide/Verewood/Verite 2207
#Valehide/Valewood/Valorite 2219
#Avarhide/Avarwood/Avarite 1763
#######################################



#Set the base material hue for your weapon and armor
#Found with the >info command and targetting the armor or the table above
@setvar! weapon_material_hue 0
@setvar! armor_material_hue 0
@setvar! satchel_hue 0


#Set the armor type used to 1
@setvar! leather_armor 0
@setvar! bone_armor 0
@setvar! studded_armor 0
@setvar! ringmail_armor 0
@setvar! chainmail_armor 0
@setvar! plate_armor 1


#Set the weapon type used to 1
@setvar! arcane 0
@setvar! archery 0
@setvar! fencing 0
@setvar! mace_fighting 0
@setvar! magery 0
@setvar! swordsmanship 1
@setvar! wrestling 0


#Set shield to 1 only if you use a shield
@setvar! shield 0


#Set the satchel type used to 1
@setvar! potion_satchel 1
@setvar! wizard_satchel 0



#Set to 0 if you do not want the script to automatically equip your aspect
#Set the name of the aspect for your Weapon and Armor
@setvar! auto_equip_aspect 1

#DOES NOT WORK YET DO NOT USE - EDIT LINE 403 & 413
#@setvar! weapon_aspect_type "Holy"
#@setvar! armor_aspect_type "Holy"




###############################################
##### DEFAULT GLOBAL VALUES DO NOT CHANGE #####
@setvar! armor_aspect_equipped 0
@setvar! weapon_aspect_equipped 0


@setvar! all_armor_equipped 0
@setvar! pants_equipped 0
@setvar! chest_equipped 0
@setvar! helmet_equipped 0
@setvar! gloves_equipped 0
@setvar! arms_equipped 0
@setvar! neck_equipped 0


@setvar! weapon_equipped 0 
@setvar! shield_equipped 0
@setvar! satchel_equipped 0


removetimer "time_out"
if not timerexists "time_out"
    createtimer "time_out"
    settimer "time_out" 0
endif
###############################################



############################
#### BEGIN ARMOR DRESS #####
while all_armor_equipped = 0

    #Leather Armor
    if leather_armor = 1 
        if findtype 5061|5063|5070|5074|5075|7169|7175|7177|7179|7610|31191 backpack armor_material_hue as found_armor
            getlabel found_armor description

            if "exceptional" in description
                dclick found_armor
                @setvar! found_armor 0
            endif
        endif
    endif


    #Bone Armor
    if bone_armor = 1
        if findtype 5078|5201|5203|5204|5205|5207|7177|31003|31004 backpack armor_material_hue as found_armor
            getlabel found_armor description

            if "exceptional" in description
                dclick found_armor
                @setvar! found_armor 0
            endif
        endif
    endif


    #Studded Armor
    if studded_armor = 1 
        if findtype 5076|5078|5085|5089|5090|7170|7177|7181|7610|31011|31012 backpack armor_material_hue as found_armor
            getlabel found_armor description

            if "exceptional" in description
                dclick found_armor
                @setvar! found_armor 0
            endif
        endif
    endif


    #Ringmail Armor
    if ringmail_armor = 1
        if findtype 5078|5101|5103|5105|5106|5131|7177|7947|31009|31010 backpack armor_material_hue as found_armor
            getlabel found_armor description

            if "exceptional" in description
                dclick found_armor
                @setvar! found_armor 0
            endif
        endif
    endif


    #Chainmail Armor
    if chainmail_armor = 1 
        if findtype 5056|5059|5060|5063|5103|5106|7177|31005|31006|31007|31015 backpack armor_material_hue as found_armor
            getlabel found_armor description

            if "exceptional" in description
                dclick found_armor
                @setvar! found_armor 0
            endif
        endif
    endif


    #Plate Armor
    if plate_armor = 1
        if findtype 5129|5132|5135|5138|5139|5142|5143|5144|5146|7173|7177|31008 backpack armor_material_hue as found_armor
            getlabel found_armor description

            if "exceptional" in description
                dclick found_armor
                @setvar! found_armor 0
            endif
        endif
    endif


    if findlayer self 'pants' as equipped_item
        @setvar! pants_equipped 1
    endif

    if findlayer self 'innertorso' as equipped_item
        @setvar! chest_equipped 1
    endif


    if findlayer self 'head' as equipped_item
        @setvar! helmet_equipped 1
    endif


    if findlayer self 'gloves' as equipped_item
        @setvar! gloves_equipped 1
    endif


    if findlayer self 'arms' as equipped_item
        @setvar! arms_equipped 1
    endif


    if findlayer self 'neck' as equipped_item
        @setvar! neck_equipped 1
    endif
    
    if pants_equipped = 1 and chest_equipped = 1 and helmet_equipped = 1 and gloves_equipped = 1 and arms_equipped = 1 and neck_equipped = 1
        @setvar all_armor_equipped 1
    endif

    if timer "time_out" >= 10000
        overhead "ERROR - Script Timed Out" 33
        stop
    endif
endwhile
##### END ARMOR DRESS #####
###########################



##############################
##### BEGIN WEAPON DRESS #####
while weapon_equipped = 0

    #Arcane Staffs
    if arcane = 1
        if lhandempty
            if findtype 31017|31019|31021|31023|31025|31027|31029|31031|31033|31035|31037|31038|31041|31043|31045|31047|31049|31051|31053|31055 backpack weapon_material_hue as found_weapon
                getlabel found_weapon description

                if "exceptional" in description
                    dclick found_weapon
                    @setvar! found_weapon 0
                endif
            endif
        endif
    endif


    #Archery Weapons
    if archery = 1
        if lhandempty
            if findtype 3920|5042|5117|30990|30993|30994|30995|31184|31186 backpack weapon_material_hue as found_weapon
                getlabel found_weapon description

                if "exceptional" in description
                    dclick found_weapon
                    @setvar! found_weapon 0
                endif
            endif
        endif
    endif


    #Fencing Weapons
    if fencing = 1
        if lhandempty
            if findtype 3719|3922|3938|5121|5123|5125|30989|30992|30996|30997|30998|31176|31188 backpack weapon_material_hue as found_weapon
                getlabel found_weapon description

                if "exceptional" in description
                    dclick found_weapon
                    @setvar! found_weapon 0
                endif
            endif
        endif
    endif


    #Mace Fighting Weapons
    if mace_fighting = 1
        if lhandempty
            if findtype 3568|3713|3721|3932|5040|5044|5112|5127|5177|5179|5181|30991|30999|31000|31001|31014|31178|31180|31182 backpack weapon_material_hue as found_weapon
                getlabel found_weapon description

                if "exceptional" in description
                    dclick found_weapon
                    @setvar! found_weapon 0
                endif
            endif
        endif
    endif


    #Magery Weapons
    if magery = 1
        if lhandempty
            if findtype 3570|3571|3572|3573|3834 backpack weapon_material_hue as found_weapon
                getlabel found_weapon description

                if "exceptional" in description
                    dclick found_weapon
                    @setvar! found_weapon 0
                endif
            endif
        endif
    endif

    
    #Swordsmanship Weapons
    if swordsmanship = 1
        if lhandempty
            if findtype 3907|3908|3909|3911|3913|3915|3917|3934|3937|5046|5049|5115|5119|5182|5185|5187|30988|31128|31190 backpack weapon_material_hue as found_weapon
                getlabel found_weapon description

                if "exceptional" in description
                    dclick found_weapon
                    @setvar! found_weapon 0
                endif
            endif
        endif
    endif


    #Wrestling Weapons
    if wrestling = 1
        if lhandempty
            if findtype 3907|3908|3909|3911|3913|3915|3917|3934|3937|5046|5049|5115|5119|5182|5185|5187|30988|31128|31190 backpack weapon_material_hue as found_weapon
                getlabel found_weapon description

                if "exceptional" in description
                    dclick found_weapon
                    @setvar! found_weapon 0
                endif
            endif
        endif
    endif


    if findlayer self 'lefthand' as checkitem
        @setvar! weapon_equipped 1
    endif
    
    if timer "time_out" >= 10000
        overhead "ERROR - Script Timed Out" 33
        stop
    endif
endwhile
##### END WEAPON DRESS #####
############################




##############################
##### BEGIN SHIELD DRESS #####
#Shields
if shield = 1
    while shield_equipped = 0
        if lhandempty
            if findtype 7026|7027|7029|7031|7033|7034|7035|7107|7109|31002|31130 backpack weapon_material_hue as found_shield
                getlabel found_shield description

                if "exceptional" in description
                    dclick found_shield
                    @setvar! found_shield 0
                endif
            endif
        endif

        if findlayer self 'righthand' as checkitem
            @setvar! shield_equipped 1
        endif
    endwhile
endif
##### END SHIELD DRESS #####
############################



##############################
##### BEGIN SATCHEL DRESS #####
if satchel_equipped = 0
    if potion_satchel = 1
        if findtype 31262 backpack satchel_hue as satchel
            lift satchel
            wait 100
            drop "self" OuterBody
            wait 100
            dclick satchel

            @setvar! satchel_equipped 1
        endif
    endif


    if wizard_satchel = 1
        if findtype 30765 backpack satchel_hue as satchel
            lift satchel
            wait 100
            drop "self" OuterBody
            wait 100
            dclick satchel

            @setvar! satchel_equipped 1
        endif
    endif
endif
##### END SATCHEL DRESS #####
##############################



##############################
##### BEGIN EQUIP ASPECT #####
if auto_equip_aspect = 1
    if all_armor_equipped = 1 and weapon_equipped = 1
        say "[aspect"
        waitforgump 2424293173 500

        if armor_aspect_equipped = 0
            if ingump "Holy" 2424293173
                gumpresponse 17
                waitforgump 2424293173 100
                wait 100
                gumpresponse 17
                waitforgump 2424293173 100

                @setvar! armor_aspect_equipped 1
            endif
        endif
        
        wait 500
        
        if weapon_aspect_equipped = 0
            if ingump "Holy" 2424293173
                gumpresponse 8
                waitforgump 2424293173 100
                wait 100
                gumpresponse 8
                waitforgump 2424293173 100

                @setvar! weapon_aspect_equipped 1
            endif
        endif
    endif
    
    gumpclose 2424293173

endif
##### END EQUIP ASPECT #####
##############################
overhead "Let's Get Dangerous!"
stop