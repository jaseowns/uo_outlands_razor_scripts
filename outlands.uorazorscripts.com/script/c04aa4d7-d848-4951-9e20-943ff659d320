# This is an automated backup - check out https://outlands.uorazorscripts.com/script/c04aa4d7-d848-4951-9e20-943ff659d320 for latest
# Automation by Jaseowns
## Script: Ore / Lumber / Skinning Map Helper
## Created by: special_sy#0
#############################################

# Script by [DoS] Chumber
# Works with a SINGLE ressource map in your backpack
# Just go to the spot (or near it) and start the script 
# It will equip the right tool and start looking for the cart
@setvar option_dropMap 0

clearsysmsg
@setvar! myMap 0
if findtype 'map' self 2799 as myCurrentMap
    overhead "Lumber Map" 2799
    @setvar! myMap myCurrentMap
    if lhandempty
        if counttype 'hatchet' self = 0
            overhead "No Hatchets found, i cant work like this!" 32
            stop
        endif
        dclicktype 'hatchet'
        # long wait for queue and stuff
        wait 2000
    endif
    @setvar! whichHand lefthand
elseif findtype 'map' self 2796 as myCurrentMap
    overhead "Ore Map" 2796
    @setvar! myMap myCurrentMap
    if lhandempty
        if counttype 'pickaxe' self = 0
            overhead "No pickaxes found, i cant work like this!" 32
            stop
        endif
        dclicktype 'pickaxe'
        # long wait for queue and stuff
        wait 2000
    endif
    @setvar! whichHand righthand
elseif findtype 'map' self 2651 as myCurrentMap
    overhead "Skinning Map" 2651
    @setvar! myMap myCurrentMap
    if findtype 'Elven Spellblade' self as skinningBlade
        @setvar! whichHand skinningBlade
    else
        overhead "No skinning blade found, i cant work like this!" 32
        stop
    endif
endif

# look for the right spot
dclick whichHand
wft 500
target self
wait 2000

if findtype 'cart|stretched hide' 'ground' -1 -1 6 as cart
    overhead "You found it, lets go!" 2207
    while not insysmsg 'You have completely exhausted'
        if not hidden and skill 'hiding' >= 80
            useskill 'hiding'
        endif
        dclick whichHand
        wft 500
        target self
        wait 2000
    endwhile
    dclick cart
    overhead "Map done" 2207
    if option_dropMap = 1
        lift myMap
        wait 250
        droprelloc 0 0
    endif
    @setvar! myMap 0
endif

if myMap > 0
    overhead "Wrong spot? Lets try again!" 2207
    wait 2000
    replay
endif