# Dexxer Bot by Jaseowns
# UO Outlands
#######
# Working on this during the live streams: Monday / Wednesday / Friday
# https://youtube.com/jaseowns/live
#######
#####
##  Verison 0.2.0
##  Last Edit: 1/19/2023 - adding codex stance checks
###

# Basic Heal and Chivalry abilties to heal by Jaseowns
# UO Outlands
@setvar! showTargetOverheadMessages 1
@setvar! cdPreventOverheadSpam 2000

# Check potion buffs controllers
@setvar! checkStr 1
@setvar! checkDex 1
@setvar! checkMagic 1
@setvar! checkHealPot 1
@setvar! checkCurePot 1
@setvar! checkRefreshPot 1

################################
###### Chiv abilities
################
@setvar! useCleanseByFireAutomatically 1
@setvar! useCloseWoundsAutomatically 1
@setvar! useHolyLightAutomatically 1
@setvar! useDivineFuryAutomatically 1
@setvar! hpTriggerForHolyLight 35
@setvar! lowHealthTriggerForNobleSacrifice 40

@setvar! cdCleanseByFire 31000
@setvar! cdCloseWounds 31000
@setvar! cdHolyLight 31000
@setvar! cdDivineFury 31000
@setvar! cdNobleSacrifice 31000

################################
###### Chiv abilities overheads
################
@setvar! showChivUsageAsOverhead 1
@setvar! showChivUsageAsSystemMessage 1
@setvar! showChivUsageAsOverheadColor 88
@setvar! showChivUsageAsSystemMessageColor 88



# These control when the potion will be drank
# Edit to your liking
@setvar! hpCheckToTriggerHealPotion 35
@setvar! hpCheckToTriggerCurePotion 40
@setvar! stamDiffCheckToTriggerRedPotion 10

################################
###### Codex Stance Switch
################
@setvar! switchShieldCodexAutomatically 1
@setvar! switchToWardingStanceDuringBleed 1
@setvar! switchToWardingStanceDuringDisease 1
@setvar! switchToWardingStanceDuringPoison 1
@setvar! cdSwitchStance 10000

# Automatically Select Next Target
@setvar! autoTargetNextMob 1

###########
# If shit is not working right, adjust this number to ping
#####
@setvar! minimumWaitForPing 200
@setvar! minimumWaitForPingUnequipping 650

##############################################
###########  DO NOT EDIT BELOW THIS
#######

if dead 
    overhead "Rip" 34

    if gumpexists 2957810225
        gumpresponse 1 2957810225
    endif

    wait 5000
    replay
endif

if not varexist jaseCrimHealCheck
    gumpclose 3587807212
    while not gumpexists 3587807212
        say '[commands'
        wait 200
    endwhile
    gumpresponse 11 3587807212
    waitforgump 3587807212 5000
    gumpresponse 3 3587807212
    waitforgump 3587807212 5000
    if ingump "[PreventCriminalHealing" 3587807212
        if ingump "Prevented"
            gumpclose 3587807212
        else
            gumpresponse 29 3587807212
            waitforgump 3587807212 5000
            gumpclose 3587807212
        endif
    endif
    @setvar! jaseCrimHealCheck 1
endif

if insysmsg 'world is saving'
    for 60
        overhead 'Waiting for world save...'
        wait 100
        if insysmsg 'save complete'
            overhead 'Save complete - continue on!' 88
            break
        endif
    endfor
endif

if not findtype "White Potion" backpack
    @setvar! checkStr 0
endif

if not findtype "Blue Potion" backpack
    @setvar! checkDex 0
endif

if not findtype "Black Potion" backpack
    @setvar! checkMagic 0
endif

@setvar! cdAids 15000
if dex >= 100
    @setvar! cdAids 10000    
endif

if not timerexists preventOverheadSpamTimer
    createtimer preventOverheadSpamTimer
    settimer preventOverheadSpamTimer cdPreventOverheadSpam
endif

if not timerexists aidsTimer
    createtimer aidsTimer
    settimer aidsTimer cdAids
endif

@setvar! myKillTarget 0 
@setvar! targetInRange 0 

if not varexist myPreviousKillTarget
    @setvar myPreviousKillTarget 0     
endif

if autoTargetNextMob = 1
    @hotkey 'Target Closest Non-Friendly Monster'
    wait minimumWaitForPing
    if insysmsg "No one matching that was found"
        if timer preventOverheadSpamTimer >= cdPreventOverheadSpam
            overhead "found no one" 34
            settimer preventOverheadSpamTimer 0
        endif
    else
        @setvar! myKillTarget lasttarget 
    endif    
    
    if myKillTarget = 0
        // do nothing
    elseif not find lasttarget ground -1 -1 1
        if showTargetOverheadMessages = 1 and timer preventOverheadSpamTimer >= cdPreventOverheadSpam
            overhead "● ▼ OUT OF RANGE ▼ ●" 88 lasttarget
            settimer preventOverheadSpamTimer 0
        endif
        if find lasttarget ground -1 -1 8
            attack lasttarget 
        endif
    else
        @setvar! targetInRange 1
        if showTargetOverheadMessages = 1 and timer preventOverheadSpamTimer >= cdPreventOverheadSpam
            overhead "● ▼▼ HERE ▼▼ ●" 88 lasttarget    
            settimer preventOverheadSpamTimer 0
        endif
        attack lasttarget 
    endif
elseif dead myKillTarget
    @hotkey 'Target Closest Non-Friendly Monster'
    wait minimumWaitForPing
    if insysmsg "No one matching that was found"
        // do nothing
    else
        @setvar! myKillTarget lasttarget 
        if myKillTarget = 0
            // do nothing
        elseif not find lasttarget ground -1 -1 1
            // do nothing
        else
            @setvar! targetInRange 1
        endif
    endif    
elseif find myKillTarget ground -1 -1 1
    @setvar! targetInRange 1
endif
 
if targetInRange = 1
    # Eat food if we got it
    if not findbuff "food" and findtype "tray" backpack as jood
        dclick jood
        wait minimumWaitForPing
    endif

    @setvar! needStrBuff 0
    @setvar! needDexBuff 0
    @setvar! needMagicBuff 0
    @setvar! needHealPot 0
    @setvar! needCurePot 0
    @setvar! needRefreshPot 0

    if checkStr = 1 and not findbuff "Strength"
        @setvar! needStrBuff 1
    endif

    if checkDex = 1 and not findbuff "Agility"
        @setvar! needDexBuff 1 
    endif

    if checkMagic = 1 and not findbuff "Magic Resist Potion"
        @setvar! needMagicBuff 1 
    endif

    if checkHealPot = 1 and hp <= hpCheckToTriggerHealPotion
        @setvar! needHealPot 1 
    endif

    if checkCurePot = 1 and poisoned and hp <= hpCheckToTriggerCurePotion
        @setvar! needCurePot 1 
    endif

    if checkRefreshPot = 1 and diffstam >= stamDiffCheckToTriggerRedPotion
        @setvar! needRefreshPot 1 
    endif

    if needStrBuff = 1 and not findtype "White Potion" backpack
        @setvar! needStrBuff 0
    endif

    if needDexBuff = 1 and not findtype "Blue Potion" backpack
        @setvar! needDexBuff 0
    endif

    if needMagicBuff = 1 and not findtype "Black Potion" backpack
        @setvar! needMagicBuff 0
    endif

    if needHealPot = 1 and not findtype "Yellow Potion" backpack
        @setvar! needHealPot 0
    elseif needHealPot = 1 and findtype "Yellow Potion" backpack as item
        getlabel item healPotDesc
        if "next usable" in healPotDesc
            @setvar! needHealPot 0
        else
            @setvar! needHealPot 1
        endif
    endif

    if needCurePot = 1 and not findtype "Orange Potion" backpack
        @setvar! needCurePot 0
    endif

    if needRefreshPot = 1 and not findtype "Red Potion" backpack
        @setvar! needRefreshPot 0
    endif
    
    @setvar! jaseCanDrinkPotion 0

    if needStrBuff = 1 or needDexBuff = 1 or needMagicBuff = 1 or needHealPot = 1 or needCurePot = 1 or needRefreshPot = 1

        @setvar! jaseRightHand 0
        @setvar! jaseLeftHand 0
        @setvar! needTwoHandSwap 0
        @setvar! needShieldSwap 0

        if skill "Alchemy" >= 80
            @setvar! jaseCanDrinkPotion 1
        else
            if not findlayer self lefthand and not findlayer self righthand
                @setvar! jaseCanDrinkPotion 1
            else
                if findlayer self lefthand as myLefthand
                    @setvar! jaseLeftHand myLefthand
                endif
                if findlayer self righthand as myRightHand
                    @setvar! jaseRightHand myRightHand
                endif 
                if not lhandempty 
                    if not rhandempty 
                        @setvar! jaseCanDrinkPotion 0
                    else
                        // handle two handed weapons
                        getlabel jaseLeftHand desc
                        if skill "Swordsmanship" > 0
                           if "axe" in desc or "bardiche" in desc or "halberd" in desc
                                @setvar! jaseCanDrinkPotion 0
                                @setvar! needTwoHandSwap 1
                            else   
                                @setvar! jaseCanDrinkPotion 1
                            endif
                        elseif skill "Mace Fighting" > 0
                            if "staff" in desc or "crook" in desc or "war hammer" in desc
                                @setvar! jaseCanDrinkPotion 0
                                @setvar! needTwoHandSwap 1
                            else   
                                @setvar! jaseCanDrinkPotion 1
                            endif
                        elseif skill "Fencing" > 0
                            if "spear" in desc or "pitchfork" in desc
                                @setvar! jaseCanDrinkPotion 0
                                @setvar! needTwoHandSwap 1
                            else   
                                @setvar! jaseCanDrinkPotion 1
                            endif
                        elseif skill "Archery" > 0
                            if "bow" in desc or "heavy crossbow" in desc
                                @setvar! jaseCanDrinkPotion 0
                                @setvar! needTwoHandSwap 1
                            else   
                                @setvar! jaseCanDrinkPotion 1
                            endif
                        elseif skill "Wrestling" > 0
                            @setvar! jaseCanDrinkPotion 1
                        endif  
                    endif
                endif
                if not rhandempty 
                    if lhandempty 
                        @setvar! jaseCanDrinkPotion 1
                    else
                        @setvar! needShieldSwap 1
                    endif
                endif
            endif
        endif

        if jaseCanDrinkPotion = 1
            // do nothing
        else
            if needTwoHandSwap = 1
                if skill "Swordsmanship" > 0 and findtype "cutlass|broadsword|scimitar|viking sword|longsword|katana" backpack as item
                    dclick item
                    wait minimumWaitForPing
                elseif skill "Mace Fighting" > 0 and findtype "maul|war axe|war mace|mace|hammer pick|club" backpack as item
                    dclick item
                    wait minimumWaitForPing
                elseif skill "Fencing" > 0 and findtype "kryss|war fork|dagger" backpack as item
                    dclick item
                    wait minimumWaitForPing
                elseif skill "Archery" > 0 and findtype "crossbow" backpack as item
                    dclick item
                    wait minimumWaitForPing
                else
                    hotkey 'Arm/Disarm Left Hand'    
                    wait minimumWaitForPing
                endif
                @setvar! jaseCanDrinkPotion 1
            elseif needShieldSwap = 1
                overhead "we should be disarming left hand"
                while not lhandempty
                    hotkey 'Arm/Disarm Left Hand'
                    wait minimumWaitForPingUnequipping
                    while queued
                        wait 100
                    endwhile
                endwhile
                setvar! jaseCanDrinkPotion 1
            endif
        endif

        if jaseCanDrinkPotion = 1
            if needHealPot = 1
                if findtype "Yellow Potion" backpack as jotion
                    overhead "chug heal"
                    dclick jotion
                    wait minimumWaitForPing
                    cooldown "Yellow Potion" 10000
                else
                    overhead "No Heal potion found!" 34
                endif
            endif
            
            if needCurePot = 1
                if findtype "Orange Potion" backpack as jotion
                    overhead "chug cure"
                    dclick jotion
                    wait minimumWaitForPing
                else
                    overhead "No Cure potion found!" 34
                endif
            endif
            
            if needRefreshPot = 1
                if findtype "Red Potion" backpack as jotion
                    overhead "chug refresh"
                    dclick jotion
                    wait minimumWaitForPing
                else
                    overhead "No Refresh potion found!" 34
                endif
            endif
        
            if needStrBuff = 1
                if findtype "White Potion" backpack as jotion
                    overhead "chug"
                    dclick jotion
                    wait minimumWaitForPing
                else
                    overhead "No STR potion found!" 34
                endif
            endif
            
            if needDexBuff = 1
                if findtype "Blue Potion" backpack as jotion
                    overhead "chug"
                    dclick jotion
                    wait minimumWaitForPing
                else
                    overhead "No AGI potion found!" 34
                endif
            endif
            
            if needMagicBuff = 1
                if findtype "Black Potion" backpack as jotion
                    overhead "chug"
                    dclick jotion
                    wait minimumWaitForPing
                else
                    overhead "No MR potion found!" 34
                endif
            endif

            if needTwoHandSwap = 1
                dclick jaseLeftHand
                wait minimumWaitForPing
            elseif needShieldSwap = 1
                hotkey 'Arm/Disarm Left Hand'
                wait minimumWaitForPingUnequipping
            endif
        endif
    endif
    
endif

#############
### CODEX
#############
if skill "Parrying" >= 80 and findtype "shield codex" backpack as item

    if not timerexists jaseSwitchStanceTimer
        settimer jaseSwitchStanceTimer cdSwitchStance
    endif
    
    if not varexist canUseShieldFinisher
        @setvar! canUseShieldFinisher 1
    endif
    
    while not gumpexists 1066647553
        overhead "Loading hotbar"
        yell "[CodexHotBar"
        wait minimumWaitForPing
    endwhile

    if switchShieldCodexAutomatically = 1 and timer jaseSwitchStanceTimer >= cdSwitchStance
        getlabel item codexDesc
        
        // make sure we are in warding stance
        @setvar! switchToWarding 0

        if switchToWardingStanceDuringBleed = 1 and findbuff "bleed" 
            @setvar! switchToWarding 1
        elseif switchToWardingStanceDuringDisease = 1 and findbuff "disease" 
            @setvar! switchToWarding 1
        elseif switchToWardingStanceDuringPoison = 1 and findbuff "disease" 
            @setvar! switchToWarding 1
        endif

        if switchToWarding = 1
            if "Warding" in codexDesc
                // we good
            else
                yell [ShieldsStance2
                settimer jaseSwitchStanceTimer 0
                wait 200
            endif    
            
            if "Barrier" in codexDesc
                // we good
            elseif canUseShieldFinisher = 1
                yell [ShieldsFinisher2
                wait 200
                if insysmsg "You must first reach rank 5"
                    @setvar! canUseShieldFinisher 0
                endif
            endif    
        else
            if "Bulwark" in codexDesc
                // we good
            else
                yell [ShieldsStance5
                settimer jaseSwitchStanceTimer 0
                wait 200
            endif
            
            if "Last Stand" in codexDesc
                // we good
            elseif canUseShieldFinisher = 1
                yell [ShieldsFinisher1
                wait 200
                if insysmsg "You must first reach rank 5"
                    @setvar! canUseShieldFinisher 0
                endif
            endif
        endif
    endif
endif

# Use Taste ID if possible
if skill "Taste Identification" >= 50 and not findbuff "herb"
    useskill 'tasteidentification'
    wft 500
    target backpack
endif

if skill "Camping" > 0
    if not findbuff "campfire"
        overhead "Camping Buff" 88
        if not findtype "campfire" ground -1 -1 2 and findtype "kindling" as jindling
            dclick jindling
            wait minimumWaitForPing
        endif
        if not findtype "campfire" ground -1 -1 2
            overhead "We have camping, but can not make a campfire!" 34
        else
            overhead "Make sure you stand by your camp fire!" 88
        endif
    endif
endif

if skill "Tracking" > 0
    if not findbuff "tracking"
        clearsysmsg
        while not gumpexists 4267467659
            useskill 'tracking'
            wait minimumWaitForPing
        endwhile
        if gumpexists 4267467659
            while not insysmsg "You will now hunt all hostile players."
                gumpresponse 8 4267467659
                waitforgump 4267467659 5000
            endwhile
            gumpresponse 6 4267467659
            wait minimumWaitForPing
        endif
        gumpclose 4267467659
    endif
endif

if skill "Chivalry" >= 50

    @setvar! chivSymbols 0
    @setvar! cdCleanseByFire 31000
    @setvar! cdCloseWounds 31000
    @setvar! cdHolyLight 31000
    @setvar! cdDivineFury 31000
    @setvar! cdNobleSacrifice 31000

    if not timerexists nobleSacrificeTimer
        createtimer nobleSacrificeTimer
        settimer nobleSacrificeTimer cdCleanseByFire
    endif

    if not timerexists cleanseByFireTimer
        createtimer cleanseByFireTimer
        settimer cleanseByFireTimer cdCleanseByFire
    endif

    if not timerexists closeWoundsTimer
        createtimer closeWoundsTimer
        settimer closeWoundsTimer cdCloseWounds
    endif
    
    if not timerexists holyLightTimer
        createtimer holyLightTimer
        settimer holyLightTimer cdHolyLight
    endif
    
    if not timerexists divineFuryTimer
        createtimer divineFuryTimer
        settimer divineFuryTimer cdDivineFury
    endif
    
    while not gumpexists 1387930325
        say "[ChivalryHotbar"
        wait 200
    endwhile
    
    if gumpexists 1387930325
        if ingump "12/" 1387930325
            @setvar! chivSymbols 12
        elseif ingump "13/" 1387930325
            @setvar! chivSymbols 13
        elseif ingump "14/" 1387930325
            @setvar! chivSymbols 14
        elseif ingump "15/" 1387930325
            @setvar! chivSymbols 15
        elseif ingump "16/" 1387930325
            @setvar! chivSymbols 16
        elseif ingump "17/" 1387930325
            @setvar! chivSymbols 17
        elseif ingump "18/" 1387930325
            @setvar! chivSymbols 18
        elseif ingump "19/" 1387930325
            @setvar! chivSymbols 19
        elseif ingump "20/" 1387930325
            @setvar! chivSymbols 20
        elseif ingump "21/" 1387930325
            @setvar! chivSymbols 21
        elseif ingump "11/" 1387930325
            @setvar! chivSymbols 11
        elseif ingump "10/" 1387930325
            @setvar! chivSymbols 10
        elseif ingump "9/" 1387930325
            @setvar! chivSymbols 9
        elseif ingump "8/" 1387930325
            @setvar! chivSymbols 8
        elseif ingump "7/" 1387930325
            @setvar! chivSymbols 7
        elseif ingump "6/" 1387930325
            @setvar! chivSymbols 6
        elseif ingump "5/" 1387930325
            @setvar! chivSymbols 5
        elseif ingump "4/" 1387930325
            @setvar! chivSymbols 4
        elseif ingump "3/" 1387930325
            @setvar! chivSymbols 3
        elseif ingump "2/" 1387930325
            @setvar! chivSymbols 2
        elseif ingump "1/" 1387930325
            @setvar! chivSymbols 1
        elseif ingump "0/" 1387930325
            @setvar! chivSymbols 0
        endif
    endif
    
    if hp <= lowHealthTriggerForNobleSacrifice and skill "Chivalry" >= 80
         if timer nobleSacrificeTimer >= cdNobleSacrifice and 4 < chivSymbols
            if showChivUsageAsOverhead = 1
                overhead "Noble Sacrifice" showChivUsageAsOverheadColor
            endif
            if showChivUsageAsSystemMessage = 1
                sysmsg "Noble Sacrifice" showChivUsageAsSystemMessageColor
            endif
            yell "[NobleSacrifice"
            wft 500
            hotkey "Target Self"
            settimer nobleSacrificeTimer 0
            cooldown NobleSacrifice cdNobleSacrifice
            
            if gumpexists 1387930325
                if ingump "12/" 1387930325
                    @setvar! chivSymbols 12
                elseif ingump "13/" 1387930325
                    @setvar! chivSymbols 13
                elseif ingump "14/" 1387930325
                    @setvar! chivSymbols 14
                elseif ingump "15/" 1387930325
                    @setvar! chivSymbols 15
                elseif ingump "16/" 1387930325
                    @setvar! chivSymbols 16
                elseif ingump "17/" 1387930325
                    @setvar! chivSymbols 17
                elseif ingump "18/" 1387930325
                    @setvar! chivSymbols 18
                elseif ingump "19/" 1387930325
                    @setvar! chivSymbols 19
                elseif ingump "20/" 1387930325
                    @setvar! chivSymbols 20
                elseif ingump "21/" 1387930325
                    @setvar! chivSymbols 21
                elseif ingump "11/" 1387930325
                    @setvar! chivSymbols 11
                elseif ingump "10/" 1387930325
                    @setvar! chivSymbols 10
                elseif ingump "9/" 1387930325
                    @setvar! chivSymbols 9
                elseif ingump "8/" 1387930325
                    @setvar! chivSymbols 8
                elseif ingump "7/" 1387930325
                    @setvar! chivSymbols 7
                elseif ingump "6/" 1387930325
                    @setvar! chivSymbols 6
                elseif ingump "5/" 1387930325
                    @setvar! chivSymbols 5
                elseif ingump "4/" 1387930325
                    @setvar! chivSymbols 4
                elseif ingump "3/" 1387930325
                    @setvar! chivSymbols 3
                elseif ingump "2/" 1387930325
                    @setvar! chivSymbols 2
                elseif ingump "1/" 1387930325
                    @setvar! chivSymbols 1
                elseif ingump "0/" 1387930325
                    @setvar! chivSymbols 0
                endif
            endif
        endif
    endif
    
    if poisoned and skill "Chivalry" >= 60
        if useCleanseByFireAutomatically = 1 and timer cleanseByFireTimer >= cdCleanseByFire and 2 < chivSymbols
            if showChivUsageAsOverhead = 1
                overhead "Cleanse by fire" showChivUsageAsOverheadColor
            endif
            if showChivUsageAsSystemMessage = 1
                sysmsg "Cleanse by fire" showChivUsageAsSystemMessageColor
            endif
            yell "[CleanseByFire"
            settimer cleanseByFireTimer 0
            cooldown CleanseByFire cdCleanseByFire
        endif
    elseif findbuff "bleed" or findbuff "disease" and skill "Chivalry" >= 70
        if useCloseWoundsAutomatically = 1 and timer closeWoundsTimer >= cdCleanseByFire and 3 < chivSymbols
            if showChivUsageAsOverhead = 1
                overhead "Close Wounds" showChivUsageAsOverheadColor
            endif
            if showChivUsageAsSystemMessage = 1
                sysmsg "Close Wounds" showChivUsageAsSystemMessageColor
            endif
            yell "[CloseWounds"
            settimer closeWoundsTimer 0
            cooldown CloseWounds cdCloseWounds
        endif
    endif
    
    if gumpexists 1387930325
        if ingump "12/" 1387930325
            @setvar! chivSymbols 12
        elseif ingump "13/" 1387930325
            @setvar! chivSymbols 13
        elseif ingump "14/" 1387930325
            @setvar! chivSymbols 14
        elseif ingump "15/" 1387930325
            @setvar! chivSymbols 15
        elseif ingump "16/" 1387930325
            @setvar! chivSymbols 16
        elseif ingump "17/" 1387930325
            @setvar! chivSymbols 17
        elseif ingump "18/" 1387930325
            @setvar! chivSymbols 18
        elseif ingump "19/" 1387930325
            @setvar! chivSymbols 19
        elseif ingump "20/" 1387930325
            @setvar! chivSymbols 20
        elseif ingump "21/" 1387930325
            @setvar! chivSymbols 21
        elseif ingump "11/" 1387930325
            @setvar! chivSymbols 11
        elseif ingump "10/" 1387930325
            @setvar! chivSymbols 10
        elseif ingump "9/" 1387930325
            @setvar! chivSymbols 9
        elseif ingump "8/" 1387930325
            @setvar! chivSymbols 8
        elseif ingump "7/" 1387930325
            @setvar! chivSymbols 7
        elseif ingump "6/" 1387930325
            @setvar! chivSymbols 6
        elseif ingump "5/" 1387930325
            @setvar! chivSymbols 5
        elseif ingump "4/" 1387930325
            @setvar! chivSymbols 4
        elseif ingump "3/" 1387930325
            @setvar! chivSymbols 3
        elseif ingump "2/" 1387930325
            @setvar! chivSymbols 2
        elseif ingump "1/" 1387930325
            @setvar! chivSymbols 1
        elseif ingump "0/" 1387930325
            @setvar! chivSymbols 0
        endif
    endif
    
    if useHolyLightAutomatically = 1 and hp <= hpTriggerForHolyLight and skill "Chivalry" >= 95
        
        @setvar! doubleCheckHolyLight 1
    
        if cdAids = 10000 and timer aidsTimer >= 9000
            // skip holy light
            overhead "Skipping Holy Light" 34
            @setvar! doubleCheckHolyLight 0
            // unless we gonna die
            if hp <= 20 or poisoned
                overhead "Just kidding, do not wanna die" 34
                @setvar! doubleCheckHolyLight 1
            endif
        endif
        
        if doubleCheckHolyLight = 1
            if timer holyLightTimer >= cdHolyLight and 5 < chivSymbols
                if showChivUsageAsOverhead = 1
                    overhead "Holy Light" showChivUsageAsOverheadColor
                endif
                if showChivUsageAsSystemMessage = 1
                    sysmsg "Holy Light" showChivUsageAsSystemMessageColor
                endif
                yell "[HolyLight"
                settimer holyLightTimer 0
                cooldown HolyLight cdHolyLight
            endif
        endif
    endif
    
    if targetInRange = 1 and useDivineFuryAutomatically = 1 and skill "Chivalry" >= 85
        if timer divineFuryTimer >= cdDivineFury and 4 < chivSymbols
            if showChivUsageAsOverhead = 1
                overhead "Divine Fury" showChivUsageAsOverheadColor
            endif
            if showChivUsageAsSystemMessage = 1
                sysmsg "Divine Fury" showChivUsageAsSystemMessageColor
            endif
            yell "[DivineFury"
            settimer divineFuryTimer 0
            cooldown DivineFury cdDivineFury
        endif
    endif
endif

if skill "Healing" > 0
        
    @setvar! cdAids 15000
    if dex >= 100
        @setvar! cdAids 10000    
    endif

    if not timerexists aidsTimer
        createtimer aidsTimer
        settimer aidsTimer cdAids
    endif

    if findtype "clean bandage%s%" backpack
        if skill "Chivalry" >= 80 and timer nobleSacrificeTimer <= 5000 and timer aidsTimer >= 5000 and hp < maxhp
            hotkey 'Bandage Self'
            settimer aidsTimer 0
        elseif not bandaging and hp < maxhp
            hotkey 'Bandage Self'
            settimer aidsTimer 0
        elseif not bandaging and poisoned
            hotkey 'Bandage Self'
            settimer aidsTimer 0
        elseif not bandaging and findbuff "bleed"
            hotkey 'Bandage Self'
            settimer aidsTimer 0
        elseif not bandaging and findbuff "disease"
            hotkey 'Bandage Self'
            settimer aidsTimer 0
        endif
    else
        overhead "No bandages" 34
    endif
endif
wait 200
loop
