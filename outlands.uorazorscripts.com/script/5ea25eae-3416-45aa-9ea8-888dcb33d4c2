# This is an automated backup - check out https://outlands.uorazorscripts.com/script/5ea25eae-3416-45aa-9ea8-888dcb33d4c2 for latest
# Automation by Jaseowns
## Script: Complex Single-Button Hide > Stealth
## Created by: thisideasucks#0
#############################################

# Complex Single-Button Hide > Stealth

# Set repeats, wait time, and message timeouts
@setvar! globalWait 200
@setvar! messageTimeout 2000
@setvar! repeatHide 0
@setvar! repeatStealth 1

if not timerexists warningTimer
    settimer warningTimer 2000
endif

if not timerexists notifyTimer
    settimer notifyTimer 2000
endif

if not hidden
    clearsysmsg
    useskill 'Hiding'
    wait globalWait
    if hidden
        overhead 'Hidden' 88
    else
        if timer warningTimer > messageTimeout
            if insysmsg "You must wait a few moments to use another skill"
                overhead "Skill cooldown!!" 38
                settimer warningTimer 0
            elseif not hidden
                overhead 'Not Hidden!!' 38
                settimer warningTimer 0
            endif
        endif
        wait globalWait
        if repeatHide = 1
            loop
        endif
    endif
elseif hidden
    clearsysmsg
    if timer notifyTimer > messageTimeout
        overhead 'Attempting stealth' 94
        settimer notifyTimer 0
    endif
    useskill 'Stealth'
    wait globalWait
    if insysmsg "You begin to move quietly" or insysmsg "You feel ready to continue stealthing" or insysmsg "You will automatically stealth when out of steps"
        overhead 'Stealthed' 88
    else
        if timer warningTimer > messageTimeout
            overhead 'Not Stealthed!!' 38
            settimer warningTimer 0
        endif
        wait globalWait
        if repeatStealth = 1
            loop
        endif
    endif
endif