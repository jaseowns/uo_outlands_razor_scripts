# This is an automated backup - check out https://outlands.uorazorscripts.com/script/ac637102-a701-4680-a780-faf3efb5c274 for latest
# Automation by Jaseowns
## Script: Bapeth's Trapper Background Script
## Created by: barryroser#0
#############################################

# Bapeths Trapper Background Script
#
# This script works well with Arc Trapper and Summoner Trapper
#
# Upload Date : Nov 26 2024
# 
# Keep this script playing at all times
# If you use another script, such as "BOOM BOOM by Bapeth"
# Loop this background script at the end of your other script(s)
#
# "Required Cooldowns"
#
# "Move" Cooldown should be set to "0.3 seconds"
# "Bandage" cooldown can be blank
#
# "What it does"
#
# Bandage
# Cure
# Potion Buff
# Cleansing Brew (only while in warmode)
# Warn when str/resist/reflect/reactive are needed
# Shield Codex - Shield bash, Warding, Bullwark
#
# "Time Sense Container Search"
# Enable this option to display and loot containers in "Time Dungeon"
# The "Time Codex" acts like a switch to turn this setting on/off, open your codex while the script is playing to activate switch
# Stop the script if you need to interact with the Time Codex normally
#
# "Auto-Recycler"
# This feature will auto recycle bad items and keep the good ones - To use this feature:
# You must be carrying at least one "item identification wand" and at least one "recycler tool" type (Tinker Tools, Sewing Kit, Saw, Tongs, or Scribes Pen)
# The recycle will happen when the player begins picking a fresh chest (Double Click Scissors to activate auto recycle manually)
#
# "Loot-Sense & Vacuum Mode"
# This function will overhead display *loot* above items on the ground and pick them up as you pass by (2 tiles)
# Be aware that this may make you overweight from large gold piles from time to time
# To toggle this feature on/off (default is off) "Double click a dagger" (suggest a blessed newbied dagger)
#
# Script starts here...

if skill "Parrying" >= 80 and mana >= 6
    if not timerexists bleedbuffcheck and findbuff "Bleed" and not findbuff "Diseased"
        createtimer bleedbuffcheck
        say "[ShieldsStance2" 45
        getlabel backpack ping
        say "[ShieldsFinisher2" 45
    elseif timerexists bleedbuffcheck and not findbuff "Bleed" and not findbuff "Diseased"
        removetimer bleedbuffcheck
        pause 800
        getlabel backpack ping
        say "[ShieldsStance1" 45
        getlabel backpack ping
        say "[ShieldsFinisher1" 45
    endif
    if not timerexists diseasebuffcheck and findbuff "Diseased" and not findbuff "Bleed"
        createtimer diseasebuffcheck
        say "[ShieldsStance2" 45
        getlabel backpack ping
        say "[ShieldsFinisher2" 45
    elseif timerexists diseasebuffcheck and not findbuff "Diseased" and not findbuff "Bleed"
        removetimer diseasebuffcheck
        pause 800
        getlabel backpack ping
        say "[ShieldsStance1" 45
        getlabel backpack ping
        say "[ShieldsFinisher1" 45
    endif
    if not hidden and not findbuff "Diseased" and not findbuff "Bleed"
        if not timerexists "bullwarktime"
            createtimer bullwarktime
        endif    
        if cooldown "Move"
            settimer bullwarktime 0
        endif
        if not timerexists bullwarkactive and timer bullwarktime >= 3000
            say "[ShieldsStance5" 45
            createtimer bullwarkactive
        endif
        if timerexists bullwarkactive and timer bullwarktime < 3000
            say "[ShieldsStance1" 45
            removetimer bullwarkactive
        endif
    endif
endif

if not gumpexists 3954121934
    say '[abilityhotbar'
    waitforgump 3954121934 500
endif

if skill "Spirit Speak" >= 80 and findtype "an earth elemental" ground -1 -1 1 as Summon
    if not find "bsummon1" ground -1 -1
        while findtype "an earth elemental" ground -1 -1 1 as bsummon1
            if noto bsummon1 = "friend"
                @setvar "bsummon1" bsummon1
                # Name your summon if you like (no spaces must be unique)
                rename bsummon1 "LootGolem"
            endif
            break
        endwhile
        getlabel backpack ping
    elseif not find "bsummon2" ground -1 -1
        while findtype "an earth elemental" ground -1 -1 1 as bsummon2
            if noto bsummon2 = "friend"
                @setvar "bsummon2" bsummon2
                # Name your summon if you like (no spaces must be unique)
                rename bsummon2 "LootGolem"
            endif
            break
        endwhile
    endif
endif

if not timerexists bappvp
    createtimer bappvp
    settimer bappvp 5000
elseif timer bappvp >= 5000
    overhead "Boom time..." 78
    settimer bappvp 0
endif

if not timerexists rptimer
    createtimer rptimer
    settimer rptimer 1200
endif

if not find "placedtrap" ground and findtype 51163 ground -1 -1 8 as smokingtrap
    @setvar "placedtrap" smokingtrap
endif

if not timerexists armedtimer
    createtimer armedtimer
    settimer armedtimer 3000
endif
if find "placedtrap" ground as smokingtrap and timer armedtimer >= 3000
    overhead "*Armed*" 38 smokingtrap
    settimer armedtimer 0
endif

if skill "Alchemy" >= 10 and not hidden
    if not findbuff "Strength" and findtype "White Potion" backpack as wpot
        dclick wpot
        pause 500
        getlabel backpack ping
    endif
    if not findbuff "Agility" and findtype "Blue Potion" backpack as bpot
        dclick bpot
        pause 500
        getlabel backpack ping
    endif
    if not findbuff "Magic Resist Potion" and findtype "Black Potion" backpack as mpot
        dclick mpot
        pause 500
        getlabel backpack ping
    endif
endif

if hp = maxhp
    //donothing
elseif not bandaging and findtype 3617 backpack
    hotkey "Bandage Self"
    cooldown "Bandage" 7000
endif

if skill "Magery" >= 80
    if timerexists reflect and not findbuff "Magic Reflection"
        if timer reflect > 30000
            overhead "--Cast Reflect--" 2122
            settimer reflect 12000
        endif
    endif
    if findbuff "Magic Reflection" and timerexists reflect
        removetimer reflect
    endif
    if not findbuff "Magic Reflection" and not timerexists reflect
        createtimer reflect
    endif
endif
if skill "Magery" >= 20
    if timerexists reactive and not findbuff "Reactive Armor"
        if timer reactive > 30000
            overhead "--Cast Reactive--" 2117
            settimer reactive 13500
        endif
    endif
    if findbuff "Reactive Armor" and timerexists reactive
        removetimer reactive
    endif
    if not findbuff "Reactive Armor" and not timerexists reactive
        createtimer reactive
    endif
endif
if warmode and not hidden and not timerexists brew and not targetexists and findbuff "Bleed" and findtype 50675 backpack as cbrew
    if not timerexists bleedmobdelay
        createtimer bleedmobdelay
    endif
    if timer bleedmobdelay >= 1
        while queued
            //donothing
        endwhile
        dclick cbrew
        createtimer brew
        removetimer bleedmobdelay
    endif
elseif warmode and not hidden and not timerexists brew and not targetexists and findbuff "Diseased" and findtype 50675 backpack as cbrew
    while queued
        //donothing
    endwhile
    dclick cbrew
    createtimer brew
endif
if timerexists brew
    if timer brew >= 120000
        removetimer brew
    endif
endif
if not hidden and not casting and findbuff "Weaken" and not targetexists and findtype "White Potion" backpack as stpot
    while queued
        //donothing
    endwhile
    dclick stpot
endif
if not timerexists topupstr
    createtimer topupstr
    settimer topupstr 30000
elseif timer topupstr >= 30000 and not hidden and not casting and str = 100 and findtype "White Potion" backpack as stpot
    while queued
        //donothing
    endwhile
    overhead "--Drink Strength--" 2041
    settimer topupstr 0
endif
if not timerexists topupres
    createtimer topupres
    settimer topupres 30000
elseif timer topupres >= 30000 and not hidden and not casting and not findbuff "Magic Resist Potion" and findtype "Black Potion" backpack as respot
    while queued
        //donothing
    endwhile
    overhead "--Drink Resist--" 2031
    settimer topupres 0
endif
if not targetexists and not hidden and not casting and stam <= 4 and findtype "Red Potion" backpack as redpot
    while queued
        //donothing
    endwhile
    dclick redpot
endif
if not hidden and paralyzed and not targetexists and findtype "pouch" backpack 38
    say "[Pouch"
endif
if not hidden and not casting and poisoned and not targetexists and findtype "Orange Potion" backpack as curepot
    while queued
        //donothing
    endwhile
    dclick curepot
endif

if insysmsg "What should I use these scissors on?" and findtype "sewing kit|saw|pen and ink|tongs|tool kit" backpack as breakdowntool
    hotkey "Cancel Current Target"
    if findtype 20496 backpack as IDwand
        clearsysmsg 
        if findtype 3834 backpack 0 as bbook
            @ignore bbook
        endif
        while findtype 5056|5059|5060|5061|5063|5070|5074|5075|5076|5078|5085|5089|5090|5101|5103|5105|5106|5129|5131|5132|5135|5138|5139|5142|5143|5144|5146|5201|5203|5204|5205|5207|7169|7170|7173|7175|7177|7179|7181|7610|7947|31003|31004|31005|31006|31007|31008|31009|31010|31011|31012|31015|31191|31017|31019|31021|31023|31025|31027|31029|31031|31033|31035|31037|31038|31041|31043|31045|31047|31049|31051|31053|31055|3834|3740|3742|3762|3763|10245|20006|20008|20010|20012|20014|20016|7026|7027|7029|7031|7033|7034|7035|7107|7109|31002|31130|3920|5042|5117|30990|30993|30994|30995|31184|31186|3719|3938|5121|5123|5125|30989|30992|30996|30997|30998|31176|31188|3568|3713|3721|3932|5040|5044|5112|5127|5177|5179|5181|30991|30999|31000|31001|31014|31178|31180|31182|3909|3911|3913|3915|3917|3934|3937|5046|5049|5115|5119|5182|5185|5187|30988|31128|31190|22187|31141|31142|31169|31172 backpack as IDitem
            getlabel IDitem desc
            if "unidentified" in desc
                while queued
                    //donothing
                endwhile
                dclick IDwand
                wft 1000
                target IDitem
                waitforsysmsg "You identify the item." 1000
                getlabel IDitem desc
                if "aspect" in desc or "training" in desc or "vanquishing" in desc or "supremely" in desc or "slaying" in desc or "enticement" in desc or "invulnerability" in desc or "power" in desc or "exceedingly accurate force" in desc or "exceedingly potent" in desc or "valorite" in desc or "valehide" in desc or "valewood" in desc or "avarite" in desc or "avarhide" in desc or "avarwood" in desc or "verite" in desc or "verehide" in desc or "verewood" in desc or "agapite" in desc or "rosehide" in desc or "rosewood" in desc or "fortification" in desc
                    @ignore IDitem
                    overhead "This one is worth keeping..." 2091
                    overhead "*strong magic*" 2091 IDitem
                    say "[surprise" 2091
                elseif "durable" in desc or "substantial" in desc or "massive" in desc or "fortified" in desc or "indestructible" in desc or "exceptional" in desc or "accurate" in desc or "surpassingly" in desc or "eminently" in desc or "exceedingly" in desc or "ruin" in desc or "might" in desc or "force" in desc or "power" in desc or "mastercrafted" in desc or "defense" in desc or "guarding" in desc or "hardening" in desc or "potent" in desc or "melodious" in desc
                    overhead "*trash*" 1779 IDitem
                    while not gumpexists 949095101
                        while queued
                            //donothing
                        endwhile
                        dclick breakdowntool
                        waitforgump 949095101 1000
                    endwhile
                    while not ingump "Recycle Single Item" 949095101
                        gumpresponse 2
                        waitforgump 949095101 1000
                    endwhile                                
                    gumpresponse 3
                    waitforgump 949095101 1000
                    target IDitem
                    getlabel backpack ping
                    pause 300
                    gumpclose 949095101
                else
                    @ignore IDitem
                endif
            elseif "aspect" in desc or "training" in desc or "vanquishing" in desc or "supremely" in desc or "slaying" in desc or "enticement" in desc or "invulnerability" in desc or "power" in desc or "exceedingly accurate force" in desc or "exceedingly potent" in desc or "valorite" in desc or "valehide" in desc or "valewood" in desc or "avarite" in desc or "avarhide" in desc or "avarwood" in desc or "verite" in desc or "verehide" in desc or "verewood" in desc or "agapite" in desc or "rosehide" in desc or "rosewood" in desc or "fortification" in desc
                @ignore IDitem
            elseif "durable" in desc or "substantial" in desc or "massive" in desc or "fortified" in desc or "indestructible" in desc or "exceptional" in desc or "accurate" in desc or "surpassingly" in desc or "eminently" in desc or "exceedingly" in desc or "ruin" in desc or "might" in desc or "force" in desc or "power" in desc or "mastercrafted" in desc or "defense" in desc or "guarding" in desc or "hardening" in desc or "potent" in desc or "melodious" in desc
                overhead "*trash*" 1779 IDitem
                while not gumpexists 949095101
                    while queued
                        //donothing
                    endwhile
                    dclick breakdowntool
                    waitforgump 949095101 1000
                endwhile
                while not ingump "Recycle Single Item" 949095101
                    gumpresponse 2
                    waitforgump 949095101 1000
                endwhile                                
                gumpresponse 3
                waitforgump 949095101 1000
                target IDitem
                getlabel backpack ping
                pause 300
                gumpclose 949095101
            else
                @ignore IDitem
            endif
            if not findtype 20496 backpack
                break
            endif
        endwhile
        if gumpexists 949095101
            gumpclose 949095101
        endif
    endif
    if targetexists 
        hotkey "Cancel Current Target"
    endif
endif

if not timerexists timesenseswitch
    if gumpexists 3352257441
        gumpclose 3352257441
        createtimer timesenseswitch
        overhead "--Time Sense Enabled--" 88
    endif
endif
if timerexists timesenseswitch
    if gumpexists 3352257441
        gumpclose 3352257441
        removetimer timesenseswitch
        overhead "--Time Sense Disabled--" 38
    endif
endif
if timerexists timesenseswitch and not targetexists and not casting
    if not timerexists timesensecontainer
        createtimer timesensecontainer
        settimer timesensecontainer 1000
    endif
    if timer timesensecontainer >= 1000
        for 18
            if index > 0 and findtype "wooden shelf|dead|chest of drawers|3712|3651|3649|3648|3650|3708|2475|2713|2637|2474|3647|3710|3645|3703|3702|2472|3701|2473|2639|3788|3791|3786|2711|3709|3789|3646|3705|27284|27287|27289|27286|27288|3793|3787|3794|3790|3792|2643|3644" ground -1 -1 index as dungeonfloorcontainers
                getlabel dungeonfloorcontainers desc
                overhead "*search*" 88 dungeonfloorcontainers
                @ignore dungeonfloorcontainers
            endif
            if findtype "wooden shelf|dead|chest of drawers|3712|3651|3649|3648|3650|3708|2475|2713|2637|2474|3647|3710|3645|3703|3702|2472|3701|2473|2639|3788|3791|3786|2711|3709|3789|3646|3705|27284|27287|27289|27286|27288|3793|3787|3794|3790|3792|2643|3644" ground -1 -1 2
                break
            endif
            if warmode and findtype "barred metal door|metal door" ground -1 -1 2
                break
            endif
        endfor
        @clearignore
        settimer timesensecontainer 0
    endif
    if not timerexists switchandstufftimer
        createtimer switchandstufftimer
        settimer switchandstufftimer 1000
    endif
    if timer switchandstufftimer >= 1000
        for 18
            if index > 0 and findtype 4239|4240  ground -1 -1 index as switchandstuff
                getlabel switchandstuff desc
                overhead "*switch*" 78 switchandstuff
                @ignore switchandstuff
            endif
        endfor
        @clearignore
        settimer switchandstufftimer 0
    endif
    @clearignore 
    while findtype "wooden shelf|dead|chest of drawers|3712|3651|3649|3648|3650|3708|2475|2713|2637|2474|3647|3710|3645|3703|3702|2472|3701|2473|2639|3788|3791|3786|2711|3709|3789|3646|3705|27284|27287|27289|27286|27288|3793|3787|3794|3790|3792|2643|3644" ground -1 -1 2 as timecontainer
        @setvar "baptimecontainer" timecontainer
        while queued
            //donothing
        endwhile
        getlabel timecontainer desc
        if "the remains of" in desc
            overhead "*avatar corpse*" 45 timecontainer
        else
            dclick timecontainer
            pause 500
            getlabel backpack ping
            if gumpexists 736038070 and findtype 5373 backpack
                gumpresponse 4
            endif
            @ignore timecontainer
            while findtype "wooden shelf|dead|chest of drawers|3712|3651|3649|3648|3650|3708|2475|2713|2637|2474|3647|3710|3645|3703|3702|2472|3701|2473|2639|3788|3791|3786|2711|3709|3789|3646|3705|27284|27287|27289|27286|27288|3793|3787|3794|3790|3792|2643|3644" timecontainer as innercontainer
                while queued
                    //donothing
                endwhile
                dclick innercontainer
                pause 500
                getlabel backpack ping
                @ignore innercontainer
            endwhile
            @unignore timecontainer
        endif
        while findtype 5056|5059|5060|5061|5063|5070|5074|5075|5076|5078|5085|5089|5090|5101|5103|5105|5106|5129|5131|5132|5135|5138|5139|5142|5143|5144|5146|5201|5203|5204|5205|5207|7169|7170|7173|7175|7177|7179|7181|7610|7947|31003|31004|31005|31006|31007|31008|31009|31010|31011|31012|31015|31191|31017|31019|31021|31023|31025|31027|31029|31031|31033|31035|31037|31038|31041|31043|31045|31047|31049|31051|31053|31055|3834|3740|3742|3762|3763|10245|20006|20008|20010|20012|20014|20016|7026|7027|7029|7031|7033|7034|7035|7107|7109|31002|31130|3920|5042|5117|30990|30993|30994|30995|31184|31186|3719|3922|3938|5121|5123|5125|30989|30992|30996|30997|30998|31176|31188|3568|3713|3721|3932|5040|5044|5112|5127|5177|5179|5181|30991|30999|31000|31001|31014|31178|31180|31182|3909|3911|3913|3915|3917|3934|3937|5046|5049|5115|5119|5182|5185|5187|30988|31128|31190|22187|31141|31142|31169|31172 timecontainer as checkunid
            getlabel checkunid desc
            if "unidentified" in desc
                hotkey 'Grab Item' 
                target checkunid
                overhead "*looting*" 2091 timecontainer
                while queued
                    //donothing
                endwhile
            else 
                @ignore checkunid
            endif
            if insysmsg "That container cannot hold more weight."
                break
            endif
            if not find "baptimecontainer" ground -1 -1 2
                break
            endif
        endwhile
        while findtype 2594 timecontainer 0 as zerolantern
            @ignore zerolantern
        endwhile
        while findtype 3827 timecontainer 0 as zeroscroll
            @ignore zeroscroll
        endwhile
        while findtype 4225 timecontainer 0 as zeroleather
            @ignore zeroleather
        endwhile
        while findtype 7127 timecontainer 0 as zeroboard
            @ignore zeroboard
        endwhile
        while findtype 5901|5899|5903|5905 timecontainer as timefootwear
            getlabel timefootwear desc
            if "(hue" in desc
                break
            else 
                @ignore timefootwear
            endif
        endwhile
        while findtype "54230|Pig Iron|bottle set|entrail bucket|mounted stalaghorn|sea glass bulb|juggling daggers|st patricks cross|divination orb|mirror display|murder of crows|charmed cobra|prodigy fiddle|wilting rose belljar|scale of balance|screaming mandrake|statue of the siren|the final hour|tyromancer's cache|tyromancer's cheese|specimen jars|shattered weapon|bird cage|widows grief|54684|27612|49223|54591|54601|54718|7408|19948|58325|19959|19822|49730|49729|19944|19949|47613|47612|47611|47610|47611|4112|3986|3966|cathedral tapestry|Darkscale tapestry|large painting|landscape painting|portrait painting|figurine|statue|20291|44987|49730|47107|18653|45127|20103|47103|29363|47105|29361|43453|5899|47109|45250|41509|43447|18657|4025|51308|45156|51304|45214|28769|28799|28803|28765|45211|45248|51259|45220|51328|45218|3842|51327|45222|45241|51336|11858|51375|45282|54722|45255|45246|28795|44983|45235|45251|45238|51258|51314|28761|45236|51260|54718|18400|58325|45252|18656|47111|3839|45239|45242|45254|45281|45247|42241|45216|28775|2594|51321|3838|51318|5901|5905|45259|5903|48407|51302|4248|27611|15296|43166|5359|5981|4026|3985|25359|17686|3836|17087|5356|29030|24434|22336|29036|3843|29025|51098|29034|8826|51094|576|3827|22326|45315|3891|2539|39898|39896|39892|39918|39911|39916|39897|39905|39891|39917|39912|39909|39889|31047|31017|31049|31019|31055|31051|31053|31025|31027|31031|31041|54717|31021|31011|31023|31029|31043|31003|31035|31033|31045|31037|31002|20006|31169|31006|30999|31012|31009|30996|7109|7107|31128|31188|20014|31014|31004|31142|31001|31000|37181|31010|30998|31172|30988|30997|31038|31141|7947|20008|31005|31191|31008|31007|30994|30993|31182|30989|20012|31186|31178|31015|30991|20010|20016|31184|31190|30990|30995|31176|31180|30992|31130|5207|7031|7026|7034|7033|7027|7035|7029|5078|5063|5059|5105|5060|5138|5129|5201|5142|5143|5076|5106|7610|5139|5090|5103|5132|7177|5061|7181|7179|3920|5042|5127|5117|5185|5125|3938|5177|3937|5044|5121|3915|3909|3568|3932|5123|5144|5119|5056|5135|5187|5074|5089|3934|3913|3917|3721|5075|5046|5049|7170|5146|5205|5203|5115|5204|5040|5179|5070|5182|5085|5181|3719|7173|5131|3911|5101|7175|7169|3713|5112|22187|9917|3859|3862|3878|3865|3856|3873|3877|3834|3742|3762|3740|3763|10245|3572|3573|3571|3885|7127|4225|12686|19985|19981|19984|19982|19983|19991|19989|19986|19987|19988|19994|19995|19992|19993|19990|19980|19977|19976|19978|7154|19979|43423|3861|3821" timecontainer as bitem
            hotkey 'Grab Item' 
            target bitem
            overhead "*looting*" 2091 timecontainer
            while queued
                //donothing
            endwhile
            if insysmsg "That container cannot hold more weight."
                break
            endif
            if not find "baptimecontainer" ground -1 -1 2
                break
            endif
        endwhile
        @ignore timecontainer
    endwhile
endif
loop