# This is an automated backup - check out https://outlands.uorazorscripts.com/script/e2e579e4-4728-43db-80da-00fdbe3df50a for latest
# Automation by Jaseowns
## Script: Hops & Barley Farmer
## Created by: oakman8490#0
#############################################

## "Oaks [KoU]" Script for Farming Hops & Barley
## Any issues with the script, please message me on Discord: Oakman8490

# Script is written specifically for Hops & Barley to make Cleansing Brews, but other seed types can be manually added.

# Items used in Harvest Process:
    ## 100 skill in Taste ID
    ## Watering Can (25570) - 1
    ## Water Bucket (4090) - as many as you need. I have 5, but larger gardens will need more.
    ## Defoliant (6187) - Keep plenty in inventory
    ## Fungicide (6193) - Keep plenty in inventory
    ## Farmers Almanac (29343) - Keep plenty in inventory
    ## Dirt (2323) - Keep plenty in inventory
    ## Fertilizer (4154.2897) - Keep plenty in inventory
    ## Spade (29868) - A couple should be fine
    ## Hops Seed (51078.2208) - Enough to re-plant what you harvest
    ## Barley Seed (51078.2418) - Enough to re-plant what you harvest
    
    
# Work Order
    ## Harvests any crops that are ready, and then re-plants with the same seed.
    ## Determines the ideal levels of heat, water, and dirt
    ## Refills water to the ideal
    ## Refills dirt to the ideal
    ## Applies Labor & Toil buff
    ## Applies Fertilizer (if needed)
    ## Removes debuffs (Fungal, Creature, Pest)
    ## Moves on to the next planter
    ## Tells you to move when you have tended every planter within 2 tiles
   

clearignore
while not dead
    while findtype 4550|4551|6327|6328|6330|6341|56413|56415|56408|56410|28987|6333|6334|6335|6337|6338 ground -1 -1 2
        if findtype 4550|4551|6327|6328|6330|6341|56413|56415|56408|56410|28987|6333|6334|6335|6337|6338 ground -1 -1 2 as myPot
            getlabel myPot myPotLabel
            overhead '{{myPotLabel}}' 55 myPot
            if 'harvest' in myPotLabel
                if 'barley' in myPotLabel
                    @setvar! myCrop 1
                elseif 'hops' in myPotLabel
                    @setvar! myCrop 2
//Additional plant descriptions can be added here with an elseif                
                else 
                    @setvar! myCrop 0
                endif
                if myCrop = 1
                    if findtype 51078 backpack 2418 as seeds
                        @setvar! mySeeds seeds
                    else 
                        overhead 'No Barley Seeds' 33
                    endif
                elseif myCrop = 2
                    if findtype 51078 backpack 2208 as seeds
                        @setvar mySeeds seeds
                    else 
                        overhead 'No Hops Seeds' 33
                    endif
//Additional seed types can be added here with an elseif                    
                else 
                    @setvar! mySeeds 0
                endif
                dclick myPot
                overhead 'Harvesting'
                gumpresponse 2 3224608375
                wait 450
                gumpresponse 2 3224608375
                wait 450    
                overhead 'Planting'
                while not targetexists 
                    gumpresponse 2 3224608375
                    wait 450    
                endwhile
                if mySeeds = 0
                    overhead 'Manual Seeding Required' 33
                    hotkey 'Cancel Current Target'
                    @ignore! myPot
                else 
                    target mySeeds
                    wait 1000
                endif
            endif
            if 'grown' in myPotLabel
                dclick myPot
                overhead 'Setting Ideals'
                waitforgump 3224608375
                gumpresponse 3 3224608375
                waitforgump 3224608375
                gumpresponse 4 3224608375
                waitforgump 3224608375
                gumpresponse 5 3224608375
                wait 200
                if findtype 56416|56417|25570 backpack as myWater
                    overhead 'Watering'
                    dclick myWater
                    wait 200
                    gumpresponse 2 3224608375
                    waitfortarget 5000
                    target myWater
                    waitforgump 3929333591
                    gumpresponse 2 3929333591
                    wait 200
                    gumpclose 3929333591
                else      
                    overhead 'No Water' 33
                endif
                if findtype 2323 backpack as myDirt
                    overhead 'Adding Dirt'
                    gumpresponse 2 3224608375
                    waitfortarget 5000
                    target myDirt
                    waitforgump 4024474571
                    gumpresponse 2 4024474571
                    wait 200
                    gumpclose 4024474571
                else 
                    overhead 'No Dirt' 33
                endif
                if findtype 29868 backpack as mySpade
                    overhead 'Tilling'
                    gumpresponse 2 3224608375
                    waitfortarget 5000
                    target mySpade
                    waitforgump 1429840072
                    gumpresponse 6 1429840072
                    wait 200
                    gumpclose 1429840072
                else 
                    overhead 'No Spade' 33
                endif
                if findtype 4154|19403|29217 backpack as myFertilizer
                    overhead 'Fertilizing'
                    gumpresponse 2 3224608375
                    waitfortarget 5000
                    target myFertilizer
                    wait 200
                else 
                    overhead 'No Fertilizer'
                endif
                wait 200
                clearsysmsg
                for 100
                    gumpresponse 10 3224608375
                    wait 100
                    if insysmsg 'Reduces'
                        gumpresponse 10 3224608375
                        wait 100                
                        if insysmsg 'Fungal Effect'
                            if findtype 6193 backpack as myFungicide
                                gumpresponse 2 3224608375
                                waitfortarget 5000
                                target myFungicide
                            else 
                                overhead 'No Fungicide' 33
                            endif
                        elseif insysmsg 'Creature Effect'
                            if findtype 29343 backpack as myAlmanac
                                gumpresponse 2 3224608375
                                waitfortarget 5000
                                target myAlmanac
                            else 
                                overhead 'No Almanac' 33
                            endif                    
                        elseif insysmsg 'Pestilent Effect'
                            if findtype 6187 backpack as myDefoliant
                                gumpresponse 2 3224608375
                                waitfortarget 5000
                                target myDefoliant
                            else 
                                overhead 'No Defoliant' 33
                            endif
                        else 
                            overhead 'Debuff error?'
                        endif
                    elseif insysmsg 'Increases'
                        break
                    endif
                    wait 200
                endfor
            else 
                overhead 'Manual Seeding Required' 33
            endif
            @ignore! myPot
            gumpclose
        endif
    endwhile
    overhead 'Time to Move' 33
    wait 5000
endwhile