# This is an automated backup - check out https://outlands.uorazorscripts.com/script/1f5cda21-7f62-4c44-82c6-c575df2a5869 for latest
# Automation by Jaseowns
## Script: Organizer v0.1
## Created by: michael_8911#0
#############################################

# Organizer v0.1

# Organizes a target container by searching it to a depth of `layersToSearch` and sorting them
# items into specifiec containers based on category

# Michael Leide 10/18/25 v0.1

setvar debug 1
setvar 'overheadProgress' 1
setvar verbose 1

setvar dirtyBag
setvar layersToSearch 2

# enter your storage container serial numbers here:

setvar gemStorage 0x56096157
setvar progressStorage 0x4A083823
setvar 'skillScrollStorage' 0x5BF163D6
setvar regStorage 0x42751C68
setvar valuableStorage 0x5BF163D5
setvar craftStorage 0x56096153
setvar mapStorage 0x5BF163D9
setvar goldStorage 0x428A9D00

setvar coreStorage 0x5BF163D3
setvar distillStorage 0x5BF163D0
setvar extractStorage 0x47988855
setvar phylStorage 0x47988855

setvar storageShelf 0x59844F4A

# these are the type ids for the items we are going to sort

setvar gold 3821
setvar citrine 3861
setvar tourmaline 3885
setvar amber 3877
setvar amethyst 3862
setvar ruby 3859
setvar sapphire 3865
setvar emerald 3856
setvar star 3873
setvar diamond 3878

setvar redlightningBottle 25359
setvar skillBall 22336
setvar skillScroll 8826

setvar blackPearl 3962
setvar bloodmoss 3963
setvar garlic 3972
setvar ginseng 3973
setvar mandrakeRoot 3974
setvar nightshade 3976
setvar spidersSilk 3981
setvar sulfurousAsh 3980

setvar researchMaterials 24434
setvar survivalGear 37181

setvar mastercraftingDiagram 17087
setvar arcaneScroll 3827

setvar chromaticCore 4025
setvar chromaticDistillation 4026
setvar aspectCore 3985
setvar aspectDistillation 17686
setvar aspectExtract 3836
setvar aspectPhylactery 576

setvar map 5356

setvar variablesSet 1


# these are the lists that will hold the various items, by category (by storage container)

if 'overheadProgress' = 1
    overhead 'building lists'
endif

if listexists 'gemList'
    clearlist 'gemList'
else
    createlist 'gemList'
endif

if listexists 'progressList'
    clearlist 'progressList'
else
    createlist 'progressList'
endif

if listexists 'regList'
    clearlist 'regList'
else
    createlist 'regList'
endif

if listexists 'valuableList'
    clearlist 'valuableList'
else
    createlist 'valuableList'
endif

if listexists 'craftList'
    clearlist 'craftList'
else
    createlist 'craftList'
endif

# populating the lists...

if 'overheadProgress' = 1
    overhead 'populating lists'
endif

pushlist gemList citrine
pushlist gemList tourmaline
pushlist gemList amber
pushlist gemList amethyst
pushlist gemList ruby
pushlist gemList sapphire
pushlist gemList emerald
pushlist gemList star
pushlist gemList diamond

pushlist progressList redlightningBottle
pushlist progressList skillBall

pushlist regList blackPearl
pushlist regList bloodmoss
pushlist regList garlic
pushlist regList ginseng
pushlist regList mandrakeRoot
pushlist regList nightshade
pushlist regList spidersSilk

pushlist regList sulfurousAsh
pushlist valuableList researchMaterials
pushlist valuableList survivalGear

pushlist craftList mastercraftingDiagram
pushlist craftList arcaneScroll

# now we will sort the items

if 'overheadProgress' = 1
    overhead 'sorting items'
endif


# sort gold

if 'overheadProgress' = 1 and verbose = 1
    overhead 'sorting gold'
endif

while findtype gold dirtyBag -1 -1 layersToSearch as goldItem
    
    if debug = 1
        overhead goldItem
    endif
    
    lift goldItem 60000
    wait 500
    drop goldStorage -1 -1 -1
endwhile


# sort gems

if 'overheadProgress' = 1 and verbose = 1
    overhead 'sorting gems'
endif
    
foreach gem in gemList
    if debug = 1
        overhead gem
    endif

    while findtype gem dirtyBag -1 -1 layersToSearch as gemItem
        lift gemItem 60000
        wait 500
        drop gemStorage -1 -1 -1
    endwhile
endfor


# sort skill scrolls

if 'overheadProgress' = 1 and verbose = 1
    overhead 'sorting skill scrolls'
endif

while findtype 'skillScroll' dirtyBag -1 -1 layersToSearch as scrollItem
    lift scrollItem 60000
    wait 500
    drop 'skillScrollStorage' -1 -1 -1
endwhile


# sort progress items

if 'overheadProgress' = 1 and verbose = 1
    overhead 'sorting progress items'
endif
    
foreach progress in progressList
    if debug = 1
        overhead progress
    endif

    while findtype progress dirtyBag -1 -1 layersToSearch as progressItem
        lift progressItem 60000
        wait 500
        drop progressStorage -1 -1 -1
    endwhile
endfor


# sort regs

if 'overheadProgress' = 1 and verbose = 1
    overhead 'sorting regs'
endif
    
foreach reg in regList
    if debug = 1
        overhead reg
    endif

    while findtype reg dirtyBag -1 -1 layersToSearch as regItem
        lift regItem 60000
        wait 500
        drop regStorage -1 -1 -1
    endwhile
endfor


# sort valuables

if 'overheadProgress' = 1 and verbose = 1
    overhead 'sorting valuables'
endif
    
foreach valuable in valuableList
    if debug = 1
        overhead valuable
    endif

    while findtype valuable dirtyBag -1 -1 layersToSearch as valuableItem
        lift valuableItem 60000
        wait 500
        drop valuableStorage -1 -1 -1
    endwhile
endfor


# sort crafting items

if 'overheadProgress' = 1 and verbose = 1
    overhead 'sorting crafting items'
endif
    
foreach craft in craftList
    if debug = 1
        overhead craft
    endif

    while findtype craft dirtyBag -1 -1 layersToSearch as craftItem
        lift craftItem 60000
        wait 500
        drop craftStorage -1 -1 -1
    endwhile
endfor


# sort cores

if 'overheadProgress' = 1 and verbose = 1
    overhead 'sorting aspect cores'
endif

while findtype aspectCore dirtyBag -1 -1 layersToSearch as coreItem
    lift coreItem 60000
    wait 500
    drop coreStorage -1 -1 -1
endwhile

while findtype chromaticCore dirtyBag -1 -1 layersToSearch as chromaCoreItem
    lift chromaCoreItem 60000
    wait 500
    drop coreStorage -1 -1 -1
endwhile


# sort distillations

if 'overheadProgress' = 1 and verbose = 1
    overhead 'sorting aspect distillations'
endif

while findtype aspectDistillation dirtyBag -1 -1 layersToSearch as distilItem
    lift distilItem 60000
    wait 500
    drop distillStorage -1 -1 -1
endwhile

while findtype chromaticDistillation dirtyBag -1 -1 layersToSearch as chromaDistilItem
    lift chromaDistilItem 60000
    wait 500
    drop distillStorage -1 -1 -1
endwhile


# sort phyls

if 'overheadProgress' = 1 and verbose = 1
    overhead 'sorting aspect phylacteries'
endif

while findtype aspectPhylactery dirtyBag -1 -1 layersToSearch as phylItem
    lift phylItem 60000
    wait 500
    drop phylStorage -1 -1 -1
endwhile

if 'overheadProgress' = 1
    overhead 'complete'
endif

