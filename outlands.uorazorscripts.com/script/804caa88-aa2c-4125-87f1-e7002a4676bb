# This is an automated backup - check out https://outlands.uorazorscripts.com/script/804caa88-aa2c-4125-87f1-e7002a4676bb for latest
# Automation by Jaseowns
## Script: Magery One Button Hotkey
## Created by: jolanar#0
#############################################

#Wizard Grimoire
@setvar! castArrow 1
@setvar! castHarm 1
@setvar! castFireball 1
@setvar! castLightning 1

#Potions
@setvar! useCurePotion 1
@setvar! useStrengthPotion 1
@setvar! useDexterityPotion 1
@setvar! useResistPotion 1
@setvar! useStaminaPotion 1
@setvar! Panacea 1
@setvar! Freedom 1
@setvar! useExplosion 0

#Misc
@setvar! castLeyline 1
@setvar! cdLeyline 5000
@setvar! useTaunt 0
@setvar! GoldDrop 0


#Curing
if useCurePotion = 1 and poisoned
    if findtype "Orange Potion" backpack as pot
        dclick pot
    endif
endif

#Healing
if diffhits > 35
    if findtype "Yellow Potion" backpack as pot
        getlabel pot potlabel
        if 'next usable' in potlabel
            hotkey 'Play Script: Smarter Heal'
        else
        dclick pot
        endif
    else
        hotkey 'Play Script: Smarter Heal'
    endif
endif

#Stamina and Alchemy Codex
if diffstam > 8
    if findtype "Red Potion" backpack as pot
        dclick pot
    endif
endif

if Panacea = 1 and findbuff 'Disease' and not cooldown Panacea
    if findtype "Orange Potion" backpack as pot
        dclick pot
    endif
endif

if Panacea = 1 and findbuff 'Bleed' and not cooldown Panacea
    if findtype "Orange Potion" backpack as pot
        dclick pot
    endif
endif

if Freedom = 1 and findbuff 'Hinder' and not cooldown Freedom
    if findtype "Red Potion" backpack as pot
        dclick pot
        overhead 'Hinder Removed'
    endif
endif

if Freedom = 1 and findbuff 'Entangle' and not cooldown Freedom
    if findtype "Red Potion" backpack as pot
        dclick pot
        overhead 'Entangle Removed'
    endif
endif

#Buffing
if not findbuff "Reactive Armor" and mana >= 5
    cast "Reactive Armor"
endif

if useStrengthPotion = 1 and not findbuff 'Strength'
    if findtype "White Potion" backpack as pot
        dclick pot
    endif
endif

if useDexterityPotion = 1 and not findbuff 'Agility'
    if findtype "Blue Potion" backpack as pot
        dclick pot
    endif
endif

if useResistPotion = 1 and not findbuff 'Magic Resist Potion'
    if findtype "Black Potion" backpack as pot
        dclick pot
    endif
endif

if not findbuff 'Food Satisfaction'
    if findtype tray backpack as food
        dclick food
    endif
endif

#Taunt
#You will need a 15 second ingame cooldown called Taunt for this to work.
if useTaunt = 1 and not cooldown Taunt and find lasttarget ground -1 -1 12
    say '[taunt'
    wft
    lasttarget
endif

#Weapon Ability
#You will need an ingame cooldown called Leyline for this to work.
if diffmana > 30 and not cooldown Leyline and castLeyline = 1
    say '[weaponability2'
    cooldown Leyline cdLeyline
endif

#Codex Hotbar
if not gumpexists 2016719803 and skill 'Parrying' >= 80
    dclick 0x4199F522
    waitforgump 3835665787
    gumpresponse 2
    waitforgump 2016719803
    gumpclose 3835665787
endif

#Explosion Potion:
#If you have a target witin 12 tiles, this will try to check LOS with arms lore and then use an explosion potion on last target.
#You will need an ingame cooldown called LOS for this to work.
#WARNING: This does check for line of sight and range, but explosion potions run the risk of exploding on blue characters and may turn you grey.
if find lasttarget ground -1 -1 12 and skill 'Alchemy' >= 80 and not cooldown 'ExplosionPotion' and findtype '3853' > 0
    hotkey 'Arms Lore'
    wft
    lasttarget
    getlabel backpack PingCheck
    if cooldown LOS and findtype "Purple Potion" backpack as pot
        dclick pot
        wft
        lasttarget 
        cooldown ExplosionPotion 12200
    endif
endif

#Gold Drop - I have OCD so sometimes its hard not to loot gold. This will drop it for me.
if findtype "gold coin" backpack as item and GoldDrop = 1
    lift item 2000
        droprelloc 0 0
endif

#Move looted reagents to satchel
if findtype 3963|3980|3974|3973|3981|3962|3976|3972 backpack
    if findtype "reagent satchel" self as 'WizardSatchel'
        menu 'WizardSatchel' 0
    endif
endif

#Damage spells begin here:
if mana >= 60
    cast 'Flamestrike'
    stop
endif

if castArrow = 1 and mana >= 5 and not findbuff 'Magic Arrow Cooldown'
    cast 'magic arrow'
    stop
endif

if castHarm = 1 and mana >= 9 and not findbuff 'Harm Cooldown'
    cast 'harm'
    stop
endif
           
if castFireball = 1 and mana >= 12 and not findbuff 'Fireball Cooldown'
    cast 'fireball'
    stop
endif

if castLightning = 1 and mana >= 12 and not findbuff 'Lightning Cooldown'
    cast 'Lightning'
    stop                   
endif

if mana < 40 and not findbuff 'Magic Mushroom Cooldown'
    hotkey 'Eat Magic Mushroom'
endif

if mana >= 40
    cast 'Flamestrike'
    stop
endif

cast 'Mind Blast'