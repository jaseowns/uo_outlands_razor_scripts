# Training Cartography from Horseshoe Bay by Jaseowns
# UO Outlands = https://youtu.be/k5oVW2HwJi8
# Requirements:  Tons of blank maps, 40+ map maker pens
#   restock agent 5   as      10 maps and 2 map maker pens
#   sell agent        as      map and enabled
#   Create the following counters under Display/Counters
//  # name: blankmap format: blankmap item id: 5356 color: 2955
//  # name: usedmap  format: usedmap  item id: 5356 color: 2669
# Additional Info: 
#   At the time of writing this script, the map maker is serial 0xAF5DF for Brad
#   if Brad is no longer there, use >info on the map maker and update line 22
#   Put your max skill on line 13 if you want to go beyond 100 skill.
if skill 'cartography' >= 100
    overhead 'Hit max!' 88
    stop
endif

if not findtype 400
    overhead 'Stand in Horseshoe Bae near bank and map maker vendor'
elseif count 'usedmap' > 0
    overhead 'Selling to Brad the mapmaker'
    menu 0xAF5DF 2
    wait 2000
endif

gumpclose 
if count 'blankmap' < 10 or not findtype "pen and ink" backpack
    restock 5
    waitfortarget 
    hotkey 'Target Self'
    wait 2000
endif

if not findtype "pen and ink" backpack
    overhead "No map maker pen found!" 34
    wait 1000
    replay
endif

if count 'blankmap' = 0
    overhead "No blank maps" 34
    wait 1000
    replay
endif

if skill 'cartography' < 50
    overhead 'Train at vendor'
    wait 1000
    replay
endif

while skill 'cartography' < 75 and count 'blankmap' > 0 and findtype 'pen and ink' backpack
    gumpclose 
    overhead '50-75 City Map'
    dclicktype 'pen and ink' backpack
    waitforgump 949095101 
    gumpresponse 100
    waitforgump 949095101 
    gumpresponse 201
    waitforgump 949095101
    wait 3500
endwhile

while skill 'cartography' < 100 and count 'blankmap' > 0 and findtype 'pen and ink' backpack
    gumpclose 
    overhead '75-100 Regional Map'
    dclicktype 'pen and ink' backpack
    waitforgump 949095101 
    gumpresponse 100
    waitforgump 949095101 
    gumpresponse 202
    waitforgump 949095101
    wait 3500
endwhile

while skill 'cartography' < 120 and count 'blankmap' > 0 and findtype 'pen and ink' backpack
    gumpclose 
    overhead '100-120 World Map'
    dclicktype 'pen and ink' backpack
    waitforgump 949095101 
    gumpresponse 100
    waitforgump 949095101 
    gumpresponse 203
    waitforgump 949095101
    wait 3500
endwhile

loop