# This is an automated backup - check out https://outlands.uorazorscripts.com/script/dfa6fe0b-342b-4450-bf49-ef6e280835b5 for latest
# Automation by Jaseowns
## Script: Enter Rental Room
## Created by: Aphlux#6321
#############################################

# Enter Inn Room Script for UO Outlands
# 
# This script will quickly enter your rental inn room
# as either the room owner or co-owner.
#
# You must be in range of an innkeeper or house steward.
# 
# Written by:       Aphlux (aka Aphra)
# Last updated:     August 24, 2022
hotkey 'Cancel Current Target'
clearsysmsg 

@setvar rentalGumpID 2393832411

if gumpexists rentalGumpID
    gumpclose
    wait 100
endif

say 'room'
waitforgump rentalGumpID 3000

if not gumpexists rentalGumpID
    overhead 'Failed to enter room!' 38
    overhead 'Are you even near the inn?' 53
elseif ingump 'Enter Your Room'
    wait 250

    while not insysmsg 'You enter the rental room'
        overhead 'Entering room...' 903
        gumpresponse 4
        wait 250
        
        if insysmsg 'That vendor is too far away'
            overhead 'Innkeeper is too far away!' 38
            overhead 'Move closer!' 53
            stop
        endif
    endwhile
    
    overhead 'Welcome home.'
    
elseif ingump 'Rent This Room'
    gumpresponse 5
    waitforgump rentalGumpID 3000
    
    if ingump 'co-owner'
        while not insysmsg 'You enter the rental room'
            wait 250
            overhead 'Entering co-owned room...' 903
            gumpresponse 100
            
            if insysmsg 'That vendor is too far away'
                overhead 'Innkeeper is too far away!' 38
                stop
            endif
        endwhile
        
        overhead 'Welcome home.'
    else
        overhead 'No access to any rooms!' 38
    endif
elseif ingump 'Visit Other Rooms'
    gumpresponse 2
    waitforgump rentalGumpID 3000
    
    while not insysmsg 'You enter the rental room'
        wait 250
        overhead 'Entering co-owned room...' 903
        gumpresponse 100
    endwhile
else 
    sysmsg 'Character does not have a room or access to one!' 38
    overhead 'No room access!' 38
endif