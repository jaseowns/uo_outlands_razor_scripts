# This is an automated backup - check out https://outlands.uorazorscripts.com/script/2d2e974e-e0b8-44f3-bb2c-1971862c2370 for latest
# Automation by Jaseowns
## Script: Bapeth's Trapper BOOM BOOM
## Created by: barryroser#0
#############################################

# BOOM BOOM by Bapeth
#
# Update : Dec 28th 2025  Added seperate activator for weapon ability / auto attack in "Bapeths Trapper Background"
#
# This is a one button Trap placer, Remover, and Detonator
# 
# How it works:
#
# "Peacemode"
# The script will place a trap
# If you have already placed a trap the script will denonate the trap
# If you are out of range to detonate a placed trap the script will place a new trap (8 tiles)
#
# "Warmode"
# If you want to remove a trap, go into warmode near the trap (within the purple square) and play the script 
# If you are not close enough to remove the trap (within the purple square) the script will place a new trap
#
# Notes:
# When fighting a Cav 4 "a cave hunter" mob it will place traps on the ground that are identical to yours, this can cause a minor conflict with the script 
# If you have a ground trap placed and the cave hunter also has a ground trap placed within 2 tiles of yours, and you want to remove the trap (warmode), the removal feature will not work
#
# This script can tie-in to "Bapeths Trapper Background" script 
# If you would like to run this it can be found here : https://outlands.uorazorscripts.com/script/ac637102-a701-4680-a780-faf3efb5c274
# Ensure that the script is named exactly as seen above (no apostraphe or quotations)
#
# Script starts here...
#
#########################################################################################################################################
#
# "SCRIPT SETTINGS"
#
# Set this if you are playing a dexxer trapper with parry and are using Bapeths Trapper Background Script
# This setting will rapidly change you into Bullwark when you have been stationary for 3 or more seconds
# "Warning!" this consumes a lot of mana and was originally intended for Arcane Trapper
# Do not use this if you already have Bullwark set to Default in Bapeths Trapper Background Script
#
# AUTO BULLWARK
@setvar! "BullwarkActivator" 0
#
#########################################################################################################################################

if "BullwarkActivator" = 1
    if not timerexists baptrapbullwark
        createtimer baptrapbullwark
    elseif timerexists baptrapbullwark
        settimer baptrapbullwark 0
    endif
endif

if not timerexists baptrapcombat
    createtimer baptrapcombat
elseif timerexists baptrapcombat
    settimer baptrapcombat 0
endif

if warmode and findtype "metal trap" ground -1 -1 2 as removetrap
    menu removetrap 0
    warmode off
    hotkey "Attack Last Combatant"
elseif warmode and findtype "metal trap" ground -1 -1
    if findtype "metal trap" backpack
        say "[PlaceTrap"
        warmode off
    else 
        overhead "Out of Traps!" 38
    endif
endif

if not findtype 51163 ground
    getlabel backpack ping
endif

if not warmode and findtype "metal trap" ground -1 -1 8 and findtype "music box" backpack as boom
    clearsysmsg
    say "[DetonateTrap"
    getlabel backpack ping
    if insysmsg "You detonate a ground trap"
        overhead "BOOM!" 88
    elseif insysmsg "You must wait another"
        // do nothing
    elseif insysmsg "You are too far away"
        overhead "Gotta get closer..." 45
    elseif insysmsg "You do not have an active floor trap."
        if findtype "metal trap" backpack
            say "[PlaceTrap"
        else 
            overhead "Out of Traps!" 38
        endif
    endif
elseif not warmode
    if findtype "metal trap" backpack
        say "[PlaceTrap"
    else 
        overhead "Out of Traps!" 38
    endif
endif

if skill "Tinkering" >= 80
    script "Bapeths Trapper Background"
endif