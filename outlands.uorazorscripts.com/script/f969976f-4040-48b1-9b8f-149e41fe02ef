# This is an automated backup - check out https://outlands.uorazorscripts.com/script/f969976f-4040-48b1-9b8f-149e41fe02ef for latest
# Automation by Jaseowns
## Script: Tinkering jewels training beads seller
## Created by: special_sy#0
#############################################

@clearignore 
# Script by [DoS] Chumber
# This script is a semi automatic helper if you train tinkering
# with jewels. 
# 
# It stops the queue if full and resumes after unload
# It will work with any jeweler, just be in range (10 tiles)
# It can buy tools if you setup a buy agent for them
# Just change buyToolkits to 1
#
# !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
# !! REQUIREMENTS:                                                    !!
# !! Add the beads to your sell agent and enable it                   !!
# !! Add 5 toolkits to your buy agent and enable it                   !!
# !! Setup your crafting queue with beads (craft until skill reached) !!
# !! This Script uses Organizer 10 to move gold, usually your         !!
# !! boombox or your bank where possible (Outpost)                    !!
# !! Setup Restock Agent 10 to grab ingots and jewels                 !!
# !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

@setvar buyToolkits 1
@setvar restockAgentNo 10
@setvar organizerAgentNo 10
@setvar skillCap 121

# !!! DONT EDIT !!
@setvar jewelerNPC 0
if not timerexists 'sellBeads'
    createtimer 'sellBeads'
    clearsysmsg 
endif

if skill 'tinkering' >= skillCap
    overhead "All done, congrats!" 88
    stop
endif

# Find a jeweler
while findtype 400|401 'ground' -1 -8 as npc
    getlabel npc npcLabel
    if 'jeweler' in npcLabel
        @setvar jewelerNPC npc
    endif
    @ignore npc
endwhile

# Didnt find a jeweler?
if jewelerNPC = 0
    overhead "No jeweler in range!"
    hotkey 'Crafting Queue'
    overhead "I cant work like that!" 32
    wait 250
    gumpresponse 7
    stop
endif

# Buy more toolkits from jeweler if down to 1
if counttype 'tool kit' <= 1 and buyToolkits = 1
    overhead "Last toolkit, buying more!"
    menu jewelerNPC 1
    wait 500
endif

# Restock
if counttype 'iron ingot%s' 'backpack' <= 3 and restockAgent > 0
    overhead "Restocking..."
    restock restockAgentNo
    wft 500
    target 'self'
    while queued
        wait 500
    endwhile
endif

## Sell on 10 beads every 30s or if almost full
if counttype 4233 > 10 and timer 'sellBeads' > 30000 or diffweight  < 10
    getlabel jewelerNPC npcLabel
    overhead '$$$ {{npcLabel}}'
    menu jewelerNPC 2
    wait 500
    organizer organizerAgentNo
    settimer 'sellBeads' 0
endif

## Full
if diffweight < 1
    overhead "This stuff is heavy!"
    hotkey 'Crafting Queue'
    wait 250
    gumpresponse 7
    # Drop Gold somewhere
    while diffweight < 10
        overhead "Unload gold!"
        wait 5000
    endwhile
    # Get back
    while not find jewelerNPC 'ground' -1 -1 10 
        overhead "Get back to work!"
        wait 5000
    endwhile
    # Resume
    hotkey 'Crafting Queue'
    overhead "We are back in business!"
    wait 250
    gumpresponse 7
endif

# Keep the crafting queue open
if not gumpexists 453554062
    hotkey 'Crafting Queue'
    wait 500
endif

# Start queue
if ingump 'Begin Crafting'
    overhead "Here we go!"
    gumpresponse 7
endif
wait 5000
replay