# This is an automated backup - check out https://outlands.uorazorscripts.com/script/fdc958d0-d88f-42e0-a877-83acf7a53680 for latest
# Automation by Jaseowns
## Script: Training Cartography and Selling Maps
## Created by: Jaseowns#6694
#############################################

# Training Cartography from Horseshoe Bay by Jaseowns
# UO Outlands = https://youtu.be/k5oVW2HwJi8
# Requirements:  Tons of blank maps, 40+ map maker pens
#   restock agent 5   as      10 maps and 2 map maker pens
#   sell agent        as      map and enabled
####
# UPDATED September 17th 2022 to no longer need counters
####
# Additional Info: 
#   At the time of writing this script, the map maker is serial 0xAF5DF for Brad
#   if Brad is no longer there, use >info on the map maker and update line 18
#   Put your max skill on line 13 if you want to go beyond 100 skill.
if skill 'cartography' >= 100
    overhead 'Hit max!' 88
    stop
endif

@setvar! mapMakerVendorId 0xAF5DF

// sell if the maps we have in our backpack are used and not blank
if counttype 5356 backpack 2669 > 0 and counttype 5356 backpack 2955 = 0
    overhead 'Selling to Brad the mapmaker'
    menu mapMakerVendorId 2
    wait 2000
    if counttype 5356 backpack 2669 > 0
        overhead "Make sure your sell agent is active and set to map!" 34
        wait 2000
        replay
    endif
endif

gumpclose 949095101
if counttype 5356 backpack 2955 < 10 or not findtype "pen and ink" backpack
    restock 5
    waitfortarget 
    hotkey 'Target Self'
    wait 2000
endif

if not findtype "pen and ink" backpack
    overhead "No map maker pen found!" 34
    overhead "Make sure restock agent 5 is set as maps & pens" 88
    wait 1000
    replay
endif

if counttype 5356 backpack 2955 = 0
    overhead "No blank maps" 34
    overhead "Make sure restock agent 5 is set as maps & pens" 88
    wait 1000
    replay
endif

if skill 'cartography' < 50
    overhead 'Train at vendor'
    wait 1000
    replay
endif

while skill 'cartography' < 75 and counttype 5356 backpack 2955 > 0 and findtype 'pen and ink' backpack
    gumpclose 949095101
    overhead '50-75 City Map'
    dclicktype 'pen and ink' backpack
    waitforgump 949095101 
    gumpresponse 100 949095101
    waitforgump 949095101 
    gumpresponse 201 949095101
    waitforgump 949095101
    wait 3500
endwhile

while skill 'cartography' < 100 and counttype 5356 backpack 2955 > 0 and findtype 'pen and ink' backpack
    gumpclose 
    overhead '75-100 Regional Map'
    dclicktype 'pen and ink' backpack
    waitforgump 949095101 
    gumpresponse 100 949095101
    waitforgump 949095101 
    gumpresponse 202 949095101
    waitforgump 949095101
    wait 3500
endwhile

while skill 'cartography' < 120 and counttype 5356 backpack 2955 > 0 and findtype 'pen and ink' backpack
    gumpclose 
    overhead '100-120 World Map'
    dclicktype 'pen and ink' backpack
    waitforgump 949095101 
    gumpresponse 100 949095101
    waitforgump 949095101 
    gumpresponse 203 949095101
    waitforgump 949095101
    wait 3500
endwhile

loop