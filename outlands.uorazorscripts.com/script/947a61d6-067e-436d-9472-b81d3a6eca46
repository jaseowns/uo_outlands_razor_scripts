# This is an automated backup - check out https://outlands.uorazorscripts.com/script/947a61d6-067e-436d-9472-b81d3a6eca46 for latest
# Automation by Jaseowns
## Script: Fishing on Boat
## Created by: tarouca#0
#############################################

# IT IS TAILORED TO USE WITH A DEXXER HARPOONER TEMPLATE
# THE GREATER ABILITY IS MARINERs FORTUNE AND ITS TIMER MUST BE ADJUSTED TO YOUR OWN BOAT

if not timerexists "curePotTimer_"
    createtimer "curePotTimer_"
    settimer "curePotTimer_" 99999
endif
if not timerexists "stillPoisonedTimer_"
    createtimer "stillPoisonedTimer_"
    settimer "stillPoisonedTimer_" 99999
endif
if not timerexists "fishingTimer_"
    createtimer "fishingTimer_"
    settimer "fishingTimer_" 99999
endif
if not timerexists "checkForNetsTimer_"
    createtimer "checkForNetsTimer_"
    settimer "checkForNetsTimer_" 99999
endif
if not timerexists "skinningTimer_"
    createtimer "skinningTimer_"
    settimer "skinningTimer_" 99999
endif
if not timerexists "netCastTimer_"
    createtimer "netCastTimer_"
    settimer "netCastTimer_" 99999
endif
if not timerexists "checkMyHatchTimer"
    createtimer "checkMyHatchTimer"
    settimer "checkMyHatchTimer" 99999
endif
if not timerexists "checkSOSLocationTimer"
    createtimer "checkSOSLocationTimer"
    settimer "checkSOSLocationTimer" 99999
endif
if findtype 3702 backpack as fishBag__
    @setvar fishBag_ fishBag__
    overhead "FISH BAG FOUND"
endif
if findtype 3999 backpack as scissors__
    @setvar scissors_ scissors__
    overhead "FOUND SCISSORS"
endif
if findtype 31172|31169 backpack as myWeapon__
    @setvar myWeapon_ myWeapon__
    overhead "FOUND HARPON"
endif

@clearignore
@setvar me_ self
@setvar pvpPvmStatus_ 0
@setvar lagTimer_ 750
@setvar greaterTimerValue_ 205000
@setvar spyglassChoice_ 0
@setvar fishingWithNet_ 0
@setvar minHpAllowed_ 50

@setvar weaponAbility_ 3
# 0 - FISHING SPOTS, 1 - SPECIAL NETS, 2 - SOSs
@setvar justSpecialFishing_ 0

while not dead
    if findtype "spyglass" backpack as spyglass_ and not cooldown "Spyglass" and hp > minHpAllowed_
        while gumpexists 2890020940
            gumpclose 2890020940
            wait lagTimer_
        endwhile        
        hotkey "> Interrupt"
        dclick spyglass_
        waitfortarget
        target self
        while not gumpexists 2890020940
            wait lagTimer_
        endwhile
        if spyglassChoice_ = 0
            gumpresponse 10 2890020940
            while not gumpexists 2890020940
                wait lagTimer_
            endwhile
            gumpresponse 4 2890020940
            if justSpecialFishing_ = 0
                @setvar spyglassChoice_ 1
            endif
        else
            gumpresponse 11 2890020940
            while not gumpexists 2890020940
                wait lagTimer_
            endwhile
            gumpresponse 4 2890020940
            @setvar spyglassChoice_ 0
        endif
        cooldown "Spyglass" 5000
    endif

    if poisoned
        if statusPoisoned_ = 0
            @setvar statusPoisoned_ 1
            settimer "stillPoisonedTimer_" 0
        endif
        if timer "stillPoisonedTimer_" > 5000
            if timer "curePotTimer_" > 5100
                if findtype "Orange Potion" backpack as pot_
                    dclick pot_
                    wait lagTimer_
                    settimer "curePotTimer_" 0
                else
                    overhead "MISSING POTS" 38 
                endif
            else 
                overhead "Wait for it..."
            endif
        endif
    elseif statusPoisoned_ = 1
        @setvar statusPoisoned_ 0
    endif

    if hp < maxhp
        if pvpPvmStatus_ = 0 and not bandaging
            if  findtype "clean bandage%s%" backpack
                hotkey "Bandage Self"
                cooldown "Bandage" 7100
            else 
                overhead "MISSING: BANDAGES" 38
            endif
        elseif pvpPvmStatus_ = 1 and not bandaging and findtype "clean bandage%s%" backpack 
            hotkey "Bandage Self"
            cooldown "Bandage" 10100
        endif    
        if hp < 60
            if pvpPvmStatus_ = 0
                if not cooldown "Health Potion" and findtype "Yellow Potion" backpack as pot_
                    dclick pot_
                    cooldown "Health Potion" healthPotTimer_
                endif
                if hp < minHpAllowed_ and not cooldown "Holy Light" and not cooldown "Chiv Timer" and skill "chivalry" >= 95
                    say "[HolyLight"
                    wait lagTimer_
                    if insysmsg "Holy symbols remaining"
                        cooldown "Holy Light" 30000
                        cooldown "Chiv Timer" 500
                    endif
                endif
            else 
                if hp < 60 and not cooldown "Health Potion" and findtype "Yellow Potion" backpack as pot_
                    dclick pot_
                    cooldown "Health Potion" 11100
                endif
            endif
        endif
    endif
    
    if not cooldown "Main Greater"
        say "[GreaterAbility"
        wait lagTimer_
        cooldown "Main Greater" greaterTimerValue_
    endif
    
    if not warmode
        if not warmode and timer "fishingTimer_" > 8100 and fishingWithNet_ = 0 and justSpecialFishing_ = 0
            if findtype 3520 backpack as pole_
                dress Fishing
                while queued
                endwhile        
            endif
            hotkey "Use Item in Hand"
            settimer "fishingTimer_" 0
        endif    

        while findtype "fish" fishBag_ as fish_
            dclick scissors_
            waitfortarget 
            target fish_
            wait lagTimer_
        endwhile
    
        if not findtype 3530 backpack and timer "checkForNetsTimer_" > 60000
            overhead "MISSING FISHING NET" 38
            settimer "checkForNetsTimer_" 0
        endif

        if findtype "corpse" ground -1 -1 2 as corpse_ and timer "skinningTimer_" > 3000
            if findtype "Elven Spellblade" backpack as blade_
                dclick blade_
                waitfortarget
                target self
                settimer "skinningTimer_" 0
                @ignore corpse_
            endif
        endif
    endif

    if justSpecialFishing_ = 0 and findtype 1286|18824|3530|26683|39345|29410|29230|2646|18824|441|25769|39434|27641|3707|29230|25769 ground -1 -1 12 as fishingSpot_
        if not find fishingSpot_ ground -1 -1 8
            overhead "MOVE CLOSER" 48
            overhead "X" 38 fishingSpot_
            wait lagTimer_
            wait lagTimer_
        else
            if cooldown "Main Greater" < 40000
                overhead "WAITING ON MARINERs" 48
                wait 1000
            else
                @setvar fishingWithNet_ 1
                if find myWeapon_ backpack
                    dclick myWeapon_ 
                endif
                if not warmode and timer "netCastTimer_" > 3000
                    if findtype 3530 backpack as net_
                        getlabel net_ net__
                        if "special" in net__
                            @ignore net_
                        else
                            dclick net_
                            waitfortarget 
                            target self
                            settimer "netCastTimer_" 0
                        endif
                    else 
                        overhead "MISSING NETS" 38
                    endif
                endif
            endif
        endif
    else
        if fishingWithNet_ = 1
            @setvar fishingWithNet_ 0
        endif
    endif
    
    if justSpecialFishing_ = 1
        @setvar fishingWithNet_ 1
        if find myWeapon_ backpack
            dclick myWeapon_ 
        endif        
        if cooldown "Main Greater" < 40000
            overhead "WAITING ON MARINERs"
        else
            if not warmode and timer "netCastTimer_" > 9000
                if findtype 3530 fishBag_ as net_
                    getlabel net_ net__
                    if "special" in net__
                        overhead "CASTING SPECIAL"
                        dclick net_
                        while not targetexists 
                        endwhile
                        while targetexists 
                            overhead "CAST NET" 48
                            wait 500
                        endwhile
                        settimer "netCastTimer_" 0
                     endif
                else 
                    @setvar fishingWithNet_ 0
                    overhead "MISSING NETS" 38
                endif
            endif
        endif
    endif

    if justSpecialFishing_ = 2
        @setvar fishingWithNet_ 1
        if find myWeapon_ backpack
            dclick myWeapon_ 
        endif        
        if cooldown "Main Greater" < 40000
            overhead "WAITING ON MARINERs"
        else
            if not warmode and timer "netCastTimer_" > 4000
                if findtype 3530 backpack as net_
                    getlabel net_ net__
                    if "special" in net__
                        @ignore net_
                    else
                        dclick net_
                        waitfortarget 
                        target self
                        settimer "netCastTimer_" 0
                    endif
                else 
                    overhead "MISSING NETS" 38
                endif
            endif
        endif
    endif

    if not find myAttackTarget_ ground -1 -1 8 or myAttackTarget_ = me_
        hotkey "Target Closest Grey Monster"
        hotkey "Target Closest Enemy Monster"
        hotkey "Next Non-Friendly Player Target"
        hotkey "Next Murderer Player Target"
        if lasttarget != me_ and find lasttarget ground -1 -1 8
            warmode "on"
            @setvar myAttackTarget_ lasttarget
            say "[CrewAttack"
            waitfortarget
            target myAttackTarget_
            wait lagTimer_
        else 
            warmode "off"
        endif
    endif    

    if warmode 
        if find myWeapon_ backpack
            dclick myWeapon_ 
        endif
        
        if find myAttackTarget_ ground -1 -1 8 or myAttackTarget_ = me_
            attack myAttackTarget_
        endif
        
        if not cooldown "Weapon Ability" and warmode
            if weaponAbility_ = 1
                say "[WeaponAbility1"
                wait lagTimer_
            elseif weaponAbility_ = 2
                say "[WeaponAbility2"
                wait lagTimer_
            elseif weaponAbility_ = 3
                say "[WeaponAbility3"
                wait lagTimer_
            endif
            if insysmsg "You cannot perform that ability yet."
                cooldown "Weapon Ability" 2500
            endif
        endif
    endif 
        
endwhile