# This is an automated backup - check out https://outlands.uorazorscripts.com/script/3d9a75bb-acaf-4adb-a583-b22f788a5072 for latest
# Automation by Jaseowns
## Script: Bard Trainer for Prevalia Zoo
## Created by: denox#0
#############################################

#####################" A Simple Script For Raising Your Barding Skills "######################

#############################################################################################
###############################" “ Make Sure We Are Ready! “ "###############################
#############################################################################################
#
# "Step 1:" Set To Match Your "Desired Results"
################# Choose "“" Which Skills To Train: "0=N0, 1=Yes"
@setvar! TrainPeacemaking 1
@setvar! TrainDiscordance 1
@setvar! TrainProvocation 1
@setvar! TrainMusicianshp 1
################ "“““" Choose Level To Train: "0 to 120 (100.1 - 120 Requires Skill Mastery)"
@setvar! DiscoMax 120
@setvar! MusicMax 100
@setvar! PeaceMax 100
@setvar! ProvoMax 120
# 
# "Step 2:" Make sure you have enough "Cheap Basic Instruments" in your inventory
#                           "(drum, lute, flute, harp or tambourine)"
#
# "Step 3:" Go to "Prevalia Zoo" and stand near one of the animal pens
#        "(or your favorite Battle Trainer)"
#
# "Step 4:" Now we "RUN" the script
#
#############################################################################################
###############################"  I Got It From Here!!!  "###################################
#############################################################################################
if findtype '3763' backpack as BasicLute
    @setvar! CurrentInstrument BasicLute
    sysmsg "We found a Lute to begin"
elseif findtype '3740' backpack as BasicDrum
    @setvar! CurrentInstrument BasicDrum
    sysmsg "We found a Drum to begin"
elseif findtype '3742' backpack as BasicTamborine
    @setvar! CurrentInstrument BasicTamborine
    sysmsg "We found a Tamborine to begin"
elseif findtype '3762' backpack as BasicHarp
    @setvar! CurrentInstrument BasicHarp
    sysmsg "We found a Harp to begin"
elseif findtype '10245' backpack as BasicFlute
    @setvar! CurrentInstrument BasicFlute
    sysmsg "We found a Flute to begin"
endif
@setvar! PeacemakingTimerCD 11000
@setvar! DiscordanceTimerCD 11000
@setvar! ProvocationTimerCD 11000
@setvar! MusicianshipTimerCD 7000
if not timerexists PeacemakingTimer
    settimer PeacemakingTimer PeacemakingTimerCD
endif
if not timerexists DiscordanceTimer
    settimer DiscordanceTimer DiscordanceTimerCD
endif
if not timerexists ProvocationTimer
    settimer ProvocationTimer ProvocationTimerCD
endif
if not timerexists MusicianshipTimer
    dclick CurrentInstrument
    settimer MusicianshipTimer 0
endif


while not dead
    if insysmsg "What instrument shall you play?"
        if findtype '3763' backpack as BasicLute
            @setvar! CurrentInstrument BasicLute
        elseif findtype '3740' backpack as BasicDrum
            @setvar! CurrentInstrument BasicDrum
        elseif findtype '3742' backpack as BasicTamborine
            @setvar! CurrentInstrument BasicTamborine
        elseif findtype '3762' backpack as BasicHarp
            @setvar! CurrentInstrument BasicHarp
        elseif findtype '10245' backpack as BasicFlute
            @setvar! CurrentInstrument BasicFlute
        else
            overhead 'Out of instruments' 33
            overhead 'Stopping script' 33
            sysmsg 'Out of instruments'
            sysmsg 'Stopping script'
            stop
        endif
        wft 500
        dclick CurrentInstrument
        clearsysmsg
    endif    

    if TrainPeacemaking = 1
        @setvar! ProvocationTimerCD 20000
    else 
        @setvar! ProvocationTimerCD 11000
    endif
    if TrainProvocation = 1
        @setvar! PeacemakingTimerCD 20000
    else 
        @setvar! PeacemakingTimerCD 11000
    endif
    
    if TrainPeacemaking = 1 and timer PeacemakingTimer > PeacemakingTimerCD and timer DiscordanceTimer > 11000 and timer DiscordanceTimer > 5500 and timer MusicianshipTimer > 2000
        if skill "Peacemaking" < PeaceMax
            useskill 'Peacemaking'
            wft 500
            hotkey "Target Closest Grey Monster"
            settimer PeacemakingTimer 0
        else 
            overhead "Peacemaking skill {{PeaceMax}} achieved" 48
            @setvar! TrainPeacemaking 0
        endif
    elseif TrainDiscordance = 1 and timer DiscordanceTimer > DiscordanceTimerCD and timer PeacemakingTimer > 5500 and timer ProvocationTimer > 5500 and timer MusicianshipTimer > 2000
        if skill "Discordance" < DiscoMax
            useskill 'Discordance'
            wft 500
            hotkey "Target Closest Grey Monster"
            settimer DiscordanceTimer 0
        else 
            overhead "Discordance skill {{DiscoMax}} achieved" 48
            @setvar! TrainDiscordance 0
        endif
    elseif TrainProvocation = 1 and timer ProvocationTimer > ProvocationTimerCD and timer PeacemakingTimer > 11000 and timer DiscordanceTimer > 5500 and timer MusicianshipTimer > 2000
        if skill "Provocation" < ProvoMax
            useskill 'Provocation'
            wft 500
            hotkey "Target Closest Grey Monster"
            wft 500
            target self
            settimer ProvocationTimer 0
        else 
            overhead "Provocation skill {{ProvoMax}} achieved" 48
            @setvar! TrainProvocation 0 
        endif
    elseif TrainMusicianshp = 1 and timer PeacemakingTimer > 1000 and timer DiscordanceTimer > 1000 and timer ProvocationTimer > 1000 and timer MusicianshipTimer > MusicianshipTimerCD
        if skill "Musicianship" < MusicMax
            dclick CurrentInstrument
            wait 250
            settimer MusicianshipTimer 0
        else 
            overhead "Musicianship skill {{MusicMax}} achieved" 48
            @setvar! TrainMusicianshp 0
        endif
    else 
        if TrainMusicianshp = 0 and TrainPeacemaking = 0 and TrainDiscordance = 0 and TrainProvocation = 0
            sysmsg "We have achieved our goals!" 88
            overhead "Session Complete" 68
            sysmsg "Script Ended" 34
            sysmsg "Thank you for training with us!" 48
            overhead "Happy Barding!" 68
            stop
        endif
    endif
endwhile
while dead 
    overhead "User Error" 34
    overhead "System Corrupted" 34
    sysmsg "You are dead!" 48
    wait 3750
endwhile