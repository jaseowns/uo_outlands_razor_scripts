# This is an automated backup - check out https://outlands.uorazorscripts.com/script/71119747-d817-4931-a700-63807879df12 for latest
# Automation by Jaseowns
## Script: Burgerman's Alchemy Leveling Script
## Created by: peepeewilson#0
#############################################

## BURGERMAN'S ALCHEMY LEVELING SCRIPT (60-120)
## Craft useful potions and restock them to your storage shelf
## UPDATED 11/26/25 - Made the script check for regs on each pot type individually and shortened the pause when regs are missing. 

## REQUIREMENTS:
## A Storage Shelf with a loadout containing 10 of each reg and 4 empty bottles
## Mortar and Pestle in your bag - Either add this to your shelf loadout, or stand next to a resource stockpile (either will work)
## Lots of Nightshade, Spiders Silk, Bloodmoss, Mandrake Root, Garlic, Ginseng, Sulfurous Ash, and empty bottles in your shelf
 
## CRAFT ORDER:
## 60-70: Lesser Resist (drinks pot and restocks empty bottle to shelf)
## 70-90: Greater Cure, Greater Heal, Greater Strength, Greater Agility
## 90-100: Greater Explosion
## 100-115: Greater Resist
## 115-120: Lethal Poison
    
clearsysmsg 
gumpclose 949095101
gumpclose 1859005118

if findtype "mortar and pestle" backpack as item
    dclick item
    waitforgump 949095101 5000 
elseif findtype "resource container" ground -1 -1 2 as item
    dclick item
    waitforgump 1859005118
    gumpresponse 7 1859005118
    waitforgump 1859005118 5000
    gumpresponse 100 1859005118
    waitforgump 949095101 5000
endif

if findtype "storage shelf|gothic storage shelf|stump storage shelf" ground -1 -1 2 as shelf
    menu shelf 1
    wait 200
endif

## EDIT THE LINE BELOW IF YOU WISH TO STOP BEFORE 120
if skill "Alchemy" >= 120
    overhead "Hit 120 Alchemy!" 88
    wait 6000
    replay
endif

if skill "Alchemy" < 70
    if counttype 3981 backpack < 6
    overhead "Need more Spiders Silk!" 34
    wait 500
    else
    overhead "60 - 70: Lesser Magic Resist"
    gumpresponse 105 949095101
    waitforgump 949095101 5000
    gumpresponse 200 949095101
    waitforgump 949095101 5000
    wait 500
    hotkey "Drink Magic Resist"
    wait 3000
    endif
endif

if skill "Alchemy" >= 70 and skill "Alchemy" < 90
    if counttype 3972 backpack < 6
    overhead "Need more Garlic!" 34
    wait 500
    else
    overhead "70 - 90: Greater Cure"
    gumpresponse 101 949095101
    waitforgump 949095101 5000
    gumpresponse 202 949095101
    waitforgump 949095101 5000
    wait 3000
    endif
endif  

if skill "Alchemy" >= 70 and skill "Alchemy" < 90
    if counttype 3973 backpack < 6
    overhead "Need more Ginseng!" 34
    wait 500
    else
    overhead "70 - 90: Greater Heal"
    gumpresponse 100 949095101
    waitforgump 949095101 5000
    gumpresponse 202 949095101
    waitforgump 949095101 5000
    wait 3000
    endif
endif
    
if skill "Alchemy" >= 70 and skill "Alchemy" < 90
    if counttype 3974 backpack < 6
    overhead "Need more Mandrake Root!" 34
    wait 500
    else
    overhead "70 - 90: Greater Strength"
    gumpresponse 103 949095101
    waitforgump 949095101 5000
    gumpresponse 201 949095101
    waitforgump 949095101 5000
    wait 3000
    endif
endif   

if skill "Alchemy" >= 70 and skill "Alchemy" < 90
    if counttype 3963 backpack < 6
    overhead "Need more Bloodmoss!" 34
    wait 500
    else
    overhead "70 - 90: Greater Agility"
    gumpresponse 104 949095101
    waitforgump 949095101 5000
    gumpresponse 201 949095101
    waitforgump 949095101 5000
    wait 3000
    endif
endif
    
if skill "Alchemy" >= 90 and skill "Alchemy" < 100
    if counttype 3980 backpack < 6
    overhead "Need more Sulfurous Ash!" 34
    wait 500
    else
    overhead "90 - 100: Greater Explosion"
    gumpresponse 107 949095101
    waitforgump 949095101 5000
    gumpresponse 202 949095101
    waitforgump 949095101 5000
    wait 3000
    endif
endif

if skill "Alchemy" >= 100 and skill "Alchemy" < 115
    if counttype 3981 backpack < 9
    overhead "Need more Spiders Silk!" 34
    wait 500
    else
    overhead "100 - 115: Greater Magic Resist"
    gumpresponse 105 949095101
    waitforgump 949095101 5000
    gumpresponse 202 949095101
    waitforgump 949095101 5000
    wait 3000
    endif
endif

if skill "Alchemy" >= 115 and skill "Alchemy" < 120
    if counttype 3976 backpack < 10
    overhead "Need more Nightshade!" 34
    wait 500
    else
    overhead "115 - 120: Lethal Poison"
    gumpresponse 106 949095101
    waitforgump 949095101 5000
    gumpresponse 206 949095101
    waitforgump 949095101 5000
    wait 3000
    endif
endif

if findtype "storage shelf|gothic storage shelf|stump storage shelf" ground -1 -1 2 as shelf
    menu shelf 0
    wft 500
    target self
    wait 200
    gumpclose 3232825965
endif

loop