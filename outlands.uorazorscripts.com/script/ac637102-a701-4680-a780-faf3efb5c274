# This is an automated backup - check out https://outlands.uorazorscripts.com/script/ac637102-a701-4680-a780-faf3efb5c274 for latest
# Automation by Jaseowns
## Script: Bapeth's Trapper Background
## Created by: barryroser#0
#############################################

# "Bapeths Trapper Background"
# Jan 1st 2026 - Added setting to control default Shield Stance for Parry users
#
# "REQUIRED": <---------- "Important information!"
# User must create two cooldowns named:
#
# "Move" --- Cooldown : 0.3
#    
# "Bandage" --- No data needed
# Bandage cooldown assumes you have max points in "Self Treatment" (Healers Codex) and 100 dex
#
# Cooldowns can be found in the in-game options menu
#
# "What it does"
#
# Placed Trap Overhead indicator (shows your trap + info about it)
# Auto Bandage
# Auto Cure
# Use Cleansing Brew (activate by going into warmode while bleeding/diseased)
# Auto Drink str/resist/agi or Warn when needed (See setting below)
# Warn when reflect/reactive are needed
# Auto pouch when paralyzed 
# Auto str pot when weakened
# Auto re-equip after disarmed
# Aspect Checker (Armor)
# Shield Codex - Shield bash, Warding, Bulwark (See Bapeths Parry Hotkeys for extra plug-ins to this script)
# For dex builds - auto attack / use weapon ability
# Note on Dex Builds : "BOOM BOOM" hotkey script required to activate auto-bullwark/ auto attack / auto weapon ability
# Wizard hat stealth trick (stamina) if you are a stealth trapper
#
# Bapeths "BOOM BOOM" (one button trap placer/detonator/remover) script can be found here :
# https://outlands.uorazorscripts.com/script/2d2e974e-e0b8-44f3-bb2c-1971862c2370
#
##########################################################################################################################################################################
#
# "SCRIPT SETTINGS"
#
# Set your "Weapon Ability" to auto use it while in combat
# One of these must always be set to 1, leave the others as a 0
#
# WEAPON ABILITY
@setvar! "BapWeaponAbility-1" 0
@setvar! "BapWeaponAbility-2" 1
@setvar! "BapWeaponAbility-3" 0
#
##########################################################################################################################################################################
#
# To enable "Auto-Drink Buff Pots" set the below setting to 1
# The script will drink Strength, Agility, and Magic Resist if it doesnt detect a buff
#
# AUTO BUFF POTS
@setvar! "AutoBuffPots" 1
#
##########################################################################################################################################################################
#
# To set "Gold Drop Amount" set one of the below settings to 1
# It will drop either a small amount (444), a medium amount (2222), or a large amount (3444)
#
# GOLD DROPPER
@setvar! "Drop444" 1
@setvar! "Drop2222" 0
@setvar! "Drop3444" 0
#
##########################################################################################################################################################################
#
# For Parry Codex Users "Set your Default Stance"
# Because the script uses an auto-warding routine you can set the following stance to default when not bleeding/diseased
# Select which stance you wish to defualt to with a 1, leave the rest at 0
#
# DEFAULT PARRY STANCE
@setvar! "DefaultShieldBash" 1
@setvar! "DefaultTestudo" 0
@setvar! "DefaultMirror" 0
@setvar! "DefaultBulwark" 0
#
##########################################################################################################################################################################
#
# "Auto Switch Flipper"
# This is for green wall switches in "Time Dungeon" - Requires "Time Codex" Open this while the script is palying to swtich the setting on/off
# Go into warmode when standing near a wall switch and the script will flip the switch and return to peacemode
#
# "Auto-Recycler"
# This feature will auto recycle bad items and keep the good ones - To use this feature:
# You must be carrying at least one "container identification wand" and at least one "recycler tool" type (Tinker Tools, Sewing Kit, Saw, Tongs, or Scribes Pen)
#
# "Loot-Sense & Vacuum Mode"
# This function will overhead display *loot* above items on the ground and pick them up as you pass by (2 tiles)
# Be aware that this may make you overweight from large gold piles from time to time
# To toggle this feature on/off (default is off) "Double click a Wizard Grimoire"
#
# "Gold Sacker"
# To toggle this feature on/off and set a new sack bag open the "Atlas" while the script it playing
# This feature will set a ground bag to drop gold into
# Check out another script by Bapeth called "Bapeths Object Drag & Dropper" found here : https://outlands.uorazorscripts.com/script/619a6bcc-bf10-4c8c-a645-282c94f6fa99
#
# Script starts here...

// Named Summon Routine
if not find "bsummon1" ground and findtype "an earth elemental|a fire elemental|a water elemental|an air elemental|a daemon" ground -1 -1 1 as sum1  
    if noto sum1 = "friend"
        # Name your summon if you like (no spaces must be unique)
        rename sum1 "TrapGolem"
    endif
elseif not find "bsummon1" ground and findtype "TrapGolem" ground as sum1
    @setvar! "bsummon1" sum1
endif
if not find "bsummon2" ground and findtype "an earth elemental|a fire elemental|a water elemental|an air elemental|a daemon" ground -1 -1 1 as sum2  
    if noto sum2 = "friend"
        # Name your summon if you like (no spaces must be unique)
        rename sum2 "TrapGolems"
    endif
elseif not find "bsummon2" gorund and findtype "TrapGolems" ground as sum2
    @setvar! "bsummon2" sum2
endif
if findtype "a primordial whelp|an aegis rat|a cougar|a wolf|a grizzly bear|a brown bear|a corpse eater|a familiar|a rock guar|a flamehound|a sand crab|a monitor hatchling|a firebat|a black bear|a muck|a polar bear|shallow water|a scorpion|a swamp spider|a searing lizard|a minion|a fire salamander|a fire ant|a silverback|a jaguar|an aegis mongbat|a cave bat|a primordial|a monitor|an asp|an imp|a winter wolf|a cave bear|a giant swamp slug|an aegis slime|a trapdoor spider|a chameleon|a searing imp|a dragon whelp|a drake whelp|a devilbat|a wolfhound|a blood ape|an army ant|a molten mongbat|a stinger|a rime guar|an aegis scorpion|a skulker|a komodo|a shade wolf|a husk crab|an aegis imp|a fire minion|a snowdrift|a spitting viper|a vampire bat" as csum
    if noto csum = "friend"
        # Name your summon if you like (no spaces must be unique)
        rename csum "TrapCreature"
    endif
endif
// End of Named Summon Routine

// Overhead Notification that script is active
if skill "Parrying" >= 80
    //donothing
else
    if not timerexists bapactive
        createtimer bapactive
        settimer bapactive 5000
    elseif timer bapactive >= 5000
        overhead "Boom time..." 78
        settimer bapactive 0
    endif
endif
// End of overhead notification

// Auto Attack + Weapon Ability Routine
if skill "Swordsmanship" >= 80 or skill "macefighting" >= 80 or skill "fishing" >= 80 or skill "fencing" >= 80 or skill "archery" >= 80 or skill "wrestling" >= 80 and not casting and not targetexists and not cooldown "Move"
    if not gumpexists 3954121934
        say '[abilityhotbar'
        waitforgump 3954121934 500
    endif
    if timerexists baptrapcombat
        if timer baptrapcombat <= 2000
            clearsysmsg 
            if not varexist "currentbaptarget"
                hotkey 'Target Closest Grey Monster'
                if insysmsg "No one matching"
                    //donothing
                else
                    @setvar "currentbaptarget" lasttarget 
                endif
                hotkey "Cancel Current Target"
            elseif not find "currentbaptarget" ground -1 -1 8
                hotkey 'Target Closest Grey Monster'
                if insysmsg "No one matching"
                    //donothing
                else
                    @setvar "currentbaptarget" lasttarget 
                endif
                hotkey "Cancel Current Target"
            elseif find "currentbaptarget" ground -1 -1 8
                if not timerexists bapweaponabilityuse
                    createtimer bapweaponabilityuse
                    settimer bapweaponabilityuse 31000
                elseif timer bapweaponabilityuse >= 31000
                    clearsysmsg
                    if "BapWeaponAbility-1" = 1
                        say "[WeaponAbility1"
                        getlabel backpack ping
                        if insysmsg "You cannot perform that ability yet"
                            //donothing
                        else 
                            settimer bapweaponabilityuse 0
                        endif
                    elseif "BapWeaponAbility-2" = 1
                        say "[WeaponAbility2"
                        getlabel backpack ping
                        if insysmsg "You cannot perform that ability yet"
                            //donothing
                        else 
                            settimer bapweaponabilityuse 0
                        endif
                    elseif "BapWeaponAbility-3" = 1
                        say "[WeaponAbility3"
                        getlabel backpack ping
                        if insysmsg "You cannot perform that ability yet"
                            //donothing
                        else 
                            settimer bapweaponabilityuse 0
                        endif
                    endif
                endif
                attack "currentbaptarget"
            endif
        endif
    endif
endif
// End of Auto Attack + Weapon Ability Routine

// Aspect checker routine
if not timerexists checkaspect
    createtimer checkaspect
    settimer checkaspect 60000
elseif timer checkaspect >= 60000 
    if findlayer self innertorso as aspectarm
        getlabel aspectarm desc
        if "aspect" in desc
            if "air" in desc
                overhead "<Air Active>" 2263
            elseif "arcane aspect" in desc
                overhead "<Arcane Active>" 1560
            elseif "artisan aspect" in desc
                overhead "<Artisan Active>" 2096
            elseif "blood aspect" in desc
                overhead "<Blood Active>" 2087
            elseif "command aspect" in desc
                overhead "<Command Active>" 1782
            elseif "death aspect" in desc
                overhead "<Death Active>" 2155
            elseif "discipline aspect" in desc
                overhead "<Discipline Active>" 2490
            elseif "earth aspect" in desc
                overhead "<Earth Active>" 2767
            elseif "eldritch aspect" in desc
                overhead "<Eldritch Active>" 2615
            elseif "fire aspect" in desc
                overhead "<Fire Active>" 2635
            elseif "fortune aspect" in desc
                overhead "<Fortune Active>" 2843
            elseif "frost aspect" in desc
                overhead "<Frost Active>" 2234
            elseif "gadget aspect" in desc
                overhead "<Gadget Active>" 2669
            elseif "harvest aspect" in desc
                overhead "<Harvest Active>" 2086
            elseif "holy aspect" in desc
                overhead "<Holy Active>" 2153
            elseif "lightning aspect" in desc
                overhead "<Lightning Active>" 2650
            elseif "lyric aspect" in desc
                overhead "<Lyric Active>" 2241
            elseif "madness aspect" in desc
                overhead "<Madness Active>" 1060
            elseif "poison aspect" in desc
                overhead "<Poison Active>" 2897
            elseif "shadow aspect" in desc
                overhead "<Shadow Active>" 1790
            elseif "void aspect" in desc
                overhead "<Void Active>" 2599
            elseif "water aspect" in desc
                overhead "<Water Active>" 1788
            elseif "war aspect" in desc
                overhead "<War Active>" 2503
            endif
        else 
            overhead "No aspect enabled" 45
        endif
    endif
    settimer checkaspect 0
endif
// End of aspect checker routine

// Auto Re-equip after disarm routine
if insysmsg "disarms you!"
    createtimer disarmtimerright
    createtimer disarmtimerleft
    getlabel backpack ping
endif
if not findlayer self righthand and not findlayer self lefthand
    //donothing
else
    if not timerexists checkequippeditems
        createtimer checkequippeditems
        settimer checkequippeditems 15000
    elseif timerexists checkequippeditems
        if timer checkequippeditems >= 15000
            settimer checkequippeditems 0
            if timerexists equippedrighthand and not timerexists disarmtimerright
                removetimer equippedrighthand
                @unsetvar "myrighthanditem"
            endif
            if timerexists equippedlefthand and not timerexists disarmtimerleft
                removetimer equippedlefthand
                @unsetvar "mylefthanditem"
            endif
        endif
    endif
endif
if timerexists disarmtimerright and findlayer self righthand
    removetimer disarmtimerright
endif
if timerexists disarmtimerleft and findlayer self lefthand
    removetimer disarmtimerleft
endif
if not timerexists disarmtimerright or not timerexists disarmtimerleft
    if not varexist myrighthanditem and timerexists equippedrighthand
        removetimer equippedrighthand
    endif
    if not varexist mylefthanditem and timerexists equippedlefthand
        removetimer equippedlefthand
    endif
    if not find "myrighthanditem" self and findlayer self righthand
        removetimer equippedrighthand
    endif
    if not find "mylefthanditem" self and findlayer self lefthand
        removetimer equippedlefthand
    endif
    if not timerexists equippedrighthand and findlayer self righthand as rheqitem
        getlabel rheqitem desc
        if "spells" in desc
            createtimer exceptionalspellbook
        elseif timerexists exceptionalspellbook
            removetimer exceptionalspellbook
        endif
        @setvar! "myrighthanditem" rheqitem
        createtimer equippedrighthand
    endif
    if not timerexists equippedlefthand and findlayer self lefthand as lheqitem
        @setvar! "mylefthanditem" lheqitem
        createtimer equippedlefthand
    endif
endif
if timerexists disarmtimerright
    if timer disarmtimerright >= 5000
        while not targetexists and not casting and varexist myrighthanditem and not timerexists exceptionalspellbook and not findlayer self righthand
            dclick "myrighthanditem"
            while queued
                //dn
            endwhile
        endwhile
        while not targetexists and not casting and varexist myrighthanditem and timerexists exceptionalspellbook and not findlayer self righthand
            while queued
                //dn
            endwhile
            drop backpack
            lift "myrighthanditem"
            drop self righthand
            while queued
                //dn
            endwhile
        endwhile
        while targetexists and not casting and varexist myrighthanditem and not findlayer self righthand
            while queued
                //dn
            endwhile
            drop backpack
            lift "myrighthanditem"
            drop self righthand
            while queued
                //dn
            endwhile
        endwhile
    endif
endif
if timerexists disarmtimerleft  
    if timer disarmtimerleft >= 5000
        while not targetexists and not casting and varexist mylefthanditem and not findlayer self lefthand
            dclick "mylefthanditem"
            while queued
                //dn
            endwhile
        endwhile
        while targetexists and not casting and varexist mylefthanditem and not findlayer self lefthand
            while queued
                //dn
            endwhile
            drop backpack
            lift "mylefthanditem"
            drop self lefthand
            while queued
                //dn
            endwhile
        endwhile
    endif
endif
if find "mylefthanditem" backpack and not casting and not targetexists and skill "Tactics" >= 80 and not findlayer self lefthand
    dclick "mylefthanditem"
    pause 500
    getlabel backpack ping
endif
if find "myrighthanditem" backpack and not casting and not targetexists and skill "Tactics" >= 80 and not findlayer self righthand
    dclick "myrighthanditem"
    pause 500
    getlabel backpack ping
endif
// End of Auto Re-equip after disarm routine

// Auto Shield Stance Routine
if skill "Parrying" >= 80
    if not timerexists checkshieldcodex
        createtimer checkshieldcodex
        settimer checkshieldcodex 13000
    elseif timer checkshieldcodex >= 13000
        settimer checkshieldcodex 0
        if findtype "shield codex" backpack as scodex
            getlabel scodex desc
        if not findbuff "Bleed" and not findbuff "Diseased" and mana >= 3
                if "DefaultShieldBash" = 1
                    if "Shield Bash" in desc
                        //donothing
                    elseif "Testudo" in desc
                        say "[ShieldsStance1"
                    elseif "Mirror" in desc
                        say "[ShieldsStance1"
                    elseif "Bulwark" in desc
                        say "[ShieldsStance1"
                    endif
                elseif "DefaultTestudo" = 1
                    if "Shield Bash" in desc
                        say "[ShieldsStance3"
                    elseif "Testudo" in desc
                        //donothing
                    elseif "Mirror" in desc
                        say "[ShieldsStance3"
                    elseif "Bulwark" in desc
                        say "[ShieldsStance3"
                    endif
                elseif "DefaultMirror" = 1
                    if "Shield Bash" in desc
                        say "[ShieldsStance4"
                    elseif "Testudo" in desc
                        say "[ShieldsStance4"
                    elseif "Mirror" in desc
                        //donothing
                    elseif "Bulwark" in desc
                        say "[ShieldsStance4"
                    endif
                elseif "DefaultBulwark" = 1
                     if "Shield Bash" in desc
                        say "[ShieldsStance5"
                    elseif "Testudo" in desc
                        say "[ShieldsStance5"
                    elseif "Mirror" in desc
                        say "[ShieldsStance5"
                    elseif "Bulwark" in desc
                        //donothing
                    endif
                endif  
            endif
            if "Shield Bash" in desc
                overhead "--Shield Bash Stance--" 2490
            elseif "Warding" in desc
                overhead "--Warding Stance--" 2087
                if not findbuff "Bleed" and not findbuff "Diseased" and mana >= 3
                    if "DefaultShieldBash" = 1
                        say "[ShieldsStance1"
                    elseif "DefaultTestudo" = 1
                        say "[ShieldsStance3"
                    elseif "DefaultMirror" = 1
                        say "[ShieldsStance4"
                    elseif "DefaultBulwark" = 1
                        say "[ShieldsStance5"
                    endif
                    getlabel backpack ping
                    say "[ShieldsFinisher1" 45
                endif
            elseif "Testudo" in desc
                overhead "--Testudo Stance--" 2503
            elseif "Mirror" in desc
                overhead "--Mirror Stance--" 2615
            elseif "Bulwark" in desc
                overhead "--Bulwark Stance--" 2767
            else 
                if mana >= 3
                    if "DefaultShieldBash" = 1
                        say "[ShieldsStance1"
                    elseif "DefaultTestudo" = 1
                        say "[ShieldsStance3"
                    elseif "DefaultMirror" = 1
                        say "[ShieldsStance4"
                    elseif "DefaultBulwark" = 1
                        say "[ShieldsStance5"
                    endif
                endif
            endif
        endif
    endif
    if "DefaultBulwark" = 1
        if not timerexists standstill
            createtimer standstill
        endif
        if cooldown "Move"
            createtimer OTBulwarkIndicator
            settimer standstill 0
        endif
        if timer standstill >= 3000
            if timerexists OTBulwarkIndicator
                overhead "Bulwark Fortified!" 88
                removetimer OTBulwarkIndicator
            endif
        endif
    endif
    if not timerexists bleedbuffcheck and findbuff "Bleed" and not findbuff "Diseased" and mana >= 3
        removetimer bullwarkactive
        createtimer bleedbuffcheck
        say "[ShieldsStance2" 45
        getlabel backpack ping
        say "[ShieldsFinisher2" 45
    elseif timerexists bleedbuffcheck and not findbuff "Bleed" and not findbuff "Diseased" and mana >= 3
        removetimer bleedbuffcheck
        pause 800
        getlabel backpack ping
        if "DefaultShieldBash" = 1
            say "[ShieldsStance1"
        elseif "DefaultTestudo" = 1
            say "[ShieldsStance3"
        elseif "DefaultMirror" = 1
            say "[ShieldsStance4"
        elseif "DefaultBulwark" = 1
            say "[ShieldsStance5"
        endif
        getlabel backpack ping
        say "[ShieldsFinisher1" 45
    endif
    if not timerexists diseasebuffcheck and findbuff "Diseased" and not findbuff "Bleed" and mana >= 3
        removetimer bullwarkactive
        createtimer diseasebuffcheck
        say "[ShieldsStance2" 45
        getlabel backpack ping
        say "[ShieldsFinisher2" 45
    elseif timerexists diseasebuffcheck and not findbuff "Diseased" and not findbuff "Bleed" and mana >= 3
        removetimer diseasebuffcheck
        pause 800
        getlabel backpack ping
        if "DefaultShieldBash" = 1
            say "[ShieldsStance1"
        elseif "DefaultTestudo" = 1
            say "[ShieldsStance3"
        elseif "DefaultMirror" = 1
            say "[ShieldsStance4"
        elseif "DefaultBulwark" = 1
            say "[ShieldsStance5"
        endif
        getlabel backpack ping
        say "[ShieldsFinisher1" 45
    endif
    if timerexists baptrapbullwark and mana >= 3
        if not findbuff "Diseased" and not findbuff "Bleed" and not timerexists defaultstance and timer baptrapbullwark <= 10000
            if not timerexists "bullwarktime"
                createtimer bullwarktime
            endif    
            if cooldown "Move"
                settimer bullwarktime 0
            endif
            if not timerexists bullwarkactive and timer bullwarktime >= 3000
                say "[ShieldsStance5" 45
                createtimer bullwarkactive
            endif
        endif
    endif
    if cooldown "Move"
        settimer bullwarktime 0
    endif
    if timerexists bullwarkactive and mana >= 3
        if timer bullwarktime < 3000
            if "DefaultShieldBash" = 1
                say "[ShieldsStance1"
            elseif "DefaultTestudo" = 1
                say "[ShieldsStance3"
            elseif "DefaultMirror" = 1
                say "[ShieldsStance4"
            elseif "DefaultBulwark" = 1
                say "[ShieldsStance5"
            endif
            removetimer bullwarkactive
        endif
    endif
endif
// End of Auto Shield Stance Routine

// Overhead Trap Display routine
if not find "placedtrap" ground and findtype 51163 ground -1 -1 8 as smokingtrap
    @setvar! "placedtrap" smokingtrap
endif
if not timerexists armedtimer
    createtimer armedtimer
    settimer armedtimer 3000
endif
if find "placedtrap" ground -1 -1 8 as smokingtrap and timer armedtimer >= 1500
    overhead "*Armed*" 38 smokingtrap
    settimer armedtimer 0
elseif find "placedtrap" ground as smokingtrap and timer armedtimer >= 1500
    overhead "*Too Far*" 45 smokingtrap
    settimer armedtimer 0
endif
// End of Overhead Trap Display routine

// Utility Routine
if skill "healing" >= 20 and not casting and not targetexists 
    if hp = maxhp
        //donothing
    elseif not bandaging and findtype 3617 backpack
        hotkey "Bandage Self"
        cooldown "Bandage" 7000
    endif
endif
if skill "Magery" >= 80
    if timerexists reflect and not findbuff "Magic Reflection"
        if timer reflect > 30000
            overhead "--Cast Reflect--" 2122
            settimer reflect 12000
        endif
    endif
    if findbuff "Magic Reflection" and timerexists reflect
        removetimer reflect
    endif
    if not findbuff "Magic Reflection" and not timerexists reflect
        createtimer reflect
    endif
endif
if skill "Magery" >= 20
    if timerexists reactive and not findbuff "Reactive Armor"
        if timer reactive > 30000
            overhead "--Cast Reactive--" 2122
            settimer reactive 13500
        endif
    endif
    if findbuff "Reactive Armor" and timerexists reactive
        removetimer reactive
    endif
    if not findbuff "Reactive Armor" and not timerexists reactive
        createtimer reactive
    endif
endif
if skill "Magery" >= 40
    if timerexists protection and not findbuff "Protection"
        if timer protection > 30000
            overhead "--Cast Protection--" 2122
            settimer protection 9000
        endif
    endif
    if findbuff "Protection" and timerexists protection
        removetimer protection
    endif
    if not findbuff "Protection" and not timerexists protection
        createtimer protection
    endif
endif
if warmode and not hidden and not timerexists brew and not targetexists and findbuff "Bleed" and findtype 50675 backpack as cbrew
    clearsysmsg 
    while queued
        //donothing
    endwhile
    dclick cbrew
    getlabel backpack ping
    if insysmsg "You drink a cleansing brew"
        createtimer brew
    endif
elseif warmode and not hidden and not timerexists brew and not targetexists and findbuff "Diseased" and findtype 50675 backpack as cbrew
    while queued
        //donothing
    endwhile
    dclick cbrew
    getlabel backpack ping
    if insysmsg "You drink a cleansing brew"
        createtimer brew
    endif
endif
if timerexists brew
    if timer brew >= 120000
        removetimer brew
    endif
endif
if not hidden and not casting and findbuff "Weaken" and not targetexists and findtype "White Potion" backpack as stpot
    while queued
        //donothing
    endwhile
    dclick stpot
    getlabel backpack ping
endif
if "AutoBuffPots" = 1
    if not targetexists and not hidden and not casting and not findbuff "Magic Resist Potion" and findtype "Black Potion" backpack as respot
        while queued
            //donothing
        endwhile
        dclick respot
        pause 500
    endif
    if not targetexists and not hidden and not casting and not findbuff "Strength" and findtype "White Potion" backpack as wpot
        while queued
            //donothing
        endwhile
        dclick wpot
        pause 500
    endif
    if not targetexists and not hidden and not casting and not findbuff "Agility" and findtype "Blue Potion" backpack as bpot
        while queued
            //donothing
        endwhile
        dclick bpot
        pause 500
    endif
endif
if "AutoBuffPots" = 0
    if not timerexists topupstr
        createtimer topupstr
        settimer topupstr 30000
    elseif timer topupstr >= 30000 and not hidden and not casting and str = 100 and findtype "White Potion" backpack as stpot
        while queued
            //donothing
        endwhile
        overhead "--Drink Strength--" 2041
        settimer topupstr 0
    endif
    if not timerexists topupdex
        createtimer topupdex
        settimer topupdex 30000
        elseif timer topupdex >= 30000 and not hidden and not casting and str = 100 and findtype "Blue Potion" backpack as agipot
        while queued
            //donothing
        endwhile
        overhead "--Drink Agility--" 2041
        settimer topupdex 0
    endif
    if not timerexists topupres
        createtimer topupres
        settimer topupres 30000
    elseif timer topupres >= 30000 and not hidden and not casting and not findbuff "Magic Resist Potion" and findtype "Black Potion" backpack as respot
        while queued
            //donothing
        endwhile
        overhead "--Drink Resist--" 2041
        settimer topupres 0
    endif
endif
if not targetexists and not hidden and not casting and stam <= 4 and findtype "Red Potion" backpack as redpot
    while queued
        //donothing
    endwhile
    dclick redpot
    pause 500
endif
if not hidden and paralyzed and not targetexists and findtype "pouch" backpack 38
    while queued
        //donothing
    endwhile
    say "[Pouch"
    pause 500
endif
if not hidden and not casting and poisoned and not targetexists and findtype "Orange Potion" backpack as curepot
    while queued
        //donothing
    endwhile
    dclick curepot
    pause 500
endif
if not targetexists and not casting and not findbuff "Food Satisfaction"
    if findtype 2429|28885|29774|29773|28880|28888|28881|28883|28886|28879 backpack as food
        while queued
            //donothing
        endwhile
        dclick food
        pause 500
    endif
endif
// End of Utility Routine

// Container Wand Recycler Routine
if insysmsg "What should I use these scissors on?" and findtype "sewing kit|saw|pen and ink|tongs|tool kit" backpack as breakdowntool
    hotkey "Cancel Current Target"
    @clearignore
    if not find "ContainerIDWand" and findtype 20495 backpack as contIDwand
        @setvar "ContainerIDWand" contIDwand
    endif
    if find "ContainerIDWand" backpack
        clearsysmsg 
        while queued
            //donothing
        endwhile
        dclick "ContainerIDWand"
        wft 500
        overhead "Select Container to ID..." 88
        while targetexists 
            //donothing
        endwhile
        getlabel backpack ping
        overhead "Attempting recycle..." 88
        if insysmsg "No unidentified items found."
            overhead "Nothing fresh to ID..." 45
        elseif insysmsg "That is not a container." 
            overhead "Invalid container, try again.." 45
        endif
        while findtype 3834 backpack 0 as bbook
            @ignore bbook
        endwhile
        while findtype 5056|5059|5060|5061|5063|5070|5074|5075|5076|5078|5085|5089|5090|5101|5103|5105|5106|5129|5131|5132|5135|5138|5139|5142|5143|5144|5146|5201|5203|5204|5205|5207|7169|7170|7173|7175|7177|7179|7181|7610|7947|31003|31004|31005|31006|31007|31008|31009|31010|31011|31012|31015|31191|31017|31019|31021|31023|31025|31027|31029|31031|31033|31035|31037|31038|31041|31043|31045|31047|31049|31051|31053|31055|3834|3740|3742|3762|3763|10245|20006|20008|20010|20012|20014|20016|7026|7027|7029|7031|7033|7034|7035|7107|7109|31002|31130|3920|5042|5117|30990|30993|30994|30995|31184|31186|3719|3938|5121|5123|5125|30989|30992|30996|30997|30998|31176|31188|3568|3713|3721|3932|5040|5044|5112|5127|5177|5179|5181|30991|30999|31000|31001|31014|31178|31180|31182|3909|3911|3913|3915|3917|3934|3937|5046|5049|5115|5119|5182|5185|5187|30988|31128|31190|22187|31141|31142|31169|31172 backpack as IDitem
            getlabel IDitem desc
            if "aspect" in desc or "training" in desc or "vanquishing" in desc or "supremely" in desc or "slaying" in desc or "enticement" in desc or "invulnerability" in desc or "power" in desc or "exceedingly accurate force" in desc or "exceedingly potent" in desc or "valorite" in desc or "valehide" in desc or "valewood" in desc or "avarite" in desc or "avarhide" in desc or "avarwood" in desc or "verite" in desc or "verehide" in desc or "verewood" in desc or "agapite" in desc or "rosehide" in desc or "rosewood" in desc or "fortification" in desc
                @ignore IDitem
                overhead "*strong magic*" 2091 IDitem
            elseif "durable" in desc or "substantial" in desc or "massive" in desc or "fortified" in desc or "indestructible" in desc or "exceptional" in desc or "accurate" in desc or "surpassingly" in desc or "eminently" in desc or "exceedingly" in desc or "ruin" in desc or "might" in desc or "force" in desc or "power" in desc or "mastercrafted" in desc or "defense" in desc or "guarding" in desc or "hardening" in desc or "potent" in desc or "melodious" in desc
                overhead "*trash*" 1779 IDitem
                while not gumpexists 949095101
                    while queued
                        //donothing
                    endwhile
                    dclick breakdowntool
                    waitforgump 949095101 1000
                endwhile
                while not ingump "Recycle Single Item" 949095101
                    gumpresponse 2
                    waitforgump 949095101 1000
                endwhile                                
                gumpresponse 3
                waitforgump 949095101 1000
                target IDitem
                getlabel backpack ping
                pause 250
                gumpclose 949095101
            else
                @ignore IDitem
            endif
            if not find "ContainerIDWand" backpack
                break
            endif
        endwhile
        if gumpexists 949095101
            gumpclose 949095101
        endif
    else
        overhead "No Container ID Wand..." 38
    endif
    if targetexists 
        hotkey "Cancel Current Target"
    endif
    overhead "Recycle Complete." 88
endif
// End of Container Wand Recycler Routine

// Time Wall Switch Routine
if not timerexists timeswitch
    if gumpexists 3352257441
        gumpclose 3352257441
        createtimer timeswitch
        overhead "--Time Switch Enabled--" 88
    endif
endif
if timerexists timeswitch
    if gumpexists 3352257441
        gumpclose 3352257441
        removetimer timeswitch
        overhead "--Time Switch Disabled--" 38
    endif
endif
if timerexists timeswitch
    if not timerexists switchandstufftimer
        createtimer switchandstufftimer
        settimer switchandstufftimer 1000
    endif
    if timer switchandstufftimer >= 1000
        for 19
            if index > 0 and findtype 4239|4240 ground -1 -1 index as switchandstuff
                getlabel switchandstuff desc
                overhead "*switch*" 78 switchandstuff
                @ignore switchandstuff
            endif
            if warmode and findtype 4239|4240 ground -1 -1 2
                break
            endif 
        endfor
        @clearignore
        settimer switchandstufftimer 0
    endif
    if warmode and findtype 4239|4240 ground -1 -1 2 as timewallswitch
        dclick timewallswitch
        warmode off
        getlabel backpack ping
    endif
endif
// End of Time Wall Switch Routine

// Loot Vacuum Routine
if not timerexists lootvacuumswitch
    if gumpexists 1281623709
        gumpclose 1281623709
        createtimer lootvacuumswitch
        overhead "--Loot Vacuum Enabled--" 2085
    endif
endif
if timerexists lootvacuumswitch
    if gumpexists 1281623709
        gumpclose 1281623709
        removetimer lootvacuumswitch
        overhead "--Loot Vacuum Disabled--" 1779
    endif
endif
while diffweight <= 0 and findtype "gold coin" backpack as dropgold
    if not hidden and stam <= 4 and findtype "Red Potion" backpack as redpot
        while queued
            //donothing
        endwhile
        dclick redpot
        pause 500
        getlabel backpack ping
    endif
    overhead "My cup runneth over..." 1194
    drop backpack
    if "Drop444" = 1
        lift dropgold 444
    elseif "Drop2222" = 1
        lift dropgold 2222
    elseif "Drop3444" = 1
        lift dropgold 3444
    endif
    drop 0
    while queued
        //donothing
    endwhile
endwhile
if timerexists lootvacuumswitch
    if not timerexists lootsensetimer
        createtimer lootsensetimer
        settimer lootsensetimer 1000
    endif
    if not findtype 4239|4240 ground -1 -1 2 and timer lootsensetimer >= 1000
        while findtype 3827 ground 0 as zeroscroll
            @ignore zeroscroll
        endwhile
        while findtype 4225 ground 0 as zeroleather
            @ignore zeroleather
        endwhile
        while findtype 7127 ground 0 as zeroboard
            @ignore zeroboard
        endwhile
        for 19
            if skill "Lockpicking" >= 50 and findtype "wooden chest|metal chest" ground -1 -1 2
                break  
            endif
            if findtype "chain link|37477|29368|18660|20099|31303|31165|tribal ruins|electric ankh|54230|44985|glass display|20049|Pig Iron|broken shield|blackrock fragment|gargish fragment|11753|18705|55247|49482|53694|54570|53696|53876|50612|55420|53280|53473|54831|50278|49528|bottle set|entrail bucket|mounted stalaghorn|sea glass bulb|juggling daggers|st patricks cross|divination orb|mirror display|murder of crows|charmed cobra|prodigy fiddle|wilting rose belljar|scale of balance|screaming mandrake|statue of the siren|the final hour|55204|specimen jars|shattered weapon|bird cage|widows grief|54684|27612|49223|54591|54601|54718|7408|19948|58325|19959|19822|49730|49729|19944|19949|47613|47612|47611|47610|47611|3986|3966|cathedral tapestry|Darkscale tapestry|large painting|landscape painting|portrait painting|figurine|statue|20291|44987|49730|47107|18653|45127|20103|47103|29363|47105|29361|43453|5899|47109|45250|41509|43447|18657|4025|51308|45156|51304|45214|28769|28799|28803|28765|45211|45248|51259|45220|51328|45218|3842|51327|45222|45241|51336|11858|51375|45282|54722|45255|45246|28795|44983|45235|45251|45238|51258|51314|28761|45236|51260|54718|18400|58325|45252|18656|47111|3839|45239|45242|45254|45281|45247|42241|45216|28775|2594|51321|3838|51318|5901|5905|45259|5903|48407|51302|4248|27611|15296|43166|5359|5981|4026|3985|25359|17686|3836|17087|5356|29030|24434|22336|29036|3843|29025|51098|29034|8826|51094|576|3827|22326|45315|3891|2539|39898|39896|39892|39918|39911|39916|39897|39905|39891|39917|39912|39909|39889|31047|31017|31049|31019|31055|31051|31053|31025|31027|31031|31041|54717|31021|31011|31023|31029|31043|31003|31035|31033|31045|31037|31002|20006|31169|31006|30999|31012|31009|30996|7109|7107|31128|31188|20014|31014|31004|31142|31001|31000|37181|31010|30998|31172|30988|30997|31038|31141|20008|31005|31191|31008|31007|30994|30993|31182|30989|20012|31186|31178|31015|30991|20010|20016|31184|31190|30990|30995|31176|31180|30992|31130|5207|7031|7026|7034|7033|7027|7035|7029|5078|5063|5059|5105|5060|5138|5129|5201|5142|5143|5076|5106|7610|5139|5090|5103|5132|7177|5061|7181|7179|3920|5042|5127|5117|5185|5125|3938|5177|3937|5044|5121|3915|3909|3568|3932|5123|5144|5119|5056|5135|5187|5074|5089|3934|3913|3917|3721|5075|5046|5049|7170|5146|5205|5203|5115|5204|5040|5179|5070|5182|5085|5181|3719|7173|5131|3911|5101|7175|7169|3713|5112|22187|9917|3859|3862|3878|3865|3856|3873|3877|3834|3742|3762|3740|3763|10245|3572|3573|3571|3885|7127|4225|12686|19985|19981|19984|19982|19983|19991|19989|19986|19987|19988|19994|19995|19992|19993|19990|19980|19977|19976|19978|7154|19979|43423|7154|4225|7127|3861|24343" ground -1 -1 2
                break
            endif
            if skill "Stealth" >= 80 and hidden and stam = maxstam and varexist "bwiz" and find "bwiz" backpack and find "bmain" self
                break
            endif
            if diffweight >= 8 and findtype "3821" ground -1 -1 2
                break
            endif
            if index > 0 and findtype "chain link|37477|29368|18660|20099|31303|31165|tribal ruins|electric ankh|54230|44985|glass display|20049|Pig Iron|blackrock fragment|gargish fragment|11753|18705|55247|49482|53694|54570|53696|53876|50612|55420|53280|53473|54831|50278|49528|bottle set|entrail bucket|mounted stalaghorn|sea glass bulb|juggling daggers|st patricks cross|divination orb|mirror display|murder of crows|charmed cobra|prodigy fiddle|wilting rose belljar|scale of balance|screaming mandrake|statue of the siren|the final hour|55204|specimen jars|bird cage|widows grief|54684|27612|49223|54591|54601|54718|19948|58325|19959|19822|49730|49729|19944|19949|47613|47612|47611|47610|47611|3986|3966|cathedral tapestry|Darkscale tapestry|large painting|landscape painting|portrait painting|figurine|statue|20291|44987|49730|47107|18653|45127|20103|47103|29363|47105|29361|43453|5899|47109|45250|41509|43447|18657|4025|51308|45156|51304|45214|28769|28799|28803|28765|45211|45248|51259|45220|51328|45218|3842|51327|45222|45241|51336|11858|51375|45282|54722|45255|45246|28795|44983|45235|45251|45238|51258|51314|28761|45236|51260|54718|18400|58325|45252|18656|47111|3839|45239|45242|45254|45281|45247|42241|45216|28775|51321|3838|51318|5901|5905|45259|5903|48407|51302|4248|27611|15296|43166|5359|5981|4026|3985|25359|17686|3836|17087|5356|29030|24434|22336|29036|3843|29025|51098|29034|8826|51094|576|3827|22326|45315|3891|2539|39898|39896|39892|39918|39911|39916|39897|39905|39891|39917|39912|39909|39889|31047|31017|31049|31019|31055|31051|31053|31025|31027|31031|31041|54717|31021|31011|31023|31029|31043|31003|31035|31033|31045|31037|31002|20006|31169|31006|30999|31012|31009|30996|7109|7107|31128|31188|20014|31014|31004|31142|31001|31000|37181|31010|30998|31172|30988|30997|31038|31141|20008|31005|31191|31008|31007|30994|30993|31182|30989|20012|31186|31178|31015|30991|20010|20016|31184|31190|30990|30995|31176|31180|30992|31130|5207|7031|7026|7034|7033|7027|7035|7029|5078|5063|5059|5105|5060|5138|5129|5201|5142|5143|5076|5106|7610|5139|5090|5103|5132|7177|5061|7181|7179|3920|5042|5127|5117|5185|5125|3938|5177|3937|5044|5121|3915|3909|3568|3932|5123|5144|5119|5056|5135|5187|5074|5089|3934|3913|3917|3721|5075|5046|5049|7170|5146|5205|5203|5115|5204|5040|5179|5070|5182|5085|5181|3719|7173|5131|3911|5101|7175|7169|3713|5112|22187|9917|3859|3862|3878|3865|3856|3873|3877|3834|3742|3762|3740|3763|10245|3572|3573|3571|3885|12686|19985|19981|19984|19982|19983|19991|19989|19986|19987|19988|19994|19995|19992|19993|19990|19980|19977|19976|19978|19979|43423|7154|4225|7127|3861|24343" ground -1 -1 index as lootlocation
                overhead "*loot*" 116 lootlocation
                @ignore lootlocation
            endif
            if diffweight >= 8 and index > 0 and findtype "3821" ground -1 -1 index as lootlocation
                overhead "*gold*" 2091 lootlocation
                @ignore lootlocation
            endif
            if warmode and findtype 4239|4240 ground -1 -1 2
                break
            endif
        endfor
        @clearignore 
        settimer lootsensetimer 0
    endif
    while findtype 3827 ground 0 as zeroscroll
        @ignore zeroscroll
    endwhile
    while findtype 4225 ground 0 as zeroleather
        @ignore zeroleather
    endwhile
    while findtype 7127 ground 0 as zeroboard
        @ignore zeroboard
    endwhile
    while not casting and not targetexists and findtype "chain link|37477|29368|18660|20099|31303|31165|tribal ruins|electric ankh|54230|44985|glass display|20049|Pig Iron|blackrock fragment|gargish fragment|11753|18705|55247|49482|53694|54570|53696|53876|50612|55420|53280|53473|54831|50278|49528|bottle set|entrail bucket|mounted stalaghorn|sea glass bulb|juggling daggers|st patricks cross|divination orb|mirror display|murder of crows|charmed cobra|prodigy fiddle|wilting rose belljar|scale of balance|screaming mandrake|statue of the siren|the final hour|55204|specimen jars|bird cage|widows grief|54684|27612|49223|54591|54601|54718|19948|58325|19959|19822|49730|49729|19944|19949|47613|47612|47611|47610|47611|3986|3966|cathedral tapestry|Darkscale tapestry|large painting|landscape painting|portrait painting|figurine|statue|20291|44987|49730|47107|18653|45127|20103|47103|29363|47105|29361|43453|5899|47109|45250|41509|43447|18657|4025|51308|45156|51304|45214|28769|28799|28803|28765|45211|45248|51259|45220|51328|45218|3842|51327|45222|45241|51336|11858|51375|45282|54722|45255|45246|28795|44983|45235|45251|45238|51258|51314|28761|45236|51260|54718|18400|58325|45252|18656|47111|3839|45239|45242|45254|45281|45247|42241|45216|28775|51321|3838|51318|5901|5905|45259|5903|48407|51302|4248|27611|15296|43166|5359|5981|4026|3985|25359|17686|3836|17087|5356|29030|24434|22336|29036|3843|29025|51098|29034|8826|51094|576|3827|22326|45315|3891|2539|39898|39896|39892|39918|39911|39916|39897|39905|39891|39917|39912|39909|39889|31047|31017|31049|31019|31055|31051|31053|31025|31027|31031|31041|54717|31021|31011|31023|31029|31043|31003|31035|31033|31045|31037|31002|20006|31169|31006|30999|31012|31009|30996|7109|7107|31128|31188|20014|31014|31004|31142|31001|31000|37181|31010|30998|31172|30988|30997|31038|31141|20008|31005|31191|31008|31007|30994|30993|31182|30989|20012|31186|31178|31015|30991|20010|20016|31184|31190|30990|30995|31176|31180|30992|31130|5207|7031|7026|7034|7033|7027|7035|7029|5078|5063|5059|5105|5060|5138|5129|5201|5142|5143|5076|5106|7610|5139|5090|5103|5132|7177|5061|7181|7179|3920|5042|5127|5117|5185|5125|3938|5177|3937|5044|5121|3915|3909|3568|3932|5123|5144|5119|5056|5135|5187|5074|5089|3934|3913|3917|3721|5075|5046|5049|7170|5146|5205|5203|5115|5204|5040|5179|5070|5182|5085|5181|3719|7173|5131|3911|5101|7175|7169|3713|5112|22187|9917|3859|3862|3878|3865|3856|3873|3877|3834|3742|3762|3740|3763|10245|3572|3573|3571|3885|12686|19985|19981|19984|19982|19983|19991|19989|19986|19987|19988|19994|19995|19992|19993|19990|19980|19977|19976|19978|19979|43423|7154|4225|7127|3861|24343" ground -1 -1 2 as eatloot
        hotkey 'Grab Item' 
        target eatloot
        if insysmsg "Invalid or inaccessible item."
            @ignore eatloot
        endif            
        while queued
            //donothing
        endwhile 
    endwhile
    while not casting and not targetexists and diffweight >= 8 and findtype 3821 ground -1 -1 2 as goldcoinz
        hotkey 'Grab Item' 
        target goldcoinz
        while queued
            //donothing
        endwhile 
    endwhile
endif
// End of Loot Vacuum Routine

// Stealth Wiz Hat Routine
if findtype 5912 self and not varexist "bwiz" and findlayer self head as regularhat
    getlabel regularhat desc
    if "magical" in desc
        dclick regularhat
        pause 500
        getlabel backpack ping
        replay
    endif
endif
if not find "bwiz" self and findtype 5912 backpack as wizhat
    getlabel wizhat desc
    if "restyled" in desc
        @ignore wizhat
        replay
    elseif "armored" in desc
        @setvar! "bwiz" wizhat
    elseif "magical" in desc
        @setvar! "bwiz" wizhat
        @setvar! "nonaspectedwiz" wizhat
    endif
endif
if varexist "bwiz" and find "bwiz" backpack
    if not find "bmain" self and findlayer self head as hat
        @setvar! "bmain" hat
    endif
    if not varexist "bmain"
        overhead "I need a primary hat or helm to wear.." 88
        pause 2000
        overhead "Anything will do, just not a magical wizard hat..." 88
        stop
    endif
endif
if varexist "bwiz" and find "bwiz" backpack and find "bmain" self and skill "Stealth" >= 80
    if not targetexists and not casting and hidden and stam = maxstam
        while queued
            //donothing
        endwhile
        dclick "bwiz"
        while queued
            //donothing
        endwhile
        pause 30
        dclick "bmain"
        pause 500
        getlabel backpack ping
    endif
    if not targetexists and not casting and not findlayer self head and find "bmain" self
        while queued
            //donothing
        endwhile
        dclick "bmain"
        pause 500
        getlabel backpack ping
    endif
endif
if varexist "bwiz" and find "bmain" self and skill "Stealth" >= 80
    if not targetexists and not casting and not find "bwiz" backpack
        while queued
            //donothing
        endwhile
        dclick "bmain"
        pause 500
        getlabel backpack ping
    endif
endif
// End of Stealth Wiz Hat Routine

// Gold Sacker Routine
if not timerexists goldsacker
    if gumpexists 341416395
        gumpclose 341416395
        createtimer goldsacker
        overhead "--Gold Sacker Enabled--" 2085
        hotkey "Set Last Target"
        wft 500
        overhead "Target Gold Drop Bag" 88
        while targetexists 
            //donothing
        endwhile
        @setvar "bgoldbag" lasttarget 
        overhead "Gold Drop Bag Set" 78
    endif
elseif timerexists goldsacker
    if gumpexists 341416395
        gumpclose 341416395
        removetimer goldsacker
        overhead "--Gold Sacker Disabled--" 1779
    endif
endif
if not timerexists sacktracker
    createtimer sacktracker
    settimer sacktracker 5000
elseif timer sacktracker >= 5000 and find "bgoldbag" ground
    overhead "*gold sack*" 2843 "bgoldbag"
    settimer sacktracker 0
endif
if find "bgoldbag" ground -1 -1 2
    while findtype "gold coin" backpack as fatcoins
        while queued
            //donothing
        endwhile
        drop backpack
        lift fatcoins 60000
        drop bgoldbag -1 -1
        pause 500
        getlabel backpack ping
    endwhile
endif
// End of Gold Sacker Routine

if dead 
    overhead "I need a new body..." 88
    stop
endif
loop