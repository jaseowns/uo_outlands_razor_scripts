# This is an automated backup - check out https://outlands.uorazorscripts.com/script/3a9c222c-c1a6-4f30-ac10-c57bbd930c4d for latest
# Automation by Jaseowns
## Script: ItemID Public/Private bot
## Created by: nesci0471#0
#############################################

###
# Based on DaKlue and JaseOwns itemId scripts
# Check on the ground for a container, grabs it, identify all the items inside, recycle the unwanted item, put the arcane essence the the container and drop bag the container on the ground
# Can either be a public or private hosted bot (Public returns the identified filtered items with the original bag)
# TODO: check for 120 itemId skill
###

# set to 1 to host a public bot (returns the filtered item in the bag)
@setvar! is_bot_public 0

# cooldowns and queue times
@setvar! global_timeout 5000
@setvar! global_queue_time 1000
@setvar! global_wait 650
@setvar! global_wait_for_target 500

# accuracies
@setvar! supremely 1
@setvar! exceedingly 1
@setvar! eminently 0
@setvar! surpassingly 0
@setvar! accurate 0

# damages
@setvar! vanquishing 1
@setvar! power 1
@setvar! force 0
@setvar! might 0
@setvar! ruin 0

# slaying
@setvar! slaying 1

# protections
@setvar! invulnerability 1
@setvar! fortification 1
@setvar! hardening 0
@setvar! guarding 0
@setvar! defense 0

if skill "Item Identification" < 100
    overhead "Requires item ID skill to continue.." 34
    stop
endif

if not findtype 4032|4032|3997|7864|4148|4136 backpack
    overhead "Jase says: Not able to recycle items.." 34
    overhead "Jase says: Need tinker tool, sewing kit or saw" 77
    stop
endif

# this is your save box
if is_bot_public = 0 and not varexist good_container
    overhead 'Please select your chest to keep good items.' 88
    @setvar! good_container
    wft global_wait_for_target
endif

# this is for a resource container where you store ingot leather scrolls ect...
if not varexist resource_container
    overhead 'Please select your chest where you store your resources.' 88
    @setvar! resource_container
    wft global_wait_for_target
endif

if not listexists id_weapon_items
    createlist id_weapon_items
    pushlist id_weapon_items "martial arts scroll"
    pushlist id_weapon_items "halberd"
    pushlist id_weapon_items "bardiche"
    pushlist id_weapon_items "battle axe"
    pushlist id_weapon_items "double axe"
    pushlist id_weapon_items "two handed axe"
    pushlist id_weapon_items "executioner's axe"
    pushlist id_weapon_items "large battle axe"
    pushlist id_weapon_items "axe"
    pushlist id_weapon_items "scimitar"
    pushlist id_weapon_items "longsword"
    pushlist id_weapon_items "viking sword"
    pushlist id_weapon_items "katana"
    pushlist id_weapon_items "cutlass"
    pushlist id_weapon_items "broadsword"
    pushlist id_weapon_items "war hammer"
    pushlist id_weapon_items "war axe"
    pushlist id_weapon_items "maul"
    pushlist id_weapon_items "quarter staff"
    pushlist id_weapon_items "gnarled staff"
    pushlist id_weapon_items "black staff"
    pushlist id_weapon_items "hammer pick"
    pushlist id_weapon_items "club"
    pushlist id_weapon_items "shepherd's crook"
    pushlist id_weapon_items "war mace"
    pushlist id_weapon_items "mace"
    pushlist id_weapon_items "Long Spear"
    pushlist id_weapon_items "pitchfork"
    pushlist id_weapon_items "war fork"
    pushlist id_weapon_items "kryss"
    pushlist id_weapon_items "short spear"
    pushlist id_weapon_items "bow"
    pushlist id_weapon_items "crossbow"
    pushlist id_weapon_items "heavy crossbow"
    pushlist id_weapon_items "bladed staff"
    pushlist id_weapon_items "harpoon"
endif

if not listexists id_armor_items
    createlist id_armor_items
    pushlist id_armor_items "leather tunic"
    pushlist id_armor_items "leather leggings"
    pushlist id_armor_items "leather sleeves"
    pushlist id_armor_items "leather cap"
    pushlist id_armor_items "leather gloves"
    pushlist id_armor_items "leather gorget"
    pushlist id_armor_items "leather armor"
    pushlist id_armor_items "leather bustier"
    pushlist id_armor_items "leather shorts"
    pushlist id_armor_items "leather skirt"
    pushlist id_armor_items "studded tunic"
    pushlist id_armor_items "studded leggings"
    pushlist id_armor_items "studded sleeves"
    pushlist id_armor_items "studded gloves"
    pushlist id_armor_items "studded gorget"
    pushlist id_armor_items "studded armor"
    pushlist id_armor_items "studded bustier"
    pushlist id_armor_items "bone armor"
    pushlist id_armor_items "bone legs"
    pushlist id_armor_items "bone arms"
    pushlist id_armor_items "bone helmet"
    pushlist id_armor_items "bone gloves"
    pushlist id_armor_items "ringmail tunic"
    pushlist id_armor_items "ringmail leggings"
    pushlist id_armor_items "ringmail sleeves"
    pushlist id_armor_items "helmet"
    pushlist id_armor_items "ringmail gloves"
    pushlist id_armor_items "orc helm"
    pushlist id_armor_items "chainmail tunic"
    pushlist id_armor_items "chainmail leggings"
    pushlist id_armor_items "chainmail coif"
    pushlist id_armor_items "platemail"
    pushlist id_armor_items "platemail legs"
    pushlist id_armor_items "platemail arms"
    pushlist id_armor_items "plate helm"
    pushlist id_armor_items "platemail gloves"
    pushlist id_armor_items "platemail gorget"
    pushlist id_armor_items "plate armor"
    pushlist id_armor_items "bascinet"
    pushlist id_armor_items "close helm"
    pushlist id_armor_items "norse helm"
    pushlist id_armor_items "wooden shield"
    pushlist id_armor_items "kite shield"
    pushlist id_armor_items "buckler "
    pushlist id_armor_items "metal shield"
    pushlist id_armor_items "bronze shield"
    pushlist id_armor_items "kite shield"
    pushlist id_armor_items "heater shield"
    pushlist id_armor_items "Chaos shield"
    pushlist id_armor_items "Order shield"
endif

if not listexists id_bard_items
    createlist id_bard_items
    pushlist id_bard_items "lap harp"
    pushlist id_bard_items "drum"
    pushlist id_bard_items "lute"
    pushlist id_bard_items "bamboo flute"
    pushlist id_bard_items "tambourine"
endif

if not listexists id_mage_weapon_items
    createlist id_mage_weapon_items
    pushlist id_mage_weapon_items "spellbook"
endif

if not listexists id_items
    createlist id_items
    foreach id_weapon_item in id_weapon_items
        pushlist id_items id_weapon_item
    endfor

    foreach id_armor_item in id_armor_items
        pushlist id_items id_armor_item
    endfor

    foreach id_bard_item in id_bard_items
        pushlist id_items id_bard_item
    endfor

    foreach id_mage_weapon_item in id_mage_weapon_items
        pushlist id_items id_mage_weapon_item
    endfor
endif

if not listexists keep_item_prefixes
    createlist keep_item_prefixes
    if slaying = 1
        pushlist keep_item_prefixes "slaying"
    endif
    if supremely = 1
        pushlist keep_item_prefixes "supremely"
    endif
    if exceedingly = 1
        pushlist keep_item_prefixes "exceedingly"
    endif
    if eminently = 1
        pushlist keep_item_prefixes "eminently"
    endif
    if surpassingly = 1
        pushlist keep_item_prefixes "surpassingly"
    endif
    if accurate = 1
        pushlist keep_item_prefixes "accurate"
    endif
    if vanquishing = 1
        pushlist keep_item_prefixes "vanquishing"
    endif
    if power = 1
        pushlist keep_item_prefixes "power"
    endif
    if force = 1
        pushlist keep_item_prefixes "force"
    endif
    if might = 1
        pushlist keep_item_prefixes "might"
    endif
    if ruin = 1
        pushlist keep_item_prefixes "ruin"
    endif
endif

@clearignore
@setvar! last_traded_bag 0
while not dead
    if findtype 3705|3702|3701 ground as bag
        if last_traded_bag != bag
            @clearignore
            @setvar! last_traded_bag bag
            overhead "Found bag to ID"
            lift bag 60000
            wait global_wait
            drop self
            wait global_wait
            dclick bag
            wait global_wait

            foreach id_item in id_items
                while findtype id_item bag as item
                    getlabel item desc

                    if "unidentified" in desc
                        useskill "itemId"
                        wft global_wait_for_target
                        target item
                        wait global_wait
                    endif

                    getlabel item desc
                    @setvar! save_item 0
                    @setvar! ignore_item 1

                    foreach keep_item_prefix in keep_item_prefixes
                        if keep_item_prefix in desc
                            overhead keep_item_prefix 77
                            @setvar! save_item 1
                        endif
                    endfor

                    if save_item = 1
                        @setvar! ignore_item 0
                        if is_bot_public = 0
                            overhead "Saving: {{desc}}" 77
                        else
                            say "Saving: {{desc}}"
                        endif
                        wait global_timeout
                        lift item
                        wait global_queue_time
                        if is_bot_public = 0
                            drop good_container -1 -1 -1
                        else
                            drop bag -1 -1 -1
                        endif

                        wait global_timeout
                        @ignore item
                    endif

                    if ignore_item = 1
                        lift item
                        wait global_queue_time
                        drop backpack -1 -1 -1
                        wait global_queue_time
                        if is_bot_public = 0
                            overhead "Recycleing: {{desc}}" 77
                        else
                            say "Recycleing: {{desc}}"
                        endif
                        if find item backpack
                            if not gumpexists 949095101
                                if findtype 4032|3997|7864|4148|4136 backpack as recycler_item_kit
                                    dclick recycler_item_kit
                                    waitforgump 949095101 5000
                                endif
                            endif
                            if gumpexists 949095101
                                gumpresponse 3 949095101
                                wft global_wait_for_target
                                target item
                            endif
                        endif
                        @ignore item
                    endif
                endwhile
            endfor
        endif
        wait global_wait
        
        if findtype 12686 backpack as arcane_essence
            lift arcane_essence 60000
            wait global_wait
            drop bag
        endif
        
        lift bag
        wait global_wait
        droprelloc 1 1
        wait global_timeout
    else
        @setvar! last_traded_bag 0
        while findtype 7154|3827|4225|7127 backpack as material
            lift material 60000
            wait global_wait
            drop resource_container -1 -1 -1
            ignore material
        endwhile
    endif

    wait global_wait
endwhile