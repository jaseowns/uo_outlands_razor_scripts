# This is an automated backup - check out https://outlands.uorazorscripts.com/script/0c71f1c7-6608-4cce-9bd0-9984e049057d for latest
# Automation by Jaseowns
## Script: Anti-theft looting box
## Created by: dennozz#0
#############################################

#Anti-theft looting box ver.2.00

#********** INFORMATION **********# 
#Made by @dennozz on discord. 

#If you have any questions, suggestions or concerns please message me via discord
#Inspired/learned by other scrips in https://outlands.uorazorscripts.com/
    
#IMPORTANT: You need 1 wooden box and 1 copper key (you can craft it with 50 carpentry and 50 tinkering)

#This script will auto store all your loot into a wooden box and lock it.
#This script already stores a bunch of valuable items like cores, ss, research mats, etc.

#OPTIONAL : If you want to add more items modify the line code #45 and #51 -> `while findtype 1234|0000|0000` 
    #       COPY and PASTE the item ID you wish to add to the beggining of the list

#Update: ver.2.00 now stores item that are inside a `loot bag`

clearignore 

@setvar! maxwaittimeinms 650

@setvar! messageCD 2800
@settimer! messageTimer messageCD

while weight > maxweight
    if findtype "gold coin" backpack as gold
        lift gold 500
        droprelloc 0 0
        getlabel backpack xxx
        wait 200
        if insysmsg "cannot drop anything"
            drop backpack -1 -1 -1
        endif
        wait maxwaittimeinms
        overhead "* gold dropped *" 55
    endif
endwhile

if findtype 'wooden box' backpack as box
    if findtype 'copper key' backpack as key

#------ ADD more items below here ------# v
    
    while findtype 4025|11858|20309|37181|27612|29030|576|2472|2475|2594|2597|2744|2750|2760|2771|2778|2799|3648|3649|3650|3651|3708|3712|3735|3736|3737|3738|3827|3836|3838|3839|3842|3843|3985|4026|4248|5356|5359|5981|8826|12686|15296|15297|17087|17686|19717|22244|22336|23996|23997|24343|24434|29025|29036|29040|29832|29833|43166|43179|43206|48405|48407 box as boxItem
        @ignore boxItem            
    endwhile
    
#------ ADD more items below here ------# v

        while findtype 4025|11858|20309|37181|27612|29030|576|2472|2475|2594|2597|2744|2750|2760|2771|2778|2799|3648|3649|3650|3651|3708|3712|3735|3736|3737|3738|3827|3836|3838|3839|3842|3843|3985|4026|4248|5356|5359|5981|8826|12686|15296|15297|17087|17686|19717|22244|22336|23996|23997|24343|24434|29025|29036|29040|29832|29833|43166|43179|43206|48405|48407 backpack as backpackItem
            getlabel backpackItem backpackItemLabel
            
            if "blessed" in backpackItemLabel
                #skip
                @ignore backpackItem
            else                              
                while not dead 
                    getlabel box boxLabelBefore 
                    if "items" in boxLabelBefore
                        sysmsg "Break : box is open" 11 
                        break
                    else
                        dclick key
                        getlabel backpack xxx
                        wait 325
                        target box
                        getlabel backpack xxx
                        wait 325
                        overhead "[Box Opened]" 44     
                    endif
                endwhile
                
                if timer messageTimer > messageCD
                    overhead "moving item..." 11
                    settimer messageTimer 0
                endif             
                         
                lift backpackItem 1000
                drop box
                getlabel backpack xxx    
                wait maxwaittimeinms
                
                @ignore backpackItem
                
            endif
            
        endwhile

        getlabel box boxLabel

        while "items" in boxLabel
            dclick key
            getlabel backpack xxx
            wait 325
            target box
            getlabel backpack xxx
            wait 325 
            getlabel box boxLabel
        endwhile 

        overhead "[Box Closed]" 65 
    else
        overhead "no copper key found" 34
        wait maxwaittimeinms
    endif
else 
    overhead "no wooden box found" 34
    wait maxwaittimeinms
endif

removetimer messageTimer