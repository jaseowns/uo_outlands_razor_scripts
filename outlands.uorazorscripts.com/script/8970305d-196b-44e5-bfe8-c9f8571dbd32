# This is an automated backup - check out https://outlands.uorazorscripts.com/script/8970305d-196b-44e5-bfe8-c9f8571dbd32 for latest
# Automation by Jaseowns
## Script: Boating Navigation
## Created by: yarick.#0
#############################################

#You need to create 10 scripts which call the BoatDirection script
#Example script for North which i set to keypad 9

#setvar desiredDirection 1
#hotkey 'Play Script: AA\BoatDirection'


####################################################################
#Below script for me is in the AA Category and called BoatDirection
####################################################################
#Script assumes boat starts facing north
#script does NOT handle failing to turn so i advise you have some buttons to recover from this

if not varexist boatDirection
    overhead 'Using Default Start'
    //can only be N1 E3 S5 W7
    setvar boatDirection 1
endif

if not varexist desiredDirection
    overhead 'missing direction'
elseif desiredDirection = 0
    say '[Stop'
elseif desiredDirection = 9
    if boatDirection = 1
        setvar boatDirection 3
    elseif boatDirection = 3
        setvar boatDirection 5
    elseif boatDirection = 5
        setvar boatDirection 7
    elseif boatDirection = 7
        setvar boatDirection 1
    endif
    say '[TurnRight'
else
    #Turn boat so it matches desired direction
    if boatDirection = 1
        if desiredDirection = 5
            say '[TurnRight'
            wait 600
            say '[TurnRight'
            setvar boatDirection 5
            wait 600
        elseif desiredDirection = 3 or desiredDirection = 4 
            say '[TurnRight'
            setvar boatDirection 3
            wait 600
        elseif desiredDirection = 6 or desiredDirection = 7 
            say '[TurnLeft'
            setvar boatDirection 7
            wait 600
        endif
    elseif boatDirection = 3
        if desiredDirection = 7
            say '[TurnRight'
            wait 600
            say '[TurnRight'
            setvar boatDirection 7
            wait 600
        elseif desiredDirection = 5 or desiredDirection = 6 
            say '[TurnRight'
            setvar boatDirection 5
            wait 600
        elseif desiredDirection = 8 or desiredDirection = 1 
            say '[TurnLeft'
            setvar boatDirection 1
            wait 600
        endif
    elseif boatDirection = 5
        if desiredDirection = 1
            say '[TurnRight'
            wait 600
            say '[TurnRight'
            setvar boatDirection 1
            wait 600
        elseif desiredDirection = 7 or desiredDirection = 8 
            say '[TurnRight'
            setvar boatDirection 7
            wait 600
        elseif desiredDirection = 2 or desiredDirection = 3 
            say '[TurnLeft'
            setvar boatDirection 3
            wait 600
        endif
    elseif boatDirection = 7
        if desiredDirection = 3
            #spin
            say '[TurnRight'
            wait 600
            say '[TurnRight'
            setvar boatDirection 3
            wait 600
        elseif desiredDirection = 1 or desiredDirection = 2 
            say '[TurnRight'
            setvar boatDirection 1
            wait 600
        elseif desiredDirection = 5 or desiredDirection = 4 
            say '[TurnLeft'
            setvar boatDirection 5
            wait 600
        endif
    endif

    #go forward
    if desiredDirection = 1 or desiredDirection = 3 or desiredDirection = 5 or desiredDirection = 7
        say '[Forward'
    elseif boatDirection = 1 and desiredDirection = 2
        say '[ForwardRight'
    elseif boatDirection = 3 and desiredDirection = 2
        say '[ForwardLeft'
    elseif boatDirection = 3 and desiredDirection = 4
        say '[ForwardRight'
    elseif boatDirection = 5 and desiredDirection = 4
        say '[ForwardLeft'
    elseif boatDirection = 5 and desiredDirection = 6
        say '[ForwardRight'
    elseif boatDirection = 7 and desiredDirection = 6
        say '[ForwardLeft'
    elseif boatDirection = 7 and desiredDirection = 8
        say '[ForwardRight'
    elseif boatDirection = 1 and desiredDirection = 8
        say '[ForwardLeft'
    endif
endif

#Set this to your loop script to resume your loop
#hotkey 'Play Script: AA\Route'